/// Definition of the objects surrounding the human.
AnyFolder EnvironmentModel = {
  
  // Global Reference Frame
  AnyFixedRefFrame GlobalRef = {
    AnyRefNode Hpoint = {
      sRel = ...Hpoint;
    };
    AnyRefNode LeftFoot = {
      sRel = {-0.25, -0.15, -0.20};
    };    AnyDrawRefFrame drw = {};
  };  // Global reference frame
  
  AnySeg Pedal = {
    r0 = {-0.15, -4.996653e-017, 0};
    Axes0 = {{-4.440892e-016, -1, 0}, {1, -4.440892e-016, 0}, {0, 0, 1}};
    Mass = 2;
    Jii = {0.05, 0.001, 0.05};
    AnyRefNode Hinge = {
      sRel = {0, -0.15, 0};
    };
    AnyRefNode FootNode = {
      sRel = {0.02, 0.15, 0};
    };
    AnyDrawSeg drw = {};
  };
  
  AnyRevoluteJoint HingeJoint = {
    Axis = z;
    AnyFixedRefFrame &Ground = .GlobalRef;
    AnyRefNode &Pedal = .Pedal.Hinge;
  };
  
  AnyForce Spring = {
    AnyRevoluteJoint &Hinge = .HingeJoint;
    F = -..SpringStiff*.HingeJoint.Pos;
  };
};
