// Example of calculating the medial/lateral knee force
//
// Most muscluloskeltal models uses a simplified knee joint (revolute joint). But we can still estimate
// what the medial and lateral femoral condyle contact forces are on the tibial plateau. 
// 
// In this example we show how to find the knee reaction forces and moments and decompose them into 
// two equivalent forces which represent the medial and lateral knee force components acting from the 
// femur to the tibial tray.
//  
// The model example contains a number of code comments which explains how this is done.
//
#include "../libdef.any"




// First we include one of the example applications so we don't have to create a full model here. 
#include "<ANYBODY_PATH_AMMR>/Application/Examples/Squat/Squat.main.any"

// The code below could be in any application.
Main = {
  DrawSettings.Muscle.Opacity = 0.1;
  DrawSettings.BonesOpacity.RightThigh = 0.3;
  DrawSettings.BonesOpacity.RightPatella = 0.3;
  
  // We add our code to the study folder, so we are sure it is included in the analysis.
  Study = {
    
    AnyFolder KneeForces = {
      
      // Some references for convenience and less typing. 
      AnyFolder& LegRef = Main.HumanModel.BodyModel.Right.Leg;
      AnyFolder& GlobalRef = Main.Model.Environment.GlobalRef;
      
      // We can get the forces/moments in the knee joint by finding the Contraints.Reactions object in the joint.
      // F[0] or F[1] are the same force vector but with different signs. Depends if we want the force femur inserts on tibia or opposite.
      // Here we chose [0] which is the force the shank inserts on the femur.
      AnyVec3 KneeForceInGlobal = LegRef.Jnt.Knee.Constraints.Reaction.RefFrameOutput.F[0];
      AnyVec3 KneeMomentInGlobal = LegRef.Jnt.Knee.Constraints.Reaction.RefFrameOutput.M[0];
      
      // We can now express the force and moments in the coordinate system we want. 
      AnyVec3 KneeForceInShank = transf3D(KneeForceInGlobal, &GlobalRef, &LegRef.Seg.Shank.KneeJoint);
      AnyVec3 KneeMomentInShank = transf3D(KneeMomentInGlobal, &GlobalRef, &LegRef.Seg.Shank.KneeJoint);
      
      // Note this transformation is most easily done with the transf3D function but the same could have been achieved 
      // by simply multiplying the 'Axes' variable (rotation matrix) of the Shank knee joint coordinate system.
      // E.i.
      // AnyVec3 KneeForceInShank = (LegRef.Seg.Shank.KneeJoint.Axes'*KneeForceInGlobal')';
      
      
      // We can now calculate two equivalent condyle compression forces which 
      // generates the same total force and moment. We can imagine this as the 2 forces in the frontal 
      // plane which representas the force from the tibial platau on the femoral condyles. 
      // The two components are placed at somme displacement (L_medial/L_lateral) from the knee center. 
      AnyVar L_medial = 0.03;
      AnyVar L_lateral = 0.03;

      // Here F_total is the vertical component of the force and M_total is the abbduction moment in the frontal plane.
      AnyVar F_total = KneeForceInShank[1];
      AnyVar M_total = KneeMomentInShank[0];
      
      // The relationships with the lateral/medial forces can be expressed as:
      // 
      //  F_total = F_medial + F_lateral
      //  M_total = F_medial*L_medial - F_lateral*L_lateral
      // 
      // From these the F_medial/F_lateral can be found:
      
      AnyFloat F_lateral = (F_total * L_medial - M_total) / (L_lateral + L_medial);
      AnyFloat F_medial = F_total - F_lateral;
      
      
      
      // Finally let us visualize the forces and the knee axis:
      // First we create the nodes at some medial/lateral distance 
      // along the knee axis. Inside the nodes we create small
      // vectors representing the forces. 
      
      LegRef.Seg.Shank = {
        AnyRefNode LateralKneeContactNode = {
          AnyVec3 uKneeAxis = {0,0,1}*ARel';
          sRel = .KneeJoint.sRel + ....L_lateral *uKneeAxis;
          ARel = .KneeJoint.ARel;
          AnyDrawVector LateralForceVec = {
            // We scale the force so the vector gets a resonable length
            Vec = {0,.....F_lateral, 0}/5000;
            GlobalCoord = Off;
            Line.End.Style = Line3DCapStyleArrow;
          };          
        };
        AnyRefNode MedialKneeContactNode = {
          AnyVec3 uKneeAxis = {0,0,1}*.KneeJoint.ARel';
          sRel = .KneeJoint.sRel - ....L_medial *uKneeAxis;
          ARel = .KneeJoint.ARel;
          AnyDrawVector MedialForceVec = {
            Vec = {0,.....F_medial, 0}/5000;
            GlobalCoord = Off;
            Line.End.Style = Line3DCapStyleArrow;
          };
        }; 
        KneeJoint = {
          AnyDrawLine DrawKneeJointAxis = {
            p0 = {0,0,-0.06};
            p1 = {0,0, 0.06};
            Line.RGB = 0.1*{1,1,1};
            Opacity = 0.3;
            GlobalCoord = Off;
          };
        };
      };
    };   
  };
};
