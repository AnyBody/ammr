#include "../libdef.any"

/**
This model shows a model of a push up

The model displays several topics
-Block building techniques
-Include files
-Structuring of models using directories

The positions of the hands can be controlled by chaning variables in the file
Environment.any

Known issues:
1: Performing pushups is strenuous for both the model and most real humans.
With the current settings, the model's muscle activity reaches 1.0,
meaning some muscles operate at their maximum strength capacity.
There is still some reserve in the overall muscle collaboration,
but small changes in movement parameters, such as increasing speed,
can easily exceed the model's capacity.
*/
Main = {
  // ----------------------------------------------------------
  // Path to draw settings
  // ----------------------------------------------------------
  #include "Model/DrawSettings.any"

  // ----------------------------------------------------------
  // Body Model Configuration
  // ----------------------------------------------------------
  #include "Model/BodyModelConfiguration.any"

  // The Mannequin file specifies load-time positions for all the segments
  // in the HumanModel. This is important for the model's ablity to resolve
  // all the kinematic constraints when the model is analyzed.
  // The mannequin file also drives those degrees of freedom of the human
  // model that are not governed by problem-specific drivers at run time.
  #include "Model/Mannequin.any"

  // Model of the human body to be used for the analysis
  #include "<ANYBODY_PATH_BODY>/HumanModel.any"

  /// The actual model where all components are assembled
  AnyFolder Model = {
    AnyVar Time ??= 1.5;
    AnyVar ChestHeightTop ??= 0.47;
    AnyVar ChestHeightBottom ??= 0.02;

    /// Body model without default drivers
    AnyFolder &BodyModel=.HumanModel.BodyModel;
    /// Reference to the mannequin folder (used by drivers)
    AnyFolder &Mannequin =.HumanModel.Mannequin;

    #include  "Model/JointsAndDrivers.any"
    #include  "Model/Environment.any"

  };



  //The study: Operations to be performed on the model
  AnyBodyStudy Study = {

    //This file set the position and orientation of the segments in the model
    AnyFolder &Model = .Model;

    Gravity = {0.0,-9.81,0.0};
    tStart = 0;
    tEnd = Model.Time;
    nStep = 21;
    //This tolerances determine how aggressively the muscles will be removed from the system
    //if there is symmetry problems this value should be lowered

    // Include functions to generate a video:
    // -> Run `Study.VideoTool.Preview` to preview the camera view.
    // -> Run `Study.VideoTool.Create_Video` to generate video from the analysis.
    #include "Model/CreateVideo.any"
  };

  // Include an operation sequence to run all required steps of your application (see Operations tab)
  #include "<ANYBODY_PATH_MODELUTILS>\Operations\RunAppSequence.any"

};// Main


