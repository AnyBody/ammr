
AnyFolder EnvironmentModel = {
  
  AnyFixedRefFrame GlobalRef = {
    Origin ={0,0,0}; 
    #include "drawcoorsystem.any"
    AnyRefNode DrwNode = {
      sRel = {-0.3, -0.1, 0};
      AnyDrawSTL bench = {
        FileName = "../Input/Bench.stl";
        RGB = {0,0,0};
      };
      
      AnyRefNode logo_node =
         {
           sRel = {-0.2, 0.03 ,0.15};
           //ARel = RotMat(90*pi/180, y);
           AnyDrawSurf Body = 
           {
             FileName = "../Input/Body-grey";
             RGB = Main.DrawSettings.Colors.AnyBodyGrey;
             Opacity = 1;
             ScaleXYZ= 0.003 * {1,1,1};
             AnyStyleDrawMaterial1 style = 
             {
               EnableCreasing = On;
               CreasingAngle = 0.524;
             };             
           };                    
           AnyDrawSurf AnyTechnology = 
           {
             FileName = "../Input/AnyTechnology-red";
             RGB = Main.DrawSettings.Colors.AnyBodyRed;
             Opacity = 1;
             ScaleXYZ= 0.003*{1,1,1};
             AnyStyleDrawMaterial1 style = 
             {
               EnableCreasing = On;
               CreasingAngle = 0.524;
             };               
           };          
         };    
         AnyRefNode logo_node2 =
         {
           sRel = {-0.2, 0.03 ,-0.15};
           ARel = RotMat(180*pi/180, y);
           AnyDrawSurf Body = 
           {
             FileName = "../Input/Body-grey";
             RGB = Main.DrawSettings.Colors.AnyBodyGrey;
             Opacity = 1;
             ScaleXYZ= 0.003*{1,1,1};
             AnyStyleDrawMaterial1 style = 
             {
               EnableCreasing = On;
               CreasingAngle = 0.524;
             };             
           };                    
           AnyDrawSurf AnyTechnology = 
           {
             FileName = "../Input/AnyTechnology-red";
             RGB = Main.DrawSettings.Colors.AnyBodyRed;
             Opacity = 1;
             ScaleXYZ= 0.003*{1,1,1};
             AnyStyleDrawMaterial1 style = 
             {
               EnableCreasing = On;
               CreasingAngle = 0.524;
             };             
           };          
         };             
      
    };
  };//GlobalRef
  
  AnySeg Weight = {
    Mass = 25;
    Jii = Mass*{1,1,0.01}/10;
    r0 = {-0.5, 0.25, 0};
    AnyRefNode DrwNode = {
      sRel = {-0.15, -0.15, 0.6};
      ARel = RotMat(pi/2,y);
      AnyDrawSTL drws = {
        FileName = "../Input/weight.stl";
      };
    };
    
    AnyRefNode lhand = {
      sRel = {0, 0, -0.4};
    };
    AnyRefNode rhand = {
      sRel = {0, 0, 0.4};
    };
  };
  
  AnyKinEq WeightRotFix = {
    AnyKinRotational rot = {
      AnyRefFrame &Weight = ..Weight;
      Type = RotAxesAngles;
    };
    Reaction.Type = {On,Off,Off};
  };
  
  AnyKinEqSimpleDriver WeightPos = {
    AnyKinLinear lin = {
      AnyRefFrame &Chest = Main.Model.HumanModel.Trunk.SegmentsThorax.ThoraxSeg;
      AnyRefFrame &Weight = ..Weight;
      Ref = -1;
    };
    MeasureOrganizer = {0,2};
    DriverVel = {0,0};
    DriverPos = {-0.35,0};
    Reaction.Type = {Off,Off};
  };
  
  AnyKinEqFourierDriver WeightHeight = {
    AnyKinLinear lin = {
      AnyRefFrame &Chest = Main.Model.HumanModel.Trunk.SegmentsThorax.ThoraxSeg.T1C7JntNode;
      AnyRefFrame &Weight = ..Weight;
      Ref = -1;
    };
    MeasureOrganizer = {1};
    Type = Cos;
    Freq = 0.1;
    A = {{0.35, 0.20}};
    B = {{0,0}};
    Reaction.Type = {Off};
  };
};