#include "../libdef.any"

/** -----------------------------------------------------------------------------
This model deals with the problem of getting out of a car - so called Egress.
For many elderly people, the task of getting up from a low car seat in a
situation where limited space is available can be quite strenous.

This model simulates an egress movement from where the occupant has just left
the support of the seat and until he or she is standing up.

The support points are:
- The left foot on the road outside the car
- The right foot on the floor inside the car
- The right hand holding on to a handle on the window frame

The question the model must answer is:
- What is the best position of the handle along the window frame?

The model investigates the muscular effort for varying positions of 
the window frame handle as it is moved from a low position to
a high position.

Notice that the model is not driven by actual motion capture data. Most
of the kinematics of the model is determined by the contact to the support
points. Additionally the pelvis is driven along an interpolated spline 
curve.*/
Main = {
  
  // Input model parameters 
  #include "Model/InputParameters.any"
  
  // ----------------------------------------------------------
  // Path to draw settings
  // ----------------------------------------------------------
  #path BM_DRAWSETTINGS_FILE "Model/DrawSettings.any"
  
  // ----------------------------------------------------------
  // Model configuration:
  // For more info on body model configuration options please load the model and double click on: 
  // #include "<ANYBODY_PATH_BODY>\Documentation\BodyModel.parameters.any"
  // ----------------------------------------------------------

  // This application was designed using old leg model: _LEG_MODEL_Leg_
  #define BM_LEG_MODEL _LEG_MODEL_Leg_
  #define BM_SCALING _SCALING_NONE_
  
  // The Mannequin file specifies load-time positions for all the segments
  // in the HumanModel. This is important for the model's ablity to resolve
  // all the kinematic constraints when the model is analyzed.
  // The mannequin file also drives those degrees of freedom of the human 
  // model that are not governed by problem-specific drivers at run time.    
  #path BM_MANNEQUIN_FILE "Model/Mannequin.any" 
  
  // Model of the human body to be used for the analysis  
  #include "<ANYBODY_PATH_BODY>/HumanModel.any"
  
  /// This folder collects all the model elements and is the target of the
  /// analyses performed by the study
  AnyFolder Model = {
    /// Body model without default drivers
    AnyFolder &HumanModel=.HumanModel.BodyModel;
    /// Reference to the mannequin folder (used by drivers)
    AnyFolder &Mannequin =.HumanModel.Mannequin;

    #include "Model/BodyModelFix.any"
    
    /// The environment, i.e. the car and ground.
    AnyFolder EnvironmentModel = {
      #include "Model/Environment.any"
    };
    
    /// Elements connecting the body to the environment
    AnyFolder ModelEnvironmentConnection = {
      #include "Model/JointsAndDrivers.any"
    };
  };
  
  AnyFolder HandlePosStudyModel1 = {
    AnyFolder &Model = .Model; 
    AnyFolder AdditionalJointsAndDriver = {
      #include "Model/HandlePosStudyJointsAndDrivers1.any"
    };
  };
  
  ///In this study the handle for the right hand changes position along
  ///the window frame. The left arm is unsupported
  AnyBodyStudy Study = {
    AnyFolder &Model = .HandlePosStudyModel1; 
    tEnd = 10.0;
    Gravity = {0.0, -9.81, 0.0};
    nStep = 11;
  }; // End of study
  
  // Include an operation sequence to run all required steps of your application (see Operations tab)
    #include "<ANYBODY_PATH_TOOLBOX>\Operations\RunAppSequence.any"  
  
}; //Main
