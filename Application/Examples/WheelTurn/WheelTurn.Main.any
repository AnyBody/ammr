#include "../libdef.any"

/**
This is a model of a person turning a horizontal wheel by means of a handle.
The purpose of the model is shoulder validation.
*/
Main = {
  
  /**Execute this operation to run the model in the intended operation sequence.  
  It is also possible to run operations seperately, by manual selections in the operation tree*/
  AnyOperationSequence RunApplication = {

    /**This operation calibrates the muscles in the model if these are of the type AnyMuscleModel3E.
    This will just be an empty operation if the model is using a muscle type that does not require calibration.*/
    AnyOperation &CalibrationAnal = Main.HumanModel.Calibration.CalibrationSequence;  

    //This operation is the inverse dynamic analysis
    AnyOperation &InvAnal=Main.Study.InverseDynamics;
   
  };



  
  AnyFolder StrengthParameters={
    AnyVar SpecificMuscleTensionSpine= 90; //N/cm^2
    AnyVar StrengthIndexLeg= 1; 
    AnyVar SpecificMuscleTensionShoulderArm= 90; //N/cm^2
  };
  
  AnyFolder WheelParameters = {
    AnyVec3 Hub = {0.35, -0.0, 0.3}; // Center of the wheel
    AnyVar Rad = 0.17;               // Radius of the wheel
    
    // Movement of the wheel
    AnyVar Omega = 30;             // Wheel angular velocity, degrees/sek
    AnyVar M = -8.0;                 // Wheel moment
  };
  
  AnyFolder ChairParameters = {
    AnyVec3 HandRest = {0.2, 0.0, -0.25};  // Rest for the left hand
  };
  
  #include "DrawSettings.any"
  AnyFolder HumanModel={
    
    AnyFolder &Mannequin=.Model.Mannequin;
    
    //This model should be used when playing around with the model in the 
    //initial modelling phase since leaving the normal muscles out, makes the 
    //model run much faster. The model uses artificial muscles on each dof. in 
    //the joints which makes it possible also to run the inverse analysis.  
    //#include  "<ANYBODY_PATH_BODY>\BodyModels\TrunkTwoArms\BodyModel_NoMuscles.any"    
    
    //This model uses the simple constant force muscles 
    //#include  "<ANYBODY_PATH_BODY>\BodyModels\TrunkNoMusclesTwoArms\BodyModel.any"
    #include  "BodyPartsSetup.any"
    #include  "<ANYBODY_PATH_BODY>/BodyModels/GenericBodyModel/BodyModel.any"
    AnyFolder StrengthParameters={
      AnyVar SpecificMuscleTensionSpine= 90; //N/cm^2
      AnyVar StrengthIndexLeg= 1; 
      AnyVar SpecificMuscleTensionShoulderArm= 90; //N/cm^2
    };
    
    
    //Choice of Scaling
    
    #include "<ANYBODY_PATH_BODY>\Scaling\ScalingStandard.any"
    //#include "<ANYBODY_PATH_BODY>\Scaling\ScalingUniform.any"
    // #include "<ANYBODY_PATH_BODY>\Scaling\ScalingLengthMass.any" 
    //#include "<ANYBODY_PATH_BODY>\Scaling\ScalingLengthMassFat.any" 
    
    Scaling={
      //#include "<ANYBODY_PATH_BODY>\Scaling\AnyFamily\AnyMan.any"
    };
    
    
    
  };
  
  AnyFolder Model = {    
    
    AnyFolder &HumanModel=.HumanModel.BodyModel;
    
    #include "Environment.any"   
    
    //The Mannequin values is used both for setting the initial positions of the model
    //and driving the model at analysis time. So you can change the values in the Mannequin
    //file and it will change both initial positions and how it is positioned at analysis time. 
    //If you want to run the model into some posture and then dump the momentary joint angles as the
    //initial ones then simply do the following:
    // 1  Double click the folder Main.MannequinValuesFromModel.AnyFolder_Posture and it will dump the joint angles to the message window 
    // 2. Copy the dumped text into the Mannequin.any replacing the "Posture" folder 
    // 3. Press ctrl+h and search and replace "_" with " "
    // 4. Once the model is reloaded it will start in the position corresponding to the one which was dumped.
    #include "Mannequin.any"   
    
    
    AnyFolder ModelEnvironmentConnection = {
      #include "JointsAndDrivers.any"
    };
  };

  AnyBodyStudy Study = {
    AnyFolder &Model = .Model;
    
    tEnd = 360/.WheelParameters.Omega;  // Always turn one round
    Gravity = {0.0, -9.81, 0.0};
    nStep=50;
  }; // End of study 
  
 
  
}; //Main

