#define MotionAndParameterOptimizationModel 1
#define InverseDynamicModel 0
#define HumanModelPresent 0
 #define LoadC3DMarkersandPlatesModel 

//This is a list of the classes used for defining the model
//Class used for defining a marker
#include "../../../Body/AAUHuman/ToolBox/Mocap/CreateMarkerClass.any"
//Class used for defining a marker
#include "../../../Body/AAUHuman/ToolBox/Mocap/CreateMarkerClassTD.any"
//Class used to construct a force plate of type4
#include "../../../Body/AAUHuman/ToolBox/Mocap/ForcePlateType4AutoDetection.any"
#include "../../../Body/AAUHuman/ToolBox/Mocap/ForcePlateType4.any"


Main = {
  
  
  
   #include "GaitLowerExtremityProject.Main.any"
  
  #include "ModelSetup.any"
  #include "TrialSpecificData.any" 
  
  
  
  Main.Project.Files.MainFile="LoadC3DData.Main.any";
  
  Main.Project.Tasks.Set_C3D_File_and_Environment = {
      
      Description = {
        Title = "<h4>Set C3D File and Environment </h4>";
        
        BodyText = 
        "This task can be used to review the loaded data C3D data."+
        "<br/>"+
        "<br/>"+
        "The following items are defined here:<br/>"+
         strformat ("\t")+"1.)"+ "The name of the C3D file:" + AnyScriptLinkOf(&Main.TrialSpecificData.NameOfFile,"NameOfFile") +"<br/>"+
        strformat ("\t")+"2.)"+ "The direction of "+ AnyScriptLinkOf(&Main.TrialSpecificData.Gravity,"gravity") + " wrt. the yellow coordinate system in the ModelView<br/>"+
        strformat ("\t")+"3.)"+ AnyScriptLinkOf(&Main.TrialSpecificData.tStart,"Start ") + "and"+  AnyScriptLinkOf(&Main.TrialSpecificData.tEnd," End") + " time of analysis. Change this only if default from C3D is not desired." +
        "<h4> 2.) Review trajectories </h4>"+
        "Review the marker trajectories and ensure that non of the markers drop out during the motion. " +
        "Try dragging the box-shaped markers and you will see them move."+
        
        "<h4> 3.) Create force plates</h4>"+
        "Change the force plate definitions if those defined in the C3D file are different from the predefined plates in the model.<br/>" +
        "This can be done in the "+  AnyScriptLinkOf(&Main.LoadC3DDataStudy.EnvironmentModel.Plate1,"environment.any") +" file.<br/>"+
        "The force plates types of the C3D file can be reviwed here: " + AnyBodyLinkOf(&Main.ModelSetup.C3DFileData.Groups.FORCE_PLATFORM.TYPE.Data,"force plate type ") +
        "If, for instance, Data={4,4,4}, the C3D file contains three plates, all of which are type 4.<br/>" +
        "More details on the different force plates types are available online: "+
        "<a href=" + strquote("http://www.c3d.org/html/default.htm?turl=theforceplatformgroup.htm") + ">www.c3d.org</a>"  +
        "<br/>"+
        "The model repository contains a selection of the most common force plate types. These can be found in the Body/AAUHuman/ToolBox/Mocap folder.<br/>"+
        "Currently the following types are available" +
        "<br/>"+
        strformat ("\t") +"1.) Force plate " + "<a href=" + strquote("http://www.c3d.org/html/type2.htm") + ">Type2</a>" +"<br/>"+
        strformat ("\t") + "    File: Body/AAUHuman/ToolBox/Mocap/ForcePlateType2.any  <br/>"+
        strformat ("\t") + "    File: Body/AAUHuman/ToolBox/Mocap/ForcePlateType2AutoDetection.any  <br/>"+
        strformat ("\t") +"2.) Force plate" +        "<a href=" + strquote("http://www.c3d.org/html/type3.htm") + ">Type3</a>" +"<br/>"+
        strformat ("\t") + "    File: Body/AAUHuman/ToolBox/Mocap/ForcePlateType3.any  <br/>"+
        strformat ("\t") + "    File: Body/AAUHuman/ToolBox/Mocap/ForcePlateType3AutoDetection.any  <br/>"+
        strformat ("\t") +"3.) Force plate" +        "<a href=" + strquote("http://www.c3d.org/html/type4.htm") + ">Type4</a>" +"<br/>"+
        strformat ("\t") + "    File: Body/AAUHuman/ToolBox/Mocap/ForcePlateType4.any  <br/>"+
        strformat ("\t") + "    File: Body/AAUHuman/ToolBox/Mocap/ForcePlateType4AutoDetection.any  <br/>"+
        "<h4>Execute Task Description</h4>" +
        "Executing this task will drive the markers and visualize the measured forces. This enables a fast review of the C3D input data. " +
        "Please pay special attention to the force directions and to non-smoothness of the data.";
        Tooltip = Title;
        
      };
      
      Operation =  &Main.LoadC3DDataStudy.Kinematics;
      
   
  };
  
  
  AnyBodyStudy LoadC3DDataStudy = {
    
    AnyFolder DummyStructure= {  
      AnyFolder Right ={ AnyFolder Leg ={AnyFolder Seg ={AnyFixedRefFrame  Foot ={ AnyRefNode HeelContactNodeLow={};AnyRefNode ToeLateralContactNode={};};};};  };
      AnyFolder Left ={ AnyFolder Leg ={AnyFolder Seg ={AnyFixedRefFrame Foot ={ AnyRefNode HeelContactNodeLow={}; AnyRefNode ToeLateralContactNode={};};};}; };
    };

    
    //Environment around the human
    AnyFolder EnvironmentModel ={
      //Model of the floor and force plates this is where the force plates are defined        
      AnyFolder &HumanModelRef=.DummyStructure;        
      //This environment file makes use of automatic detection of which foot are incontact with which plate 
      #include "EnvironmentAutoDetection.any"
      //This environment file has no automatic detecteion of which foot are incontact with which plate it has to be set manually
      //#include "Environment.any"
    };
    
    AnyFixedRefFrame GlobalRef ={       AnyDrawRefFrame drw={ScaleXYZ=0.4*{1,1,1};};    };  
    AnyFolder &C3DData=.ModelSetup.C3DFileData ;
    Gravity = .TrialSpecificData.Gravity;
    tStart = Main.TrialSpecificData.tStart+2*Kinematics.ApproxVelAccPerturb; 
    tEnd = Main.TrialSpecificData.tEnd-2*Kinematics.ApproxVelAccPerturb;
    
  };      
  
};  
#undef InverseDynamicModel
#undef MotionAndParameterOptimizationModel
#undef HumanModelPresent
 #undef LoadC3DMarkersandPlatesModel 


