//This file creates the environment around the human
//It creates the force plates and add the measure load to them

AnyFixedRefFrame GlobalRef ={
  Origin={0,0,0};
  AnyDrawRefFrame drw={ScaleXYZ=0.4*{1,1,1};};
};  

AnyFixedRefFrame Corners={
  //Axes=....Orientation;
  Axes=RotMat(-pi*0.5,z);

  AnyFloat corners=Main.C3DFileData.Groups.FORCE_PLATFORM.CORNERS.Data;
  
  AnyRefNode c01={
    AnyInt i=0;
    AnyInt plnr=0;
    sRel=0.001*{.corners[plnr][i][0],.corners[plnr][i][1],.corners[plnr][i][2]};
  };
  AnyRefNode c02={
    AnyInt i=1;   
    AnyInt plnr=0;
    sRel=0.001*{.corners[plnr][i][0],.corners[plnr][i][1],.corners[plnr][i][2]};
  };
  AnyRefNode c03={
    AnyInt i=2; 
    AnyInt plnr=0;
    sRel=0.001*{.corners[plnr][i][0],.corners[plnr][i][1],.corners[plnr][i][2]};
  };
  
  AnyRefNode c04={
    AnyInt i=3; 
    AnyInt plnr=0;
    sRel=0.001*{.corners[plnr][i][0],.corners[plnr][i][1],.corners[plnr][i][2]};
  };
  
  //plate B
  AnyRefNode c0center={
    sRel=0.25*(.c01.sRel+.c02.sRel+.c03.sRel+.c04.sRel);
    ARel=RotMat(pi,x)*RotMat(-0.5*pi,z);
  };
  
};
//setup time array for analog data

//StartFrame*1/(Main.C3DFileData.Header.VideoFrameRate).
//Og herefter follows all samples with  a frquency on
//Main.C3DFileData.Header.VideoFrameRate*
//Main.C3DFileData.Header.NoAnalogSamplesPer3DFrame
//So:
//analog sample 1: StartFrame*1/(Main.C3DFileData.Header.VideoFrameRate).
//Analog sample 2: StartFrame*1/(Main.C3DFileData.Header.VideoFrameRate) +
//1/(Main.C3DFileData.Header.VideoFrameRate*
//Main.C3DFileData.Header.NoAnalogSamplesPer3DFrame)
//and so on.
AnyInt   StartFrame = Main.C3DFileData.Header.FirstFrameNo;
AnyInt   EndFrame =Main.C3DFileData.Header.FirstFrameNo-1+(Main.C3DFileData.Header.LastFrameNo-Main.C3DFileData.Header.FirstFrameNo)* Main.C3DFileData.Header.NoAnalogSamplesPer3DFrame;
AnyFloat Time=((StartFrame*Main.C3DFileData.Header.NoAnalogSamplesPer3DFrame)+iarr(0,EndFrame-StartFrame))*(1/(Main.C3DFileData.Header.VideoFrameRate*Main.C3DFileData.Header.NoAnalogSamplesPer3DFrame));

AnyFolder Plate1={
  AnySeg ForcePlate={
    Mass=0.0;
    Jii={0,0,0};
    r0=..Corners.c0center.sRel;
    Axes0 =RotMat(pi,z)*RotMat(pi,x)*RotMat(-0.5*pi,z);

    AnyRefNode PlateGraphics ={
      sRel={0,0,0.05};
      AnyDrawRefFrame drw={
        ScaleXYZ=0.25*{1,1,1};
        RGB=.Color;
      };
      AnyVec3 Color=0.45*{1,1,1};
      AnyVec3 Size={vnorm(...Corners.c01.sRel-...Corners.c02.sRel,2),vnorm(...Corners.c02.sRel-...Corners.c03.sRel,2),0.1};
      AnyVar Opacity =0.4;
      #include "<ANYBODY_PATH_BODY>/Toolbox/DrawObjects/Box.any"
    };
    
    AnyFloat Origins= Main.C3DFileData.Groups.FORCE_PLATFORM.ORIGIN.Data;
    AnyVar ydist=Origins[0][0]; //distance from y axis on transducer to y axis of center of plate
    AnyVar xdist=Origins[0][1];//distance from x axis on transducer to x axis of center of plate
    AnyVar zdist=Origins[0][2];//distance from z axis on transducer to z surface of plate
    
    AnyRefNode P1={
      sRel=0.001*{.ydist,.xdist,.zdist};
     AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P2={
      sRel=0.001*{-.ydist,.xdist,.zdist};
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P3={
      sRel=0.001*{-.ydist,-.xdist,.zdist};
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P4={
      sRel=0.001*{.ydist,-.xdist,.zdist};
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P1P2={
      sRel=0.5*(.P1.sRel+.P2.sRel);
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P3P4={
      sRel=0.5*(.P3.sRel+.P4.sRel);
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P1P4={
      sRel=0.5*(.P1.sRel+.P4.sRel);
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };
    AnyRefNode P2P3={
      sRel=0.5*(.P2.sRel+.P3.sRel);
      AnyDrawNode drw={ScaleXYZ = 0.02*{1,1,0.1}; RGB={1,0,0};};
    };

 
  };  
  
  AnyKinEqSimpleDriver ForcePlateDriver ={
    AnyKinLinear ForcePlateLin={
      AnyRefNode &ref1=...Corners.c0center;
      AnySeg &ref2=..ForcePlate;
      Ref=0;
    };
    
    AnyKinRotational ForcePlateRot={
      AnyRefNode &ref1=...Corners.c0center;
      AnySeg &ref2=..ForcePlate;
      Type=RotAxesAngles;
    };
    DriverPos={0,0,0,pi*0.5,0,0}; 
    DriverVel={0,0,0,0,0,0};
    Reaction.Type = {Off,Off,Off,Off,Off,Off};
  };
  
  
  AnyReacForce PlateFootReaction={
    
    AnyKinLinear Lin={
      Ref=0;
      AnySeg &ref2=..ForcePlate;
      AnySeg &ref1=....HumanModel.Left.Leg.Seg.Foot;
    };
    
    AnyKinRotational Rot={
      AnySeg &ref2=..ForcePlate;
      AnySeg &ref1=....HumanModel.Left.Leg.Seg.Foot;
      Type=RotAxesAngles;
    };
  };
  
  //This particular data set has a problem with force offsets in the analog data there is a correction vector for each of the four plates
  AnyVector OffsetCorrection={0,0,0,0,0,0,0,0};
  
  
  AnyForce3D Fx12 ={
    
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fx12}+..OffsetCorrection[0] ;
    }; 
    
    AnyRefNode &ref1=.ForcePlate.P1P2;
    Flocal=.OnOff*{force(t)[0],0,0};  
    
    AnyDrawVector DrawSupport2 = {
      AnyRefFrame &ref = .ref1;
      Vec = 0.0005*.Flocal*.ref1.Axes';
      PointAway = Off;
      DrawCoord = Off;
      Line.RGB = {0,0,1};
      Line.Thickness =0.0075;
      Line.End.Thickness = 2*0.0075;
      Line.End.Length = 4*0.0075;
    };
    
  };
  
  AnyForce3D Fx34 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fx34}+..OffsetCorrection[1] ;
    }; 
    AnyRefNode &ref1=.ForcePlate.P3P4;
    Flocal=.OnOff*{force(t)[0],0,0};  
    #include "DrawSupport.any"
  };
  
  
  AnyForce3D Fy14 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fy14}+..OffsetCorrection[2] ; 
    };
    AnyRefNode &ref1=.ForcePlate.P1P4;
    Flocal=.OnOff*{0,force(t)[0],0};  
    #include "DrawSupport.any"
  };
  
  
  
  
  AnyForce3D Fy23 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fy23}+..OffsetCorrection[3] ; 
    }; 
    AnyRefNode &ref1=.ForcePlate.P2P3;
    Flocal=.OnOff*{0,force(t)[0],0};  
    #include "DrawSupport.any"
};
  
  
  
  AnyForce3D Fz1 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fz1}+..OffsetCorrection[4] ; 
    }; 
    AnyRefNode &ref1=.ForcePlate.P1;
    Flocal=.OnOff*{0,0,force(t)[0]};  
    #include "DrawSupport.any"
  };
  
  
  
  AnyForce3D Fz2 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fz2}+..OffsetCorrection[5] ; 
    }; 
    AnyRefNode &ref1=.ForcePlate.P2;
    Flocal=.OnOff*{0,0,force(t)[0]};  
    #include "DrawSupport.any"
  };
  
  AnyForce3D Fz3 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fz3}+..OffsetCorrection[6] ; 
    }; 
    AnyRefNode &ref1=.ForcePlate.P3;
    Flocal=.OnOff*{0,0,force(t)[0]};  
    #include "DrawSupport.any"
  };
  
  
  AnyForce3D Fz4 ={
    AnyFunInterpol force ={
      Type=PiecewiseLinear;
      T=...Time;
      Data={Main.C3DFileData.Analog.Data.FP1Fz4}+..OffsetCorrection[7] ; 
    }; 
    AnyRefNode &ref1=.ForcePlate.P4;
    Flocal=.OnOff*{0,0,force(t)[0]};  
    #include "DrawSupport.any"
  };
  
  AnyVar FzTotal=(Fz1.force(ForcePlate.t)[0]+Fz2.force(ForcePlate.t)[0]+Fz3.force(ForcePlate.t)[0]+Fz4.force(ForcePlate.t)[0]);
  AnyVar OnOff=iffun(gtfun(-FzTotal,15.0),1.0,0.0);
  #include "CenterOfPressureVisualization.any"

}; //plate1












