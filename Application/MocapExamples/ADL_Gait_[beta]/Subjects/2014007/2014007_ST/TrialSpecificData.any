Main.ModelSetup.TrialSpecificData =
{

  #define REFERENCE_TRIAL 1


  AnyStringVar WalkingCondition = "ST";

  // Special configuration/exception for this trial
  // SXS and JNJ are swapped in this trial
  #define EXCLUDE_SXS
  #define EXCLUDE_SJN
  Main.ModelSetup.MocapDrivers = {
    // Marker on the Sternum
    CreateMarkerDriver SXS (
      MarkerName= SJN,
      PlaceMarkerAt = XiphoidProcess,
      ScaleMarkerPosOnOff=OFF,
      MarkerPlacement=Trunk.Segments.SternalBodySeg, 
      OptX=ON,OptY=ON,OptZ=OFF,
      WeightX=1,WeightY=1,WeightZ=1
    ) = {
      sRelOpt = {0.02, 0, 0};
    };
    // Marker on the Clavicle
    CreateMarkerDriver SJN (
      MarkerName= SXS,
      PlaceMarkerAt=ij,
      ScaleMarkerPosOnOff=OFF,
      MarkerPlacement=Trunk.Segments.SternumSeg, 
      OptX=ON,OptY=OFF,OptZ=OFF
    ) = {
      sRelOpt = {0.025, 0.01, 0.0};
      #ifdef REFERENCE_TRIAL
      // High weight in Parameter optimization ensure this
      // marker determines body height
      ConstMarkerWeightY = 5;
      #endif
    };
  };


  TrialFileName = MOCAP_NAME_MAINFILEDIR;

  /// Uniqe ID for each trial
  AnyStringVar TrialID = MOCAP_NAME_MAINFILEDIR;
  // We need to manually set all these things since the reference files does not
  // contain exactly one frame.
  nStep = 1;
  Main.ModelSetup.C3DFileData.MarkerFilterIndex = -1;
  #define N_STEP_KINEMATICS 1


   // This class_template calculates the load time position and orientation of
   // the pelvis based on markers.
   AutoPelvisPos AutoPos(RASIS=R_IAS, LASIS=L_IAS, BACK=L_IPS) = {};

};
