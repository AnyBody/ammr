BodyModel.Right.Leg.Jnt = {
  AnyRevoluteJoint Inv_TF = {
    Axis = z;
    AnyRefFrame &InvFemur = Main.HumanModel.BodyModel.Right.Leg.Seg.InvisibleFemur_TF;
    AnyRefFrame &InvTibia= Main.HumanModel.BodyModel.Right.Leg.Seg.InvisibleTibia;
    //      AnyDrawStdJoint drw = {};
  };
  
  //----------------------------------------------------------------           
  // Drivers for the movement of the femoral axis
  //----------------------------------------------------------------       
  AnyKinEq InvisibleFemurMedialEFCDriver = {
    AnyKinMeasureLinComb LinComb = {
      Coef = {{1.0,0.0,0.0,1.0/(pi/2.0)*(...Seg.Thigh.Medial_TF_FFC.sRel[0]-...Seg.Thigh.Medial_TF_EFC.sRel[0])},{0.0,1.0,0.0,1.0/(pi/2.0)*(...Seg.Thigh.Medial_TF_FFC.sRel[1]-...Seg.Thigh.Medial_TF_EFC.sRel[1])},{0.0,0.0,1.0,1.0/(pi/2.0)*(...Seg.Thigh.Medial_TF_FFC.sRel[2]-...Seg.Thigh.Medial_TF_EFC.sRel[2])}};
      Const ={0.0,0.0,0.0};
      AnyKinLinear FemurTrans = {
        Ref = 0;
        AnyRefFrame &ref1 = ....Seg.Thigh.Medial_TF_EFC;
        AnyRefFrame &ref2 = ....Seg.InvisibleFemur_TF.MA_AnatomicalFrame.Medial_TF_EFC;
      };
      AnyKinMeasure &KneeFlexion = ..Inv_TF;
      OutDim = 3;  
    };
  };
  
  AnyKinEq InvisibleFemurLateralEFCDriver = {
    AnyKinMeasureLinComb LinComb = {
      Coef = {{1.0,0.0,1.0/(pi/2.0)*(...Seg.Thigh.Lateral_TF_FFC.sRel[0]-...Seg.Thigh.Lateral_TF_EFC.sRel[0])},{0.0,1.0,1.0/(pi/2.0)*(...Seg.Thigh.Lateral_TF_FFC.sRel[1]-...Seg.Thigh.Lateral_TF_EFC.sRel[1])}};
      Const ={0.0,0.0};
      AnyKinMeasureOrg org = {
        AnyKinLinear FemurTrans = {
          Ref = 0; 
          AnyRefFrame &ref1 = .....Seg.Thigh.Lateral_TF_EFC;
          AnyRefFrame &ref2 = .....Seg.InvisibleFemur_TF.MA_AnatomicalFrame.Lateral_TF_EFC;
        };
        MeasureOrganizer = {0,1}; 
      };
      AnyKinMeasure &KneeFlexion = ..Inv_TF;
      OutDim = 2;  
    };
  };
  
  AnyKinEq InvisibleFemurZRot = { 
    AnyKinMeasureOrg org = {
      AnyKinRotational rot = {
        Type = RotAxesAngles;
        Axis1 = z;
        Axis2 = x;
        Axis3 = y;
        //There was an invisible rotation node on the thigh that avoided jumping
        AnyRefFrame &ref1 = ....Seg.Thigh.InvisibleFemurRotationNode;
        AnyRefFrame &ref2 = ....Seg.InvisibleFemur_TF;          
      };
      MeasureOrganizer = {0};// 
    };
  };
  //----------------------------------------------------------------       
  // Drivers for the movement of the tibial axis
  //----------------------------------------------------------------       
  
  AnyKinEq InvisibleTibiaMedialEFCDriver = {
    AnyKinMeasureLinComb LinComb = {
      Coef = {{1.0,0.0,0.0,1.0/(pi/2.0)*(...Seg.Shank.Medial_TF_FFC.sRel[0]-...Seg.Shank.Medial_TF_EFC.sRel[0])},{0.0,1.0,0.0,1.0/(pi/2.0)*(...Seg.Shank.Medial_TF_FFC.sRel[1]-...Seg.Shank.Medial_TF_EFC.sRel[1])},{0.0,0.0,1.0,1.0/(pi/2.0)*(...Seg.Shank.Medial_TF_FFC.sRel[2]-...Seg.Shank.Medial_TF_EFC.sRel[2])}};
      Const ={0.0,0.0,0.0};
      AnyKinLinear TibiaTrans = {
        Ref = 0;
        AnyRefFrame &ref1 = ....Seg.Shank.Medial_TF_EFC;
        AnyRefFrame &ref2 = ....Seg.InvisibleTibia.MA_AnatomicalFrame.Medial_TF_EFC;
      };
      AnyKinMeasure &KneeFlexion = ..Inv_TF;
      OutDim = 3;  
    };
  };
  
  AnyKinEq InvisibleTibiaLateralEFCDriver = {
    AnyKinMeasureLinComb LinComb = {
      Coef = {{1.0,0.0,1.0/(pi/2.0)*(...Seg.Shank.Lateral_TF_FFC.sRel[0]-...Seg.Shank.Lateral_TF_EFC.sRel[0])},{0.0,1.0,1.0/(pi/2.0)*(...Seg.Shank.Lateral_TF_FFC.sRel[1]-...Seg.Shank.Lateral_TF_EFC.sRel[1])}};
      Const ={0.0,0.0};
      AnyKinMeasureOrg org = {
        AnyKinLinear TibiaTrans = {
          Ref = 0; 
          AnyRefFrame &ref1 = .....Seg.Shank.Lateral_TF_EFC;
          AnyRefFrame &ref2 = .....Seg.InvisibleTibia.MA_AnatomicalFrame.Lateral_TF_EFC;
        };
        MeasureOrganizer = {0,1};
      };
      AnyKinMeasure &KneeFlexion = ..Inv_TF;         
      OutDim = 2;  
    };
  };
  
  AnyKinEq InvisibleTibiaZRot = {
    AnyKinMeasureOrg org = {
      AnyKinRotational rot = {
        Type = RotAxesAngles;
        Axis1 = z;
        Axis2 = x;
        Axis3 = y;
        AnyRefFrame &ref1 = ....Seg.Shank.InvisibleFemurRotationNode;
        AnyRefFrame &ref2 = ....Seg.InvisibleTibia;          
      };
      MeasureOrganizer = {0};
    };
  };  
  
  
};
