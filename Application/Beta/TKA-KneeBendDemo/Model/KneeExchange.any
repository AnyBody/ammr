
//     exclude knee joint
AnyObjectPtrArray l1 = ObjSearch("Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.KneeJoint","AnyMechObject");
AnyObjectPtrArray l2 = ObjSearchRecursive("Main.HumanModel.BodyModel.Right.Leg.Jnt.Knee","*","AnyMechObject");
AnyObjectPtrArray l3 = ObjSearchRecursive("Main.Model.ModelEnvironmentConnection.JointsAndDrivers.JntDriverRightLegTD","Knee*","AnyMechObject");

AnyObjectPtrArray l4 = ObjSearch("Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.PatellaFemurJoint","AnyMechObject");
AnyObjectPtrArray l5 = ObjSearchRecursive("Main.HumanModel.BodyModel.Right.Leg.Jnt.PatellaFemur","*","AnyMechObject");
AnyObjectPtrArray l6 = ObjSearch("Main.HumanModel.BodyModel.Right.Leg.Jnt.PatellaMovement","AnyMechObject");
AnyObjectPtrArray l7 = ObjSearchRecursive("Main.HumanModel.BodyModel.Right.Leg.Jnt.PatellaMovement","*","AnyMechObject");

AnyObjectPtrArray KneeExcludeList = arrcat(arrcat(l1,l2), l3);
AnyObjectPtrArray PatellaExcludeList = arrcat(arrcat(arrcat(l4, l5),l6),l7);

AnyObjectPtrArray excludelist = arrcat(KneeExcludeList, PatellaExcludeList );
MechObjectExclude = excludelist;




AnyFolder ContactKnee = {
  
  AnyFolder KinematicMeasures = {
    AnyKinPLine Patella = {
      // These are the nodes that the measure refers to. The connection is regarded
      // as being stiff. 
      AnyRefNode &Insertion_patella_tendon = Main.HumanModel.BodyModel.Right.Leg.Seg.Shank.Insertion_patella_tendon;
      AnyRefNode &Origin_patella_tendon = Main.HumanModel.BodyModel.Right.Leg.Seg.Patella.Origin_patella_tendon;
      
      AnyDrawPLine Patella_tendon = 
      {
        Thickness = 0.001;
        RGB = Main.DrawSettings.Ligament.RGB;
      };
    };
    
  };
  
  // KinematicMeasures
  AnyKinEqSimpleDriver PatellaMovementForceDep = {
    AnyKinPLine &Jnt = .KinematicMeasures.Patella;
    
    // has to be checked
    AnyFloat gscale = {1,1,1};//vnorm(Main.HumanModel.BodyModel.Right.Leg.GeoScaling.Shank.ScaleFunction.ScaleMat);
    
    DriverPos = {0.0605};//gscale[1]*{0.0605};
    DriverVel = {0.0};
    Reaction.Type = {On};
  };
  
  
  AnyKinEqSimpleDriver PatellaForceDep = {
    DriverPos = Main.Parameters.PatellaForceDepDriverPos ;
    DriverVel = {0.0};
    
    AnyKinMeasureOrg OrgLin = {
      AnyKinLinear lin = {
        Ref=1;
        AnyRefNode &ThighNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.PatellaFemurJoint;
        AnyRefNode &PatellaNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Patella.PatellaFemurJoint;
      };
      MeasureOrganizer={0};
    };
    
    
    CType = {ForceDep};
    Reaction.Type = {Off};
  };
  
  AnyKinEqSimpleDriver PatellaLinRotEqForceDep = {
    DriverPos = {0.0,-0.003,-0.12,-0.07};
    DriverVel = {0.0,0.0,0.0,0.0};
    AnyKinMeasureOrg OrgLin = {
      AnyKinLinear lin = {
        Ref=1;
        AnyRefNode &ThighNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.PatellaFemurJoint;
        AnyRefNode &PatellaNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Patella.PatellaFemurJoint;    
      };
      MeasureOrganizer={1,2};
    };
    AnyKinMeasureOrg RotOrg = {
      AnyKinRotational rot = {
        Type = RotAxesAngles;
        AnyRefNode &ThighNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.PatellaFemurJoint;
        AnyRefNode &PatellaNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Patella.PatellaFemurJoint;
      };
      MeasureOrganizer={1,2};
    };
  };
  
  
  AnyKinEqSimpleDriver KneeForceDep = {
    DriverPos = Main.Parameters.KneeForceDepDriverPos;
    DriverVel = {0.0,0.0,0.0,0.0,0.0};
    
    AnyKinMeasureOrg OrgLin = {
      AnyKinLinear lin = {
        Ref=0;    
        AnyRefNode &ShankNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Shank.KneeJoint;    
        AnyRefNode &ThighNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.KneeJoint;
      };
      MeasureOrganizer={0,1,2};
    };
    AnyKinMeasureOrg RotOrg = {
      AnyKinRotational rot = {
        Type = RotAxesAngles;
        AnyRefNode &ThighNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Thigh.KneeJoint.RotNode;
        AnyRefNode &ShankNode = Main.HumanModel.BodyModel.Right.Leg.Seg.Shank.KneeJoint.RotNode;
        
      };
      MeasureOrganizer={1,2};
    };
    
    CType = {ForceDep,ForceDep,ForceDep,ForceDep,ForceDep};
    Reaction.Type = {Off,Off,Off,Off,Off};
  };
  
};



