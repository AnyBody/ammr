
Right = {

  AnyFolder ShoulderArm = {  
    
      AnyFolder &Scaling = ...Scaling.GeometricalScaling.Right;
      AnyFolder &MassScaling = ...Scaling.MassScaling.Right;
      AnyFolder &StrengthScaling = ...Scaling.StrengthScaling.Right;
      
      #define _LEFT_RIGHT_ "RIGHT"
      
      #include "..\..\Arm\ShoulderArmModel.root.any"
      
      #undef _LEFT_RIGHT_
      
      //Reference to folder which contains the interface of the right side
      AnyFolder &InterfaceFolder =..Interface.Right; 
      
      //Reference to folder 
      AnyFolder &MannequinValuesFolder = ..MannequinValuesFromModel.AnyFolder_Posture.AnyFolder_Right;
      
      #include "../Include/SettingsShoulderArm.any" 
      
      //-------------------------------------------------------
      // Interface references that specifies what the arm "Arm/ShoulderArm.any" should connect to 
      //-------------------------------------------------------
      //This point is used for attachement of the arm to the shoulder
      AnyRefNode &ShoulderRef=..Trunk.SegmentsThorax.ThoraxSeg.ij;
      
      AnyRefNode &PelvisRef=..Trunk.SegmentsLumbar.PelvisSeg.Right;
      AnyRefNode &L1Ref=..Trunk.SegmentsLumbar.L1Seg.Right;
      AnyRefNode &L3Ref=..Trunk.SegmentsLumbar.L3Seg.Right;   
      AnyFolder &TrunkFolderRef=..Trunk;
      
      AnyFolder &SkullSegRef =..Trunk.SegmentsThorax.SkullSeg.Right;
      AnyFolder &C1SegRef =..Trunk.SegmentsCervicalSpine.C1Seg.Right;
      AnyFolder &C2SegRef =..Trunk.SegmentsCervicalSpine.C2Seg.Right;
      AnyFolder &C3SegRef =..Trunk.SegmentsCervicalSpine.C3Seg.Right;
      AnyFolder &C4SegRef =..Trunk.SegmentsCervicalSpine.C4Seg.Right;
      AnyFolder &C5SegRef =..Trunk.SegmentsCervicalSpine.C5Seg.Right;
      AnyFolder &C6SegRef =..Trunk.SegmentsCervicalSpine.C6Seg.Right;
      AnyFolder &C7SegRef =..Trunk.SegmentsCervicalSpine.C7Seg.Right;
      
      //This is a sign variable which indicates that this is the right side
      AnyVar Sign =1;
      
      AnyFolder BonesOpacity = {
        AnyVar Clavicula = Main.DrawSettings.BonesOpacity.RightClavicula;
        AnyVar Scapula = Main.DrawSettings.BonesOpacity.RightScapula;
        AnyVar Humerus = Main.DrawSettings.BonesOpacity.RightHumerus;
        AnyVar Ulna = Main.DrawSettings.BonesOpacity.RightUlna;
        AnyVar Radius = Main.DrawSettings.BonesOpacity.RightRadius;
        AnyVar Hand = Main.DrawSettings.BonesOpacity.RightHand;
      };
      
      AnyFolder MomentMeasure=
      {
        #if BM_ARM_MUSCLES_RIGHT != _MUSCLES_NONE_ 
         #if BM_ARM_DETAIL_HAND_RIGHT == ON         
           #include "..\..\Arm\ArmHandMoments.any"
         #else
           #include "..\..\Arm\ArmMoments.any"
         #endif          
        #else
         AnyForceMomentMeasure ElbowJointReactionMoments = 
         {
           AnyForceBase &Force = ..Jnt.FE.Constraints.Reaction;
           AnyRefFrame &Humerusfe = ..Seg.Humerus.fe;
           AnyVec3 Mlocal = M*Humerusfe.Axes;
         };
         
         AnyForceMomentMeasure WristJointReactionMoments = 
         {
           AnyForceBase &Force = ..Jnt.WristJointFlexion.Constraints.Reaction;
           AnyRefFrame &Radiuswj = ..Seg.Radius.wj;
           AnyVec3 Mlocal = M*Radiuswj.Axes;
         }; 
        #endif
      };
      
           
    }; //End ShoulderArm
    
  };
  
  #if (BM_ARM_MUSCLES_RIGHT == _MUSCLES_NONE_)
    #include "../../Arm/RightArmNoMusclesSelectedOutput.any"
  #else
    #include "../../Arm/RightArmSimpleMusclesSelectedOutput.any"
  #endif