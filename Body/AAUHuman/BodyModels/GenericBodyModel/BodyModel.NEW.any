AnyFolder BodyModel = {
  
  // The folder containing the selected human model output.
  AnyFolder SelectedOutput = {
    AnyFolder Trunk = {};
  };
  
 
  // Check for non-allowed combinations
  // **********************************
    
  
  #define ERROR (ErrorMessage) AnyMessage UnhallowedCombination = { TriggerPreProcess = On; Type = MSG_ErrorFatal; Message = ErrorMessage; };




  #if BM_LEG_RIGHT & BM_LEG_LEFT & (BM_LEG_MUSCLES_RIGHT != BM_LEG_MUSCLES_LEFT) & BM_LEG_MUSCLES_LEFT & BM_LEG_MUSCLES_RIGHT
    #ifndef FORBIDDEN_COMBINATION
    ERROR ("Combination is forbidden: Two different leg muscle models are used, please use _MUSCLE_SIMPLE_ or _MUSCLE_3E_HILL_ for both sides.")
    #define FORBIDDEN_COMBINATION
    #endif
  #endif
  
  
  #if BM_LEG_RIGHT & BM_LEG_LEFT & (BM_LEG_RIGHT != BM_LEG_LEFT)
    #ifndef FORBIDDEN_COMBINATION
    ERROR ("Combination is forbidden: Two different leg models are included.")
    #define FORBIDDEN_COMBINATION
    #endif
  #endif
  
  #if BM_ARM_RIGHT & BM_ARM_LEFT & (BM_ARM_MUSCLES_RIGHT != BM_ARM_MUSCLES_LEFT) & BM_ARM_MUSCLES_LEFT & BM_ARM_MUSCLES_RIGHT 
    #ifndef FORBIDDEN_COMBINATION
    ERROR ("Combination is forbidden: Two different arm muscle models are used, please use _MUSCLE_SIMPLE_ or _MUSCLE_3E_HILL_ for both sides.")
    #define FORBIDDEN_COMBINATION
    #endif
  #endif
  
  #if BM_ARM_RIGHT & BM_ARM_LEFT & (BM_ARM_SHOULDER_RHYTHM_LEFT != BM_ARM_SHOULDER_RHYTHM_RIGHT) 
    #ifndef FORBIDDEN_COMBINATION
    ERROR ("Combination is forbidden: Two different shoulder rhythms are included.")
    #define FORBIDDEN_COMBINATION
    #endif
  #endif
  
  
  #if BM_ARM_RIGHT & BM_ARM_LEFT & (BM_ARM_DETAIL_HAND_LEFT != BM_ARM_DETAIL_HAND_RIGHT) 
    #ifndef FORBIDDEN_COMBINATION
    ERROR ("Combination is forbidden: Two different Hand models are included.")
    #define FORBIDDEN_COMBINATION
    #endif
  #endif
  
  
  #if BM_TRUNK_CERVICAL_MUSCLES != _MUSCLES_NONE_
    #if !((BM_ARM_RIGHT & BM_ARM_MUSCLES_RIGHT) | (BM_ARM_LEFT & BM_ARM_MUSCLES_LEFT))
    #ifdef FORBIDDEN_COMBINATION
      #else
      ERROR ("Combination is forbidden: As some neck muscles are in the shoulder model, the neck model with muscles must be used with the neck part of the shoulder muscles to be complete.")
      #define FORBIDDEN_COMBINATION
      #endif
    #endif
  #endif

  
  
  // Include the selected body parts
  // *******************************
  
  // The body parts are included only if there is no unallowed combination detected
  #ifdef FORBIDDEN_COMBINATION
  #else
  
  #include "TrunkModel.any"
  IndividualMasses = {AnyVar TrunkMass = .Trunk.MassSummation.Mass;};
  
  
  #if BM_ARM_RIGHT == ON
  
    #include "RightArmModel.any"
    
    IndividualMasses = {AnyVar RightArmMass = .Right.ShoulderArm.MassSummation.Mass;};
  
  #else
  IndividualMasses = {AnyVar RightArmMass = 0;};
  #endif
  
  
  
  
  #if BM_ARM_LEFT == ON
  
    #include "LeftArmModel.any"    

    IndividualMasses = {AnyVar LeftArmMass = .Left.ShoulderArm.MassSummation.Mass;};
  
  #else
  IndividualMasses = {AnyVar LeftArmMass = 0;};
  #endif
  
  
  #if BM_LEG_RIGHT == _LEG_MODEL_Leg_
  
    #include "RightLegModel.any"
    
    IndividualMasses = {AnyVar RightLegMass = .Right.Leg.MassSummation.Mass;};
  
  #else
    IndividualMasses = {AnyVar RightLegMass = 0;};
  
    #if BM_LEG_RIGHT == OFF
     Trunk = 
     {
       AnyRefNode &PsoasMajorRightReference=.Trunk.SegmentsLumbar.PelvisSeg.Right;
     };
    #else
    #endif
    
  #endif
  
  
  
  
  #if BM_LEG_LEFT == _LEG_MODEL_Leg_
  
    #include "LeftLegModel.any"
    
    IndividualMasses = {AnyVar LeftLegMass = .Left.Leg.MassSummation.Mass;};
  
  #else
    IndividualMasses = {AnyVar LeftLegMass = 0;};
  
    #if BM_LEG_LEFT == OFF
      Trunk = 
      {
        AnyRefNode &PsoasMajorLeftReference=.Trunk.SegmentsLumbar.PelvisSeg.Left;
      };
    #else
    #endif
  
  #endif
  
  
  
  #if (BM_LEG_RIGHT == _LEG_MODEL_TLEM_ | BM_LEG_RIGHT == _LEG_MODEL_TLEM2_)
      
    #include "RightLegTDModel.any"
    
    IndividualMasses = {AnyVar RightLegTDMass = .Right.Leg.MassSummation.Mass;};
      
  #else
    IndividualMasses = {AnyVar RightLegTDMass = 0;};
  #endif
  
  
  
  #if (BM_LEG_LEFT == _LEG_MODEL_TLEM_ | BM_LEG_LEFT == _LEG_MODEL_TLEM2_)
      
    #include "LeftLegTDModel.any"
    
    IndividualMasses = {AnyVar LeftLegTDMass = .Left.Leg.MassSummation.Mass;};
      
  #else
    IndividualMasses = {AnyVar LeftLegTDMass = 0;};
  #endif
  
      
  // Create the needed folders
  // *************************
  
  #if ((BM_ARM_RIGHT != OFF) | (BM_LEG_RIGHT != OFF)) 
  #include "RightFolder.any"
  #endif
  
  #if ((BM_ARM_LEFT != OFF) | (BM_LEG_LEFT != OFF)) 
  #include "LeftFolder.any"
  #endif
  
  
  AnyFolder MannequinValuesFromModel = {
    AnyFolder AnyFolder_Posture = {  
    };
  };
  
  
  AnyFolder Interface = {};
  
  AnyFolder IndividualMasses = {};
  
  // Total mass of the human model
  AnyVar TotalBodyMass = IndividualMasses.RightLegMass 
  + IndividualMasses.LeftLegMass 
  + IndividualMasses.RightLegTDMass 
  + IndividualMasses.LeftLegTDMass 
  + IndividualMasses.RightArmMass 
  + IndividualMasses.LeftArmMass 
  + IndividualMasses.TrunkMass; 
  //+ IndividualMasses.TrunkNeckMass;
//  + IndividualMasses.PelvisWithoutTrunkMass;
    
#endif

}; // end BodyModel




//include the calibration study
AnyFolder &HumanFolderRef=BodyModel;
#include "CalibrationSequence.any"
// Include moment arm study
#ifndef EVALUATE_MOMENT_ARMS
  #define EVALUATE_MOMENT_ARMS 0
#endif
#if EVALUATE_MOMENT_ARMS
  #include "EvaluateMomentArmsSequence.any"
#endif


// Include moment arm study
#ifndef EVALUATE_JOINT_STRENGTH
  #define EVALUATE_JOINT_STRENGTH 0
#endif
#if EVALUATE_JOINT_STRENGTH
  #include "EvaluateJointStrengthSequence.any"
#endif

