
//This file contains a moment measures for the following joints in the Leg:

//Ankle
//Knee
//Hip

//The measures named:
//AnkleNetMoment
//KneeNetMoment
//HipNetMoment
//will measure the moments and forces supplied by the muscles spanning these and on top of these
//moments and force the forces and moments from the constraints in the joints will be added.
//The resulting force and moment are equal to the to total moment and force which 
//could replace both the muscles spanning the joint and the joints reactions.

//The measures named:
//AnkleNetMomentMuscle
//KneeNetMomentMuscle
//HipNetMomentMuscle
//will measure ONLY the moments and forces supplied by the muscles spanning these joint and not the moments and forces which from the constraints in the joints.
//The resulting force and moment are equal to the to total moment and force which 
//are supplied by the muscles.

//In both cases the resulting force and moment is given in the global coordinate system.

//A vector which shows the resulting moment as an arrow, will automatically be shown in the model view the parameters of it can 
//be controlled from the "DrawSettings.any" file typically to be found in the application directory., 

AnyForceMomentMeasure2 AnklePlantarFlexionNetMomentMuscle = 
{
  AnyRefFrame &ref = ..Seg.Shank.AnkleJoint;
  //RefPoint = &..Seg.Shank.AnkleJoint;
  IncludeSegments = 
  {
    &..Seg.Foot, &..Seg.Talus   
  };    
  #include  "LegMusclePtrArrays.any"
  IncludeForces = pArrTargetMuscles;
  
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MPlantarFlexion=-Mlocal[2];
};

AnyForceMomentMeasure2 SubTalarEversionNetMomentMuscle = 
{
  AnyRefFrame &ref = ..Seg.Talus.SubTalarJoint;
  //RefPoint = &..Seg.Talus.SubTalarJoint;
  IncludeSegments = 
  {
    &..Seg.Foot   
  };  
  #include  "LegMusclePtrArrays.any"
  IncludeForces = pArrTargetMuscles;
  
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MSubTalarEversion=Mlocal[2];
};

AnyForceMomentMeasure2 AchillesTendonSubtalarEversion = {
  AnyRefNode &ref=..Seg.Talus.SubTalarJoint;
  AnySeg &seg=..Seg.Foot;      
  
  #ifndef LEG_WITH_JOINT_MUSCLES
  AnyFolder &mus1  = ..Mus.SoleusMedialis1;
  AnyFolder &mus2  = ..Mus.SoleusMedialis2;
  AnyFolder &mus3  = ..Mus.SoleusMedialis3;
  AnyFolder &mus4  = ..Mus.SoleusLateralis1;
  AnyFolder &mus5  = ..Mus.SoleusLateralis2;
  AnyFolder &mus6  = ..Mus.SoleusLateralis3;
  AnyFolder &mus7  = ..Mus.GastrocnemiusLateralis1;
  AnyFolder &mus8  = ..Mus.GastrocnemiusMedialis1;
  #endif
  
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MSubTalarEversion=Mlocal[2];
};

AnyForceMomentMeasure2 InversionMusclesSubtalarEversion = {
  AnyRefNode &ref=..Seg.Talus.SubTalarJoint;
  AnySeg &seg=..Seg.Foot;      
  
  #ifndef LEG_WITH_JOINT_MUSCLES
  AnyFolder &mus9  = ..Mus.FlexorDigitorumLongus1;
  AnyFolder &mus10  = ..Mus.FlexorDigitorumLongus2;
  AnyFolder &mus11  = ..Mus.FlexorDigitorumLongus3;
  AnyFolder &mus12  = ..Mus.FlexorHallucisLongus1;
  AnyFolder &mus13  = ..Mus.FlexorHallucisLongus2;
  AnyFolder &mus14  = ..Mus.FlexorHallucisLongus3;
  AnyFolder &mus15  = ..Mus.TibialisPosteriorLateralis1;
  AnyFolder &mus16  = ..Mus.TibialisPosteriorLateralis2;
  AnyFolder &mus17  = ..Mus.TibialisPosteriorLateralis3;
  AnyFolder &mus18  = ..Mus.TibialisPosteriorMedialis1;
  AnyFolder &mus19  = ..Mus.TibialisPosteriorMedialis2;
  AnyFolder &mus20  = ..Mus.TibialisPosteriorMedialis3;
  AnyFolder &mus21  = ..Mus.TibialisAnterior1;
  AnyFolder &mus22  = ..Mus.TibialisAnterior2;
  AnyFolder &mus23  = ..Mus.TibialisAnterior3;
  AnyFolder &mus33  = ..Mus.ExtensorDigitorumLongus1;
  AnyFolder &mus34  = ..Mus.ExtensorDigitorumLongus2;
  AnyFolder &mus35  = ..Mus.ExtensorDigitorumLongus3;
  AnyFolder &mus36  = ..Mus.ExtensorHallucisLongus1;
  AnyFolder &mus37  = ..Mus.ExtensorHallucisLongus2;
  AnyFolder &mus38  = ..Mus.ExtensorHallucisLongus3;
  #endif
  
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MSubTalarEversion=Mlocal[2];
};

AnyForceMomentMeasure2 EversionMusclesSubtalarEversion = {
  AnyRefNode &ref=..Seg.Talus.SubTalarJoint;
  AnySeg &seg=..Seg.Foot;      
  
  #ifndef LEG_WITH_JOINT_MUSCLES
  #if BM_LEG_MORPHOLOGY == 1
  AnyFolder &mus30  = ..Mus.PeroneusTertius1;
  AnyFolder &mus31  = ..Mus.PeroneusTertius2;
  AnyFolder &mus32  = ..Mus.PeroneusTertius3;
  #endif
  AnyFolder &mus24  = ..Mus.PeroneusBrevis1;
  AnyFolder &mus25  = ..Mus.PeroneusBrevis2;
  AnyFolder &mus26  = ..Mus.PeroneusBrevis3;
  AnyFolder &mus27  = ..Mus.PeroneusLongus1;
  AnyFolder &mus28  = ..Mus.PeroneusLongus2;
  AnyFolder &mus29  = ..Mus.PeroneusLongus3;
  #endif
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MSubTalarEversion=Mlocal[2];
};

AnyForceMomentMeasure2 KneeNetMomentMuscle = 
{
  AnyRefFrame &ref = ..Seg.Thigh.KneeJoint.RotNode;
  //RefPoint = &..Seg.Thigh.KneeJoint.RotNode; 
  IncludeSegments = 
  {
    &..Seg.Shank, &..Seg.Foot, &..Seg.Talus    
  };   
  AnyObjectPtrArray pArrJntReactions = 
  {
    &..Jnt.PatellaMovement.Reaction
  };  
  #include  "LegMusclePtrArrays.any"
  IncludeForces = arrcat(pArrTargetMuscles,pArrJntReactions);   
  
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MKneeFlexion=Mlocal[2];
};

AnyFolder &HipNodeRef =.Seg.Pelvis;
HipNodeRef={
  HipJoint ={
    AnyRefNode ISBCoor={
      ARel=....Trunk.SegmentsLumbar.PelvisSeg.AnatomicalFrame.ARel;
      AnyRefNode RotNode={
        ARel= {{...Sign*-1,0,0},{0,...Sign*-1,0},{0,0,1}};
      };
    };
  };
};

AnyForceMomentMeasure2 HipNetMomentMuscle = 
{
  AnyRefFrame &ref = ..Seg.Pelvis.HipJoint.ISBCoor.RotNode;
  //RefPoint = &..Seg.Pelvis.HipJoint.ISBCoor.RotNode;
  IncludeSegments = 
  {
    &..Seg.Shank, &..Seg.Foot, &..Seg.Thigh, &..Seg.Talus, &..Seg.Patella    
  };  
  #include  "LegMusclePtrArrays.any"
  IncludeForces = pArrTargetMuscles;
  
  AnyVec3 Mlocal=M*ref.Axes;
  AnyVar MHipAbduction=Mlocal[0];
  AnyVar MHipFlexion=Mlocal[2];
  AnyVar MHipExternalRotation=Mlocal[1];
};

//****************************************************************************

AnyForceMomentMeasure2 AnklePlantarFlexionNetMoment = 
{
  AnyRefNode &ref = ..Seg.Shank.AnkleJoint;
  //RefPoint = &..Seg.Shank.AnkleJoint;
  IncludeSegments = 
  {
    &..Seg.Foot, &..Seg.Talus    
  };
  AnyObjectPtrArray pArrJntReactions = 
  {
    &..Jnt.Ankle.Constraints.Reaction, 
    &..Jnt.SubTalar.Constraints.Reaction
  };
  #include  "LegMusclePtrArrays.any"
  IncludeForces = arrcat(pArrTargetMuscles,pArrJntReactions);
};

AnyForceMomentMeasure2 SubTalarEversionNetMoment = 
{
  AnyRefFrame &ref = ..Seg.Talus.SubTalarJoint;
  //RefPoint = &..Seg.Talus.SubTalarJoint;
  IncludeSegments = 
  {
    &..Seg.Foot    
  };
  AnyObjectPtrArray pArrJntReactions = 
  {
    &..Jnt.SubTalar.Constraints.Reaction
  };
  #include  "LegMusclePtrArrays.any"
  IncludeForces = arrcat(pArrTargetMuscles,pArrJntReactions);  
};

AnyForceMomentMeasure2 KneeNetMoment = 
{
  AnyRefFrame &ref = ..Seg.Thigh.KneeJoint.RotNode;
  //RefPoint = &..Seg.Thigh.KneeJoint.RotNode;
  IncludeSegments = 
  {
    &..Seg.Shank, &..Seg.Foot, &..Seg.Talus    
  };
  AnyObjectPtrArray pArrJntReactions = 
  {
    &..Jnt.Knee.Constraints.Reaction, 
    &..Jnt.Ankle.Constraints.Reaction, 
    &..Jnt.SubTalar.Constraints.Reaction, 
    &..Jnt.PatellaMovement.Reaction
  };  
  #include  "LegMusclePtrArrays.any"
  IncludeForces = arrcat(pArrTargetMuscles,pArrJntReactions);  
};

AnyForceMomentMeasure2 HipNetMoment = 
{
  AnyRefFrame &ref = ..Seg.Pelvis.HipJoint.RotNode;
  //RefPoint = &..Seg.Pelvis.HipJoint.RotNode;
  IncludeSegments = 
  {
    &..Seg.Shank, &..Seg.Foot, &..Seg.Thigh, &..Seg.Patella, &..Seg.Talus    
  };
  AnyObjectPtrArray pArrJntReactions = 
  {
    &..Jnt.Hip.Constraints.Reaction,
    &..Jnt.Knee.Constraints.Reaction,
    &..Jnt.Ankle.Constraints.Reaction,
    &..Jnt.SubTalar.Constraints.Reaction,
    &..Jnt.PatellaFemur.Constraints.Reaction,
    &..Jnt.PatellaMovement.Reaction
  };  
  #include  "LegMusclePtrArrays.any"
  IncludeForces = arrcat(pArrTargetMuscles,pArrJntReactions);   
};

AnyForceMomentMeasure KneeJointReactionMoments = 
{
  AnyForceBase &Force = ..Jnt.Knee.Constraints.Reaction;
  AnyRefFrame &Thighknee = ..Seg.Thigh.KneeJoint;
  AnyVec3 Mlocal = M*Thighknee.Axes;
};

AnyForceMomentMeasure AnkleJointReactionMoments = 
{
  AnyForceBase &Force = ..Jnt.Ankle.Constraints.Reaction;
  AnyRefFrame &Shankankle = ..Seg.Shank.AnkleJoint;
  AnyVec3 Mlocal = M*Shankankle.Axes;
};