AnyFolder LegCal6={
  
  AnyFolder &Pelvis = ...BodyModel.Trunk.SegmentsLumbar.PelvisSeg;
  AnyFolder &Sacrum = ...BodyModel.Trunk.SegmentsLumbar.SacrumSeg;
  
  AnyFolder &LegSegSide = .SideHumanFolderRef.Leg.Seg;
  
  AnyFolder &LegJointsSide = .SideHumanFolderRef.Leg.Jnt;
  AnyFolder &SacrumPelvisJnt = ...BodyModel.Trunk.JointsLumbar.SacrumPelvisJnt;

  
  AnyFixedRefFrame ground = {
    AnyRefNode node = { 
      ARel=..Pelvis.Axes0;
      sRel=..Pelvis.r0;
    };
  };
  
  AnyKinEqDriver PelvisFix = {
    AnyKinLinear PelvisSacrumLinFix = {
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....BodyModel.Trunk.SegmentsLumbar.PelvisSeg;
    };
    AnyKinRotational PelvisSacrumRotFix = {
      Type = RotAxesAngles;
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....BodyModel.Trunk.SegmentsLumbar.PelvisSeg;
    };
  };
  
  AnyKinEqSimpleDriver PelvisSacrumFix = {
    AnyKinMeasure& Jnt = .SacrumPelvisJnt;
    DriverPos = {0,0,0};
    DriverVel = {0,0,0};
    Reaction.Type = {Off,Off,Off}; 
  }; 

    AnyKinEqSimpleDriver Hip = {
    AnyKinRotational &Spherical = ..SideInterfaceFolderRef.HipFlexion.HipMeasure;
    DriverPos = {50,10,0}*pi/180; // Flexion - Abduction - external rotation 
    DriverVel = {0,0,0}*pi/180;
    Reaction.Type = {Off,Off,Off};
  };
    
  AnyKinEqSimpleDriver Knee = {
    AnyKinMeasure& Hinge = ..SideInterfaceFolderRef.KneeFlexion;
    DriverPos = {80*pi/180};
    DriverVel = {0*pi/180};
    Reaction.Type = {Off};
  };
    
  #if BM_FOOT_MODEL == _FOOT_MODEL_DEFAULT_
  AnyKinEqSimpleDriver Ankle = {
    AnyKinMeasure& Hinge = ..SideInterfaceFolderRef.AnklePlantarFlexion;
    DriverPos = {-5*pi/180}; 
    DriverVel = {0*pi/180};
    Reaction.Type = {Off};
  };

  AnyKinEqSimpleDriver SubTalar = {
    AnyKinMeasure& Hinge = ..SideInterfaceFolderRef.SubTalarEversion;
    DriverPos = {0*pi/180};
    DriverVel = {0*pi/180}; 
    Reaction.Type = {Off};
  };
  #endif

  AnyString MusPrefix = CompleteNameOf(&.SideHumanFolderRef.Leg.Mus);
  
  AnyObjectPtrArray GluteusMinimusMuscles = ObjSearch(MusPrefix+".GluteusMinimus*", "AnyMuscle");  
  AnyObjectPtrArray GluteusMediusMuscles = ObjSearch(MusPrefix+".GluteusMedius*", "AnyMuscle");
  AnyObjectPtrArray SoleusMuscles = ObjSearch(MusPrefix+".Soleus*", "AnyMuscle");
  AnyObjectPtrArray GemellusInferiorMuscles = ObjSearch(MusPrefix+".Gemellus*", "AnyMuscle");
  AnyObjectPtrArray TensorFasciaeLataeMuscles = ObjSearch(MusPrefix+".TensorFasciaeLatae*", "AnyMuscle");
};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy LegCalibrationStudy6 = {
  AnyFolder &ref=.LegCal6; 
  nStep = 1;
  MuscleArr = arrcat(
                ref.GluteusMinimusMuscles,
                ref.GluteusMediusMuscles,
                ref.SoleusMuscles,
                ref.GemellusInferiorMuscles,
                ref.TensorFasciaeLataeMuscles
             );
  InitialConditions.SmallStepAssumptionOnOff = Off;
  Kinematics.SmallStepAssumptionOnOff = Off;
  Kinematics.PosAnalysisOnlyOnOff = On;
  InitialConditions.PosAnalysisOnlyOnOff = On;
};
