AnyFolder GastrocnemiusCal ={
  AnyComponentDefinition obj = {};  

  AnyFolder &Pelvis = ...HumanFolderRef.Trunk.Segments.PelvisSeg;
  AnyFolder &Sacrum = ...HumanFolderRef.Trunk.Segments.SacrumSeg;
  
  AnyFolder &LegSegSide = .SideHumanFolderRef.Leg.Seg;
  
  AnyFolder &LegJointsSide = .SideHumanFolderRef.Leg.Jnt;
  AnyFolder &SacrumPelvisJnt = ...HumanFolderRef.Trunk.Joints.Lumbar.SacrumPelvis;
  
  
  AnyFixedRefFrame ground = {
    AnyRefNode node = {
      ARel=..Pelvis.Axes0;
      sRel=..Pelvis.r0;
    };
  };
  
  AnyKinEq PelvisFix = {
    AnyKinLinear PelvisSacrumLinFix = {
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....HumanFolderRef.Trunk.Segments.PelvisSeg;
    };
    AnyKinRotational PelvisSacrumRotFix = {
      Type = RotAxesAngles;
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....HumanFolderRef.Trunk.Segments.PelvisSeg;
    };
  };
  
  AnyKinEqSimpleDriver PelvisSacrumFix = {
    AnyKinMeasure& Jnt = .SacrumPelvisJnt;
    DriverPos = {0,0,0};
    DriverVel = {0,0,0};
    Reaction.Type = {Off,Off,Off};
  };
  
  AnyKinEqInterPolDriver Hip = {
    AnyKinRotational &Spherical = ..SideInterfaceFolderRef.HipFlexion.HipMeasure;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0 };
    Data = { {5,  -7},
             {0,  0},
             {0,  0}
           }*pi/180; // Flexion - Abduction - internal rotation 
    
    Reaction.Type = {Off, Off, Off};
  };
  
  AnyKinEqInterPolDriver Knee = {
    AnyKinMeasure& Hinge = ..SideInterfaceFolderRef.KneeFlexion;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0 };
    Data = {{150,0}}*pi/180; // ? degrees knee flexion
    Reaction.Type = {Off};
  };
  
  
  AnyKinEqInterPolDriver Ankle = {
    AnyKinMeasure& Hinge = ..SideInterfaceFolderRef.AnklePlantarFlexion;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0 };
    Data = {{50,-20}}*pi/180;
    Reaction.Type = {Off};
  };
  
  AnyKinEqInterPolDriver SubTalar = {
    AnyKinMeasure& Hinge = ..SideInterfaceFolderRef.SubTalarEversion;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0 };
    Data = {{0,0}}*pi/180;
    Reaction.Type = {Off};
  };

  
  AnyString MusPrefix = CompleteNameOf(&.SideHumanFolderRef.Leg.Mus);
  
  AnyObjectPtrArray GastrocnemiusMuscles = ObjSearch(MusPrefix+".Gastrocnemius*", "AnyMuscle");
  AnyFloat  GastrocnemiusRmin = repmat(NumElemOf(GastrocnemiusMuscles), 0.5);
  AnyFloat  GastrocnemiusRmax = repmat(NumElemOf(GastrocnemiusMuscles), 1.2);
  
  AnyObjectPtrArray PlantarisMuscles = ObjSearch(MusPrefix+".Plantaris*", "AnyMuscle");
  AnyFloat  PlantarisRmin = repmat(NumElemOf(PlantarisMuscles), 0.5);
  AnyFloat  PlantarisRmax = repmat(NumElemOf(PlantarisMuscles), 1.2);

  AnyObjectPtrArray PopliteusMuscles = ObjSearch(MusPrefix+".Popliteus*", "AnyMuscle");
  AnyFloat  PopliteusRmin = repmat(NumElemOf(PopliteusMuscles), 0.5);
  AnyFloat  PopliteusRmax = repmat(NumElemOf(PopliteusMuscles), 1.2);

  
};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy LegCalibrationStudyGastrocnemius = {
  AnyFolder &ref=.GastrocnemiusCal; 
  nStep = 2;
  MuscleArr = arrcat(
      ref.GastrocnemiusMuscles,
      ref.PlantarisMuscles,
      ref.PopliteusMuscles
  );
  RminMuscleFiber = arrcat(
      ref.GastrocnemiusRmin,
      ref.PlantarisRmin,
      ref.PopliteusRmin
  );
  RmaxMuscleFiber = arrcat(
      ref.GastrocnemiusRmax,
      ref.PlantarisRmax,
      ref.PopliteusRmax
  );
  InitialConditions.SolverType = KinSolOverDeterminate;
};

