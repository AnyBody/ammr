/**
Definitions of the Joints in the lumbar spine. From PelvisL5 to T12L1
*/
AnyFolder JointsLumbar = {
  
  
  AnySphericalJoint SacrumPelvisJnt = {
    AnyRefNode &PelvisNode = ..SegmentsLumbar.PelvisSeg.PelvisSacrumJntNode;
    AnyRefNode &SacrumNode = ..SegmentsLumbar.SacrumSeg.SacrumPelvisJntNode;
    
  };
  //There is no muscles attached to sacrum 
  AnyReacForce SacrumPelvisReaction = {
    AnySphericalJoint &ref=.SacrumPelvisJnt ;
  };
  
  AnySphericalJoint L5SacrumJnt = {
    AnyRefNode &SacrumNode = ..SegmentsLumbar.SacrumSeg.SacrumL5JntNode;
    AnyRefNode &L5Node = ..SegmentsLumbar.L5Seg.L5SacrumJntNode;
  };  
  
  AnySphericalJoint L4L5Jnt = {
    AnyRefNode &L5Node = ..SegmentsLumbar.L5Seg.L4L5JntNode;
    AnyRefNode &L4Node = ..SegmentsLumbar.L4Seg.L4L5JntNode;
  };
  
  AnySphericalJoint L3L4Jnt = {
    AnyRefNode &L4Node = ..SegmentsLumbar.L4Seg.L3L4JntNode;
    AnyRefNode &L3Node = ..SegmentsLumbar.L3Seg.L3L4JntNode;
  };
  
  AnySphericalJoint L2L3Jnt = {
    AnyRefNode &L3Node = ..SegmentsLumbar.L3Seg.L2L3JntNode;
    AnyRefNode &L2Node = ..SegmentsLumbar.L2Seg.L2L3JntNode;
  };
  
  AnySphericalJoint L1L2Jnt = {
    AnyRefNode &L2Node = ..SegmentsLumbar.L2Seg.L1L2JntNode;
    AnyRefNode &L1Node = ..SegmentsLumbar.L1Seg.L1L2JntNode;
  };
  
  AnySphericalJoint T12L1Jnt = {
    AnyRefNode &L1Node = ..SegmentsLumbar.L1Seg.T12L1JntNode;
    AnyRefNode &T12Node = ..SegmentsThorax.ThoraxSeg.T12L1JntNode;
  };
  
  
  
  //  //There is no muscles attached to skull 
  //  AnyReacForce NeckJntReaction = {
  //    AnyRevoluteJoint &ref=.NeckJnt;
  //  };
  
  
  
  //Spine rhythm leaves three rotational dof between pelvis and thorax free 
  AnyKinEqSimpleDriver SpineRhythmDrv = {
    
    AnyKinMeasureLinComb Measure = {
      
      AnyJoint &u1 = ..SacrumPelvisJnt;
      AnyJoint &u2 = ..L5SacrumJnt;
      AnyJoint &u3 = ..L4L5Jnt;
      AnyJoint &u4 = ..L3L4Jnt;
      AnyJoint &u5 = ..L2L3Jnt;
      AnyJoint &u6 = ..L1L2Jnt;
      AnyJoint &u7 = ..T12L1Jnt;
      #include "..\Trunk\SRMatrixes.any"            
    }; // Measure
    
    DriverPos = {0,0,0, 0,0,0, 0,0,0, 0,0,0, 0,0,0, 0,0,0};
    DriverVel = {0,0,0, 0,0,0, 0,0,0, 0,0,0, 0,0,0, 0,0,0};
    Reaction.Type = {Off,Off,Off, Off,Off,Off, Off,Off,Off, Off,Off,Off, Off,Off,Off, Off,Off,Off};
    
  }; // SpineRhythmDrv
  
  //Rotational measure between throax and pelvis
  AnyKinRotational PelvisThoraxRotMeasure = {
    AnyRefFrame &Pelvis = ..SegmentsLumbar.PelvisSeg;
    AnyRefFrame &Thorax = ..SegmentsThorax.ThoraxSeg;  
    Type = RotVector;
    AngVelOnOff = On;
  };  
  
  
  
};// End of JointsLumbar




AnyFolder JointsCervicalSpine = {
  
  AnySphericalJoint T1C7Jnt = {
    AnyRefNode &T1Node = ..SegmentsThorax.ThoraxSeg.T1C7JntNode;
    AnyRefNode &C7Node = ..SegmentsCervicalSpine.C7Seg.T1C7JntNode;
  };
  
  AnySphericalJoint C7C6Jnt = {
    AnyRefNode &C7Node = ..SegmentsCervicalSpine.C7Seg.C7C6JntNode;
    AnyRefNode &C6Node = ..SegmentsCervicalSpine.C6Seg.C7C6JntNode;
  };
  
  AnySphericalJoint C6C5Jnt = {
    AnyRefNode &C6Node = ..SegmentsCervicalSpine.C6Seg.C6C5JntNode;
    AnyRefNode &C5Node = ..SegmentsCervicalSpine.C5Seg.C6C5JntNode;
  };
  
  AnySphericalJoint C5C4Jnt = {
    AnyRefNode &C5Node = ..SegmentsCervicalSpine.C5Seg.C5C4JntNode;
    AnyRefNode &C4Node = ..SegmentsCervicalSpine.C4Seg.C5C4JntNode;
  };
  
  AnySphericalJoint C4C3Jnt = {
    AnyRefNode &C4Node = ..SegmentsCervicalSpine.C4Seg.C4C3JntNode;
    AnyRefNode &C3Node = ..SegmentsCervicalSpine.C3Seg.C4C3JntNode;
  };
  
  AnySphericalJoint C3C2Jnt = {
    AnyRefNode &C3Node = ..SegmentsCervicalSpine.C3Seg.C3C2JntNode;
    AnyRefNode &C2Node = ..SegmentsCervicalSpine.C2Seg.C3C2JntNode;
  };
  
  AnyRevoluteJoint C2C1Jnt = {
    Axis = y;
    AnyRefNode &C2Node = ..SegmentsCervicalSpine.C2Seg.C2C1JntNode;
    AnyRefNode &C1Node = ..SegmentsCervicalSpine.C1Seg.C2C1JntNode;
  };
  
  AnyRevoluteJoint C1C0Jnt = {
    Axis = z;
    AnyRefNode &C1Node = ..SegmentsCervicalSpine.C1Seg.C1C0JntNode;
    AnyRefNode &C0Node = ..SegmentsThorax.SkullSeg.C1C0JntNode;
  };
  
  
  AnyKinRotational SkullThoraxRotMeasure = {
    AnyRefFrame &Skull = ..SegmentsThorax.SkullSeg; 
    AnyRefFrame &Thorax = ..SegmentsThorax.ThoraxSeg;  
    Type = RotVector;
    AngVelOnOff = On;
  };
  
  
  // CervicalRhythm
  
  AnyFolder Flexion = {
    
    AnyKinMeasureOrg C3C2JntFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..C3C2Jnt;
    };  
    
    AnyKinMeasureOrg C4C3JntFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..C4C3Jnt;
    };  
    
    AnyKinMeasureOrg C5C4JntFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..C5C4Jnt;
    };  
    
    AnyKinMeasureOrg C6C5JntFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..C6C5Jnt;
    };  
    
    AnyKinMeasureOrg C7C6JntFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..C7C6Jnt;
    };  
    
    AnyKinMeasureOrg T1C7JntFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..T1C7Jnt;
    };  
    
    AnyKinEqSimpleDriver CervicalFlexionRhythmDrv = {
      
      AnyKinMeasureLinComb Measure = {
        
        AnyKinMeasure &u0 = ..SkullThoraxFlexion;
        AnyKinMeasure &u1 = ...C1C0Jnt;
        AnyKinMeasure &u2 = ..C3C2JntFlexion;
        AnyKinMeasure &u3 = ..C4C3JntFlexion;
        AnyKinMeasure &u4 = ..C5C4JntFlexion;
        AnyKinMeasure &u5 = ..C6C5JntFlexion;
        AnyKinMeasure &u6 = ..C7C6JntFlexion;
        AnyKinMeasure &u7 = ..T1C7JntFlexion;
        OutDim = 6;
        Const = {0, 0, 0, 0, 0, 0};
        Coef = {
          {40/122, 1, 0, 0, 0, 0, 0, 0},
          {0, 0, 1, -1, 0, 0,  0,  0},
          {0, 0, 0, 1, -1, 0,  0,  0},
          {0, 0, 0, 0, 1, -1,  0,  0},
          {0, 0, 0, 0, 0,  1, -1,  0},
          {0, 0, 0, 0, 0,  0,  1, -1}};
        
      }; // Measure
      
      DriverPos = {0, 0, 0, 0, 0, 0};
      DriverVel = {0, 0, 0, 0, 0, 0};
      Reaction.Type = {Off,Off,Off, Off,Off,Off};
      
    }; // CervicalFlexionRhythmDrv
    
    AnyKinMeasureOrg SkullThoraxFlexion  = {
      MeasureOrganizer = {2};
      AnyKinMeasure &ref = ..SkullThoraxRotMeasure;
    };
    
  };//Flexion
  
  
  AnyFolder LateralBending = {
    
    AnyKinMeasureOrg C3C2JntLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..C3C2Jnt;
    };  
    
    AnyKinMeasureOrg C4C3JntLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..C4C3Jnt;
    };  
    
    AnyKinMeasureOrg C5C4JntLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..C5C4Jnt;
    };  
    
    AnyKinMeasureOrg C6C5JntLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..C6C5Jnt;
    };  
    
    AnyKinMeasureOrg C7C6JntLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..C7C6Jnt;
    };  
    
    AnyKinMeasureOrg T1C7JntLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..T1C7Jnt;
    };  
    
    AnyKinEqSimpleDriver CervicalLateralBendingRhythmDrv = {
      
      AnyKinMeasureLinComb Measure = {
        
        AnyKinMeasure &u2 = ..C3C2JntLateralBending;
        AnyKinMeasure &u3 = ..C4C3JntLateralBending;
        AnyKinMeasure &u4 = ..C5C4JntLateralBending;
        AnyKinMeasure &u5 = ..C6C5JntLateralBending;
        AnyKinMeasure &u6 = ..C7C6JntLateralBending;
        AnyKinMeasure &u7 = ..T1C7JntLateralBending;
        OutDim = 5;
        Const = {0, 0, 0, 0, 0};
        Coef = {
          {1, -1, 0, 0,  0,  0},
          {0, 1, -1, 0,  0,  0},
          {0, 0, 1, -1,  0,  0},
          {0, 0, 0,  1, -1,  0},
          {0, 0, 0,  0,  1, -1}};
        
      }; // Measure
      
      DriverPos = {0, 0, 0, 0, 0};
      DriverVel = {0, 0, 0, 0, 0};
      Reaction.Type = {Off,Off,Off, Off,Off};
      
    }; // CervicalLateralBendingRhythmDrv
    
    AnyKinMeasureOrg SkullThoraxLateralBending  = {
      MeasureOrganizer = {0};
      AnyKinMeasure &ref = ..SkullThoraxRotMeasure;
    };
    
  };//LateralBending
  
  
  
  AnyFolder Rotation = {
    
    AnyKinMeasureOrg C3C2JntRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..C3C2Jnt;
    };  
    
    AnyKinMeasureOrg C4C3JntRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..C4C3Jnt;
    };  
    
    AnyKinMeasureOrg C5C4JntRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..C5C4Jnt;
    };  
    
    AnyKinMeasureOrg C6C5JntRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..C6C5Jnt;
    };  
    
    AnyKinMeasureOrg C7C6JntRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..C7C6Jnt;
    };  
    
    AnyKinMeasureOrg T1C7JntRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..T1C7Jnt;
    };  
    
    AnyKinEqSimpleDriver CervicalRotationRhythmDrv = {
      
      AnyKinMeasureLinComb Measure = {
        
        AnyKinMeasure &u0 = ..SkullThoraxRotation;
        AnyKinMeasure &u1 = ...C2C1Jnt;
        AnyKinMeasure &u2 = ..C3C2JntRotation;
        AnyKinMeasure &u3 = ..C4C3JntRotation;
        AnyKinMeasure &u4 = ..C5C4JntRotation;
        AnyKinMeasure &u5 = ..C6C5JntRotation;
        AnyKinMeasure &u6 = ..C7C6JntRotation;
        AnyKinMeasure &u7 = ..T1C7JntRotation;
        OutDim = 6;
        Const = {0, 0, 0, 0, 0, 0};
        Coef = {
          {77/132, 1, 0, 0, 0, 0, 0, 0},
          {0, 0, 1, -1, 0, 0,  0,  0},
          {0, 0, 0, 1, -1, 0,  0,  0},
          {0, 0, 0, 0, 1, -1,  0,  0},
          {0, 0, 0, 0, 0,  1, -1,  0},
          {0, 0, 0, 0, 0,  0,  1, -1}};
        
      }; // Measure
      
      DriverPos = {0, 0, 0, 0, 0, 0};
      DriverVel = {0, 0, 0, 0, 0, 0};
      Reaction.Type = {Off,Off,Off, Off,Off,Off};
      
    }; // CervicalRotationRhythmDrv
    
    AnyKinMeasureOrg SkullThoraxRotation  = {
      MeasureOrganizer = {1};
      AnyKinMeasure &ref = ..SkullThoraxRotMeasure;
    };
    
  };//Rotation
  
  
}; //End of JointsCervicalSpine

