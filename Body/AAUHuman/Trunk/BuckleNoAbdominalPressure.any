//This is the main file which is used to simulate the mechanical effect of intra-abdominal pressure.
//The model automatically finds the optimal abdominal pressure that will reduce the muscle activities.
//
//The abdominal volume is measure using a number of cylinders. The abdominal model can generate forces to 
//the rest of the model according to the way the volume is measured. This means that all measures in
//the volume may generate a force, since we have an abdominal muscle working on the volume.
//  
//The abdominal muscles wraps an abdominal-like shape which means that the effective area they have on 
//the abdominal volume comes automatically. This would otherwise be very difficult to determine.
//
//We have compared the model with other models in the litterature and it seems to be the only model
//where the forces that arise from the pressure between the abdominal volume and the frontal side of 
//the vertebrae is taken into account.  We are working on validating the model towards experiments found 
//in the litterature.

AnyFolder Buckle={
  
  //Settings which is used to control the geometry of the abdominal volume
  AnyVar  B=0.08;//0.07; //frontal distance between cylinders
  AnyVar  R=0.04; //radius of circles  
  AnyVar  Delta=0.015;//saggital plane distance between cylinders
  AnyVar  CylLength =0.35 ; //Lenght of cylinders
  AnyVar  A=0.05; //Backwardss offset of cylinders in the saggital plane
  AnyVec3 CylRGB={0.6,0.6,0.6};
  AnyVar  CylOpacity =0;
  AnyVar  CylCapRatio=0.4;
  AnyVar  Ratio=0.5;
  
    //References
  AnyFolder &PelvisRef=.SegmentsLumbar.PelvisSeg;
  AnyFolder &ThoraxRef=.SegmentsThorax.ThoraxSeg;
    
  AnyFolder Segments ={
      
    AnySeg BuckleSegFront ={
      Mass=0;
      Jii={0,0,0};
      
      AnyRefNode RightSideBase ={        sRel={-...R,0,...B};      };
      AnyRefNode LeftSideBase ={         sRel={-...R,0,-...B};      };
   //   AnyRefNode CenterBase ={           sRel={-...R,0,0};      };
      AnyRefNode CenterBase ={           sRel={...Delta,0,0};      };
      
      //Attachment points for muscles 
      
      //obliquusexternusleft
      AnyRefNode OEC7_RSNodeL ={      sRel={.0000, 0.033982, -0.015};      };
      AnyRefNode OEC8_RSNodeL={        sRel={0.0000, 0.014875, -0.015};      };
      AnyRefNode OEC9_RSNodeL={        sRel={0.0000, -0.006945, -0.015};      };
      AnyRefNode OEC10_RSNodeL={       sRel={0.0000, -0.030993, -0.015};       };
      
      //obliquusexternusright
      AnyRefNode  OEC7_RSNodeR={        sRel={-0.0000, 0.033982, 0.015};      };
      AnyRefNode OEC8_RSNodeR={        sRel={-0.0000, 0.014875, 0.015};      };
      AnyRefNode OEC9_RSNodeR={        sRel={-0.0000, -0.006945,0.015};      };
      AnyRefNode OEC10_RSNodeR={        sRel={-0.0000, -0.030993, 0.015};     };
      
      //obliquusinternusleft
      AnyRefNode  OICI_RS1NodeL={        sRel={.0000, 0.033982, -0.015};      };
      AnyRefNode OICI_RS2NodeL={        sRel={0.0000, 0.014875, -0.015};      };
      AnyRefNode OICI_RS3NodeL={        sRel={0.0000, -0.006945, -0.015};      };
      
      //obliquusinternusright
      AnyRefNode OICI_RS1NodeR={        sRel={-0.0000, 0.033982, 0.015};      };
      AnyRefNode OICI_RS2NodeR={        sRel={-0.0000, 0.014875,  0.015};      };
      AnyRefNode OICI_RS3NodeR ={        sRel={-0.0000, -0.006945,  0.015};      };
      
      //Rectus abdominis Right side
      AnyRefNode RACP_CO5NodeR ={        sRel={-0.0000, 0.0, 0.015};      };
      AnyRefNode RACP_CO6NodeR={        sRel={-0.0000, 0.0, 0.015};      };
      AnyRefNode RACP_CO7NodeR={        sRel={-0.0000, 0.0, 0.015};      };
      
      //Rectus abdominis Left side
      AnyRefNode RACP_CO5NodeL ={       sRel={0.0000, 0.0,-0.015};      };
      AnyRefNode RACP_CO6NodeL={        sRel={0.0000, 0.0,-0.015};      };
      AnyRefNode RACP_CO7NodeL={        sRel={0.000, 0.0,-0.015};      };
      AnyRefNode TransversusL1={        sRel={0.0000, 0.1,-0.0};      };
      AnyRefNode TransversusL2={        sRel={0.0000, 0.06,-0.0};      };
      AnyRefNode TransversusL3={        sRel={0.0000, 0.02,-0.0};      };
      AnyRefNode TransversusL4={        sRel={0.0, -0.01,0.0};      };
      AnyRefNode TransversusL5={        sRel={0.0, -0.05,0.0};      };
      
    }; //Bucklesegfront
    
    
    AnySeg BuckleSeg ={
      Mass=0;
      Jii={0,0,0};
      AnyRefNode RightSideBase ={        sRel={-...R-...Delta,0,...B};      };
      AnyRefNode LeftSideBase ={        sRel={-...R-...Delta,0,-...B};      };
//      AnyRefNode CenterBase ={        sRel={-...R-...Delta,0,0};      };
      AnyRefNode CenterBase ={        sRel={0,0,0};      };
      AnyRefNode BackSideOfCyl={        sRel={-...R-...Delta,0,0};      };
    }; //Buckle seg
    
  };//end segments
  
  
  //This folder contains a set of additional dummy segments with corresponding 
  //drivers 
  //The segments are rigidly connected to various places where the buckle segment 
  //connects with pelvis, thorax and the vertebraes. The reaction in the joints 
  //can be seen as artificial forcetransducers 
  //In other words the only purpose for adding these segments are to be able to measure forces which the 
  //abdominal volume creates on the model
  AnyFolder ForceTransducers={
    
    //Transducer mounted in the center of the buckle
    AnySeg BuckleSegFT ={
      Mass=0;
      Jii={0,0,0};  
    };
    
    AnyKinLinear BuckleSegLin={
      AnySeg &ref1=..Segments.BuckleSeg.CenterBase;
      AnyFolder &L1 = .BuckleSegFT;
      Ref=0;
    };
    AnyKinRotational BuckleSegRot={
      AnySeg &ref1=..Segments.BuckleSeg.CenterBase;
      AnyFolder &L1 = .BuckleSegFT;
      Type=RotVector;
    };
    AnyKinEq BuckleKinEq={
      AnyKinLinear &ref1=.BuckleSegLin;
      AnyKinRotational &ref2=.BuckleSegRot;
    };
    
    
    //Transducer mounted on the point on pelvis which is used for controlling 
    //the position of the buckle
    AnySeg PelvisBuckleTransducer ={
      Mass=0;
      Jii={0,0,0};
    };
    AnyKinLinear PelvisBuckleTransducerLin={
      AnyRefNode &ref1 =..PelvisRef.BuckleNodeBottomCenter;
      AnyFolder  &ref2 = .PelvisBuckleTransducer;
      Ref=0;
    };
    AnyKinRotational PelvisBuckleTransducerRot={
      AnyRefNode &ref1=..PelvisRef.BuckleNodeBottomCenter;
      AnyFolder &L1 = .PelvisBuckleTransducer;
      Type=RotVector;
      Ref=0;
    };  
    AnyKinEq PelvisBuckleTransducerKinEq={
      AnyKinLinear &ref1=.PelvisBuckleTransducerLin;
      AnyKinRotational &ref2=.PelvisBuckleTransducerRot;
    };
  
    //Transducer mounted on the point on thorax which is used for controlling 
    //the position of the buckle
    AnySeg ThoraxBuckleTransducer ={
      Mass=0;
      Jii={0,0,0};
    };
    AnyKinLinear ThoraxBuckleTransducerLin={
      AnyRefNode &ref1=..ThoraxRef.BuckleNodeTopCenter;
      AnyFolder &r5ef2 = .ThoraxBuckleTransducer;
      Ref=0;
    };
    AnyKinRotational ThoraxBuckleTransducerRot={
      AnyRefNode &ref1=..ThoraxRef.BuckleNodeTopCenter;
      AnyFolder &L1 = .ThoraxBuckleTransducer;
      Type=RotVector;
      Ref=0;
    };
    AnyKinEq ThoraxBuckleTransducerKinEq={
      AnyKinLinear &ref1=.ThoraxBuckleTransducerLin;
      AnyKinRotational &ref2=.ThoraxBuckleTransducerRot;
    };
         
    
    //This file will draw the reactions from the buckle to the spine, thorax and pelvis
    //#include "DrawReactions.any"
    
    
  };//End forcetransducers
  
  
  
  
  AnyFolder AddOnNodes={
    
    AnyFolder &PelvisRef=..SegmentsLumbar.PelvisSeg;
    AnyFolder &ThoraxRef=..SegmentsThorax.ThoraxSeg;
    
    PelvisRef={
      AnyRefNode BuckleNodeBottomCenter ={
        sRel={0.11,-0.14,0.0};
      };
    };
    
    ThoraxRef={
      AnyRefNode BuckleNodeTopCenter ={
        sRel={0.15,0.20,0.0};
      };
    };
    
    
  };
  
  AnyFolder JointsAndDrivers={
    
    ///This is the linear measure between pelvis and thorax
    AnyKinLinear LinMeasure ={
      Ref=0; 
      AnyRefNode &ref1=..PelvisRef.BuckleNodeBottomCenter;
      AnyRefNode &ref2=..ThoraxRef.BuckleNodeTopCenter;
      AnyDrawPLine drw={Thickness=0.00375;RGB=Main.DrawSettings.Colors.AnyBodyRed;};
    };
    
    ///This is the rotational measure between pelvis and thorax
    AnyKinRotational RotMeasure ={
      Ref=0; 
      AnyRefNode &ref1=..PelvisRef.BuckleNodeBottomCenter;
      AnyRefNode &ref2=..ThoraxRef.BuckleNodeTopCenter;
      Type=RotVector; 
    };
    
    ///Linear measure from pelvis control point to center of buckle
    AnyKinLinear LinMeasureBuckle ={
      Ref=0; 
      AnyRefNode &ref1=..PelvisRef.BuckleNodeBottomCenter;
      AnyRefNode &ref2=..Segments.BuckleSeg;
    };
    
    ///This is the distance between two points on pelvis and thorax 
    AnyKinPLine H ={
      AnyRefNode &ref1=..ForceTransducers.PelvisBuckleTransducer;
      AnyRefNode &ref2=..ForceTransducers.ThoraxBuckleTransducer;
    };
    
    AnyKinPLine HalfH ={
      AnyRefNode &ref1=..PelvisRef.BuckleNodeBottomCenter;
      AnyRefNode &ref2=..Segments.BuckleSeg;
    };
    
    AnyKinMeasureOrg LinMeasureBuckleYZ ={
      AnyKinLinear &ref=.LinMeasureBuckle;
      MeasureOrganizer={1,2};
    };
    
    AnyKinRotational RotMeasureBuckle ={
      Ref=0; 
      AnyRefNode &ref1=..PelvisRef.BuckleNodeBottomCenter;
      AnyRefNode &ref2=..Segments.BuckleSeg;
      Type=RotVector; 
    };
    
    
    AnyKinMeasureLinComb LinComb = {
      AnyKinMeasure &u2 = .LinMeasure;
      AnyKinMeasure &u1 = .LinMeasureBuckle;
      Coef={
        {0.5,0,0,-1,0,0},
        {0,0.5,0,0,-1,0},
        {0,0,0.5,0,0,-1}
      };      
    };
    
    AnyKinMeasureLinComb RotComb = {
      AnyKinMeasure &u2 = .RotMeasure;
      AnyKinMeasure &u1 = .RotMeasureBuckle;
      Coef={
        {0.5,0,0,-1,0,0},
        {0,0.5,0,0,-1,0},
        {0,0,0.5,0,0,-1}
      };      
    };
    
    
    AnyKinEqSimpleDriver LinDrv={
      AnyKinMeasureLinComb &ref=.LinComb;
      DriverPos={0,0,0};
      DriverVel={0,0,0};
      Reaction.Type={0,0,0};
    };
    
    AnyKinEqSimpleDriver RotDrv={
      AnyKinMeasureLinComb &ref=.RotComb;
      DriverPos={0,0,0};
      DriverVel={0,0,0};
      Reaction.Type={0,0,0};
    };
    
    //************************************************
    //Definition and control of buckle seg front
    //This segment is located frontal to the buckle and
    //holds all the muscle attachment points
    //PrismaticJoint!
    //************************************************
    AnyPrismaticJoint BuckleSegBuckleSegFrontLinX={
      AnyRefNode &ref1=..ForceTransducers.BuckleSegFT;
      AnyRefNode &ref2=..Segments.BuckleSegFront.CenterBase;
      Axis=x;
      Ref=0;
    };
  
  };
  
  
  //************************************************
  //Definition and control of cylinders
  //************************************************
    
  AnyFolder TopCylindersFront={
    
    AnyFolder Right={ 
      #include "Cyl.any"
      AnyRefNode &Base=..Segments.BuckleSegFront.RightSideBase;
      AnyVar CylDir=-1;
    };
    AnyFolder Left={ 
      #include "Cyl.any"
      AnyRefNode &Base=..Segments.BuckleSegFront.LeftSideBase;
      AnyVar CylDir=-1;
    };
  };
  
  AnyFolder BottomCylindersFront={
    
    AnyFolder Right={ 
      #include "Cyl.any"
      AnyRefNode &Base=..Segments.BuckleSegFront.RightSideBase;
      AnyVar CylDir=1;
    };
    AnyFolder Left={ 
      #include "Cyl.any"
      AnyRefNode &Base=..Segments.BuckleSegFront.LeftSideBase;
      AnyVar CylDir=1;
    };
  };
  
  
  //Adding reactions to the buckle which normally has been carried by the abdonimal pressure. 
  //In this model a reaction between the buckle and pelvis and buckle and thorax
  //in x direction of the buckle, will be added 
  AnyFolder ReactionsInsteadOfAbdoninalPressure={
  
    ///This is the linear measure between pelvis and thorax
    AnyKinLinear BucklePelvisLin ={
      Ref=0; 
      AnyRefNode &ref1=..Segments.BuckleSeg;
      AnyRefNode &ref2=..PelvisRef.BuckleNodeBottomCenter;
    };

    AnyKinMeasureOrg BucklePelvisLinX={
    AnyKinLinear &ref=.BucklePelvisLin;
    MeasureOrganizer={0};
    };
     AnyKinLinear BuckleThoraxLin ={
      Ref=0; 
      AnyRefNode &ref1=..Segments.BuckleSeg;
      AnyRefNode &ref2=..ThoraxRef.BuckleNodeTopCenter;
    };
    AnyKinMeasureOrg BuckleThoraxLinX={
    AnyKinLinear &ref=.BuckleThoraxLin;
    MeasureOrganizer={0};
    };  
  
    AnyReacForce ReactionsInsteadOfAbdominalPressure={
      AnyKinMeasureOrg &ref1=.BuckleThoraxLinX;    
      AnyKinMeasureOrg &ref2=.BucklePelvisLinX;
    };

  };
  
  
  AnyKinEqSimpleDriver BucklePosition={
    AnyKinMeasureOrg &ref=.JointsAndDrivers.BuckleSegBuckleSegFrontLinX;
    DriverPos={.Delta};
    DriverVel={0.00};
    Reaction.Type={1};  
  };
  
  
  
};






