/*
File with joints and constraints for each rib
*/

AnySphericalJoint CostoVertebral = {
  AnyRefFrame &vertebra = .RotationConstraint.vertebra.CV;
  AnyRefFrame &rib = .RotationConstraint.rib.CV;
};

AnyKinEq RotationConstraint = {
  AnyIntVar RibIndex ??= 0; ///< Define which rib we are constructing for
  
  RigidBodyAverageMeasure CombinationMeasure(ROTATIONAL = On, LINEAR = Off) = {
    LocalReferenceFrame = &..CostoVertebral.rib;
    
    InputMeasures = {
      &.Measures.CostoTransverseSuperior,
      &.Measures.CostoTransverseInferior,
      &.Measures.InterCostalSuperior,
      &.Measures.InterCostalInferior,
      &.Measures.CostoChondral,     
    };
    WeightCoefficients = arrcat(
      repmat(2, -......Data.unscaled.ModelParameters.AllRibs.CostoTransverseWeight),
      -......Data.unscaled.ModelParameters.AllRibs.InterCostalWeight,
      ......Data.unscaled.ModelParameters.AllRibs.InterCostalWeight, // positive
      -......Data.unscaled.ModelParameters.AllRibs.CostoCartilage.Kx[.RibIndex - 1]
    );
  };
  

  MeasureOrganizer??={0,1,2};

  
  Reaction.Type ??= repmat(nDim, Off);
  CType ??= repmat(nDim, Hard);

  // Measures used by the constraint
  AnyFolder Measures = {
    AnyKinLinear CostoTransverseSuperior = {
      Ref = 0;
      AnyRefNode &Vertebra = ..vertebra.CTsup;
      AnyRefNode &Rib = ..rib.CTsup;
    };
    AnyKinLinear CostoTransverseInferior = {
      Ref = 0;
      AnyRefNode &Vertebra = ..vertebra.CTinf;
      AnyRefNode &Rib = ..rib.CTinf;
    };
    AnyKinLinear InterCostalSuperior = { 
      Ref = 0;
      AnyRefFrame &UpperRib = ..rib_above.ICSpringInfNode;
      AnyRefFrame &LowerRib = ..rib.ICSpringSupNode;
      AnyFloat L = UpperRib.sRel - LowerRib.sRel;
    };
    AnyKinLinear InterCostalInferior = { 
      Ref = 0;
      AnyRefFrame &UpperRib = ..rib.ICSpringInfNode;
      AnyRefFrame &LowerRib = ..rib_below.ICSpringSupNode;
      AnyFloat L = UpperRib.sRel - LowerRib.sRel;
    };
  };
};
