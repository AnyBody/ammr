/**
Definitions of the Joints in the lumbar spine. From PelvisL5 to T12L1
*/
AnySphericalJoint SacrumPelvis = {
  AnyRefNode &PelvisNode = ...Segments.PelvisSeg.PelvisSacrumJntNode;
  AnyRefNode &SacrumNode = ...Segments.SacrumSeg.SacrumPelvisJntNode;
  
};
//There are no muscles attached to sacrum 
AnyReacForce SacrumPelvisReaction = {
  AnySphericalJoint &ref=.SacrumPelvis;
};

AnySphericalJoint L5Sacrum = {
  AnyRefNode &SacrumNode = ...Segments.SacrumSeg.SacrumL5JntNode;
  AnyRefNode &L5Node = ...Segments.L5Seg.L5SacrumJntNode;
};  

AnySphericalJoint L4L5 = {
  AnyRefNode &L5Node = ...Segments.L5Seg.L4L5JntNode;
  AnyRefNode &L4Node = ...Segments.L4Seg.L4L5JntNode;
};

AnySphericalJoint L3L4 = {
  AnyRefNode &L4Node = ...Segments.L4Seg.L3L4JntNode;
  AnyRefNode &L3Node = ...Segments.L3Seg.L3L4JntNode;
};

AnySphericalJoint L2L3 = {
  AnyRefNode &L3Node = ...Segments.L3Seg.L2L3JntNode;
  AnyRefNode &L2Node = ...Segments.L2Seg.L2L3JntNode;
};

AnySphericalJoint L1L2 = {
  AnyRefNode &L2Node = ...Segments.L2Seg.L1L2JntNode;
  AnyRefNode &L1Node = ...Segments.L1Seg.L1L2JntNode;
};

AnySphericalJoint T12L1 = {
  AnyRefNode &L1Node = ...Segments.L1Seg.T12L1JntNode;
  AnyRefNode &T12Node = ...Segments.T12Seg.T12L1JntNode;
};

#if BM_TRUNK_LUMBAR_RHYTHM != OFF

  //Spine rhythm leaves three rotational dof between pelvis and thorax free 
  RhytmDriverLinear SpineRhythmDrvFlexion(RELATIVE_TO_DOF=6, _REDEFINE_VARIABLES=On) = {
     
     AnyFloat RhythmCoefficients ??= {0, 0.165, 0.169, 0.162, 0.149, 0.125, 0.088};
     
     Measures.Input = 
    {
      AnyJoint &u1 = ...SacrumPelvis;
      AnyJoint &u2 = ...L5Sacrum;
      AnyJoint &u3 = ...L4L5;
      AnyJoint &u4 = ...L3L4;
      AnyJoint &u5 = ...L2L3;
      AnyJoint &u6 = ...L1L2;
      AnyJoint &u7 = ...T12L1;
      MeasureOrganizer = {2, 5, 8, 11, 14, 17, 20};
    };
    
    #if BM_TRUNK_LUMBAR_RHYTHM == _RHYTHM_SOFT_
    CType = repmat(nDim, Soft);
    #endif

  };
  
  RhytmDriverLinear SpineRhythmDrvRotation(RELATIVE_TO_DOF=6, _REDEFINE_VARIABLES=On) = {
     
     AnyVector RhythmCoefficients ??= {0, 0.1661, 0.1802, 0.1761, 0.1694, 0.1628, 0.1453};
     
     Measures.Input = 
    {
      AnyJoint &u1 = ...SacrumPelvis;
      AnyJoint &u2 = ...L5Sacrum;
      AnyJoint &u3 = ...L4L5;
      AnyJoint &u4 = ...L3L4;
      AnyJoint &u5 = ...L2L3;
      AnyJoint &u6 = ...L1L2;
      AnyJoint &u7 = ...T12L1;
      MeasureOrganizer = {1, 4, 7, 10, 13, 16, 19};

    };
    
    #if BM_TRUNK_LUMBAR_RHYTHM == _RHYTHM_SOFT_
    CType = repmat(nDim, Soft);
    #endif

  };

  RhytmDriverLinear SpineRhythmDrvLatBending(RELATIVE_TO_DOF=6, _REDEFINE_VARIABLES=On) = {
     
    AnyVector RhythmCoefficients ??= {0, 0.12, 0.186, 0.207, 0.187, 0.166, 0.133};
     
    Measures.Input = 
    {
      AnyJoint &u1 = ...SacrumPelvis;
      AnyJoint &u2 = ...L5Sacrum;
      AnyJoint &u3 = ...L4L5;
      AnyJoint &u4 = ...L3L4;
      AnyJoint &u5 = ...L2L3;
      AnyJoint &u6 = ...L1L2;
      AnyJoint &u7 = ...T12L1;
      MeasureOrganizer = {0, 3, 6, 9, 12, 15, 18};
    };
    
    #if BM_TRUNK_LUMBAR_RHYTHM == _RHYTHM_SOFT_
    CType = repmat(nDim, Soft);
    #endif

  };

#endif

//Rotational measure between throax and pelvis
AnyKinRotational PelvisThoraxRotMeasure = {
  AnyRefFrame &Pelvis = ...Segments.PelvisSeg.AnatomicalFrameTrunk;
  AnyRefFrame &Thorax = ...Segments.T1Seg.AnatomicalFrameTrunk;  
  Type = RotVector;
};  
  

  //Rotational measure between throax and pelvis
AnyKinRotational PelvisT12RotMeasure = {
  AnyRefFrame &Pelvis = ...Segments.PelvisSeg.AnatomicalFrameTrunk;
  AnyRefFrame &Thorax = ...Segments.T12Seg.AnatomicalFrameTrunk;  
  Type = RotVector;
};    
