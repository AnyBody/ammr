#ifndef ANYBODY_PATH_BODY_GENERICBODYMODEL
  #path ANYBODY_PATH_BODY_GENERICBODYMODEL "<ANYBODY_PATH_BODY>\BodyModels\GenericBodyModel\"
#endif

AnyFolder HumanModel={
  AnyFolder Config = 
  {
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.defaults.preprocess.any"
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.defaults.any"
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.BodyPartsSetup.any"
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.config_info.any"
  };
 
  
  
  /// This folder holds the Anthropometric data for the HumanModel.
  /// This often refered to as the "AnyMan" file. 
  Anthropometrics = { };
  #ifdef BM_SCALING_ANTHRO_FILE
    // Include the user define AnyMan file
    AnyFolder Anthropometrics = { #include "<BM_SCALING_ANTHRO_FILE>" };
  #else #if BM_SCALING == _SCALING_STANDARD_
    // Create an AnyMan folder containing 50th percentile anthropometrics
    Template_Anthropometrics_50th_percentile Anthropometrics = {};  
  #else #if BM_SCALING == _SCALING_XYZ_
    // Create the XYZ scaling AnyMan folder
    Template_Anthropometrics_XYZ Anthropometrics = {};
  #else #if BM_SCALING_STANDARDPARAM_EXTERIOR == ON 
    // Create the external parameter AnyMan folder
    Template_Anthropometrics_exterior Anthropometrics = {};  
  #else
    // Create the Normal AnyMan folder 
    Template_Anthropometrics Anthropometrics(
       T_BM_SCALING = BM_SCALING,
       T_SCALING_LENGTHMASSFAT_MULTIDOFS_ = _SCALING_LENGTHMASSFAT_MULTIDOFS_
     ) = {};
  #endif#endif#endif#endif
  
  
  
  
  #ifndef BM_MANNEQUIN_FILE
    /// This is the Mannequin folder implemented as a class template. 
    /// It behaves as a class with default values which can be overridden
    /// in your applicaiton: 
    ///
    /// .. code_block:: AnyScript
    ///
    ///    Main.HumanModel.Mannequin.Posture = 
    ///    {
    ///      ElbowPronation = -70;
    ///    };
    ///
    /// It is still possible to define your own Mannequin file in your application:
    ///
    /// .. code_block:: AnyScript
    /// 
    ///     #path BM_MANNEQUIN_FILE "MyMannequinFile.any"
    ///
    Template_Mannequin Mannequin = {};
    //Template defined in in #paths"BodyModels/GenericBodyModel/BodyModel.ClassTemplates.any"
  #else
    // Include user defined Mannequin file
    #include "<BM_MANNEQUIN_FILE>"
  #endif
  
  
  
#if BM_DRAWSETTINGS_SKIP == OFF
  Main = 
  {
  #ifndef BM_DRAWSETTINGS_FILE
    Template_DrawSettings DrawSettings = {};
  #else
    #include "<BM_DRAWSETTINGS_FILE>"
  #endif    
  };
#endif
  
  #include  "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.any"
  
  AnyFolder StrengthParameters={
    AnyVar SpecificMuscleTensionSpine= BM_TRUNK_STRENGTH_INDEX*90; //N/cm^2
    AnyVar StrengthIndexLeg= DesignVar(BM_LEG_STRENGTH_INDEX); 
    AnyVar SpecificMuscleTensionShoulderArm= BM_ARM_STRENGTH_INDEX*90; //N/cm^2
  };
  


  
  
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\CheckCustomScalingEnabled.any"
  
  #if __CUSTOM_SCALING == OFF
  AnyFolder Scaling = 
  #else 
  AnyFolder ScalingDefault = 
  #endif
  {
     #include "<ANYBODY_PATH_BODY>/BodyModels/GenericBodyModel/BodyModel.StandardParameters.any"
    // Custom scaling is not enabled; include the scaling law in the traditional way.
    // Include the scaling law selected by the user
    #include "<BM_SCALING_FILE>"
  };
  
  #if __CUSTOM_SCALING == ON
  #include "<ANYBODY_PATH_BODY>\Scaling\ScalingCustom.any"
  #endif
 
      
  AnyFolder DefaultMannequinDrivers = 
  {
    #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.MannequinDrivers.any"
  };
 
  AnyFolder BodyModelWithDefaultDrivers = {
    AnyFolder &BodyModel = .BodyModel;
    AnyFolder &Drivers = .DefaultMannequinDrivers;
  }; 
  
};

