#ifndef ANYBODY_PATH_BODY_GENERICBODYMODEL
  #path ANYBODY_PATH_BODY_GENERICBODYMODEL "<ANYBODY_PATH_BODY>\BodyModels\GenericBodyModel\"
#endif

AnyFolder HumanModel={
  AnyDoc Config = 
  {
    DocumentationLink = AMMR_PATH_DOC + "/index.html";
    
    #if (ANYBODY_V1 > 7)|(ANYBODY_V1 == 7 & ANYBODY_V2 > 1)
    #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BM_Plugin.any"
    #endif

    #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.defaults.preprocess.any"
    #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.defaults.any"
    #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.BodyPartsSetup.any"
    #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.config_info.any"
    
    

  };
 
  
  
  /// This folder holds the Anthropometric data for the
  /// HumanModel (also called the AnyMan file).
  /// You can specify your own AnyMan file with:
  /// #path BM_SCALING_ANTHRO_FILE "My_Own_AnyMan.any"
  ///
  /// If you don't specify an AnyMan file, the system 
  /// creates the 'HumanModel.Anthropometrics' folder 
  /// for you by the Template_Anthropometrics* class templates.
  /// You can view the values under HumanModel.Anthropometrics
  Anthropometrics = { };
  #ifdef BM_SCALING_ANTHRO_FILE
    // Include the user define AnyMan file
    AnyFolder Anthropometrics = { #include "<BM_SCALING_ANTHRO_FILE>" };
  #else #if BM_SCALING == _SCALING_STANDARD_
    // Create an AnyMan folder containing 50th percentile male anthropometrics
    Template_Anthropometrics_50th_percentile Anthropometrics = {};  
  #else #if BM_SCALING == _SCALING_XYZ_
    // Create the XYZ scaling AnyMan folder
    Template_Anthropometrics_XYZ Anthropometrics = {};
  #else #if BM_SCALING_STANDARDPARAM_EXTERIOR == ON 
    // Create the external parameter AnyMan folder
    Template_Anthropometrics_exterior Anthropometrics = {};  
  #else
    // Create the Normal AnyMan folder 
    Template_Anthropometrics Anthropometrics(
       T_BM_SCALING = BM_SCALING,
       T_SCALING_LENGTHMASSFAT_MULTIDOFS_ = _SCALING_LENGTHMASSFAT_MULTIDOFS_
     ) = {};
  #endif#endif#endif#endif
  
  
  
  
  #ifndef BM_MANNEQUIN_FILE
    // Include the Mannequin file with all default values.
    #include "BodyModels/GenericBodyModel/MannequinDefault.any"  
  #else
    // The old way of defining mannequin files
    AnyInt WARNING_BM_MANNEQUIN_FILE_Deprecation = warn(0, strformat("\n"+
      "----------------------------------------------------------------------------------------------------------------\n"+
      "    Setting the mannequin file with `#path BM_MANNEQUIN_FILE` is deprecated. \n"+
      "    Instead just overwrite the values you need to set directly in your application files: \n"+
      "     \n"+
      "     Main.HumanModel.Mannequin.Posture = {\n"+
      "       PelvisPosX = 0.1; \n"+
      "       ...\n"+
      "     \n"+
      "-----------------------------------------------------------------------------------------------------------------"));
    #include "<BM_MANNEQUIN_FILE>"
  #endif
  
  
  
#if BM_DRAWSETTINGS_SKIP == OFF

  Main = 
  {
  #ifndef BM_DRAWSETTINGS_FILE
    #include "<ANYBODY_PATH_MODELUTILS>/DrawSettings/DrawSettingsDefault.any"  
  #else
      // The old way of defining drawsettings files
    AnyInt WARNING_BM_DRAWSETTINGS_FILE_Deprecation = warn(0, strformat("\n"+
      "----------------------------------------------------------------------------------------------------------------\n"+
      "    Setting the DrawSettings with `#include` is deprecated. \n"+
      "    Instead just overwrite the values you need to set directly in your application files: \n"+
      "     \n"+
      "     Main.DrawSettings.Colors = {\n"+
      "       RGB = {0.9, 0.1, 0.1}; \n"+
      "       ...\n"+
      "     \n"+
      "-----------------------------------------------------------------------------------------------------------------"));
    #include "<BM_DRAWSETTINGS_FILE>"
  #endif    
  };
  
#endif
  
  #include  "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.any"
  
  AnyFolder StrengthParameters={
    AnyVar SpecificMuscleTensionSpine= BM_TRUNK_STRENGTH_INDEX*90; //N/cm^2
    AnyVar StrengthIndexLeg= DesignVar(BM_LEG_STRENGTH_INDEX); 
    AnyVar SpecificMuscleTensionShoulderArm= BM_ARM_STRENGTH_INDEX*90; //N/cm^2
  };
  


  
  
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\CheckCustomScalingEnabled.any"
  
  #if __CUSTOM_SCALING == OFF
  AnyFolder Scaling = 
  #else 
  AnyFolder ScalingDefault = 
  #endif
  {
     #include "<ANYBODY_PATH_BODY>/BodyModels/GenericBodyModel/BodyModel.StandardParameters.any"
    // Custom scaling is not enabled; include the scaling law in the traditional way.
    // Include the scaling law selected by the user
    #include "<BM_SCALING_FILE>"
  };
  
  #if __CUSTOM_SCALING == ON
  #include "<ANYBODY_PATH_BODY>\Scaling\ScalingCustom.any"
  #endif
  
  Scaling = 
  {
    AnyDoc Doc = {DocumentationLink = AMMR_PATH_DOC + "/Scaling/intro.html";};
  };
  
  #include "<ANYBODY_PATH_BODY_GENERICBODYMODEL>\BodyModel.MannequinDrivers.any"
 
  AnyFolder BodyModelWithDefaultDrivers = {
    AnyFolder &BodyModel = .BodyModel;
    AnyFolder &Drivers = .DefaultMannequinDrivers;
  }; 
  
};

