/* -------------------------------------------------------------------------------------------
This file implements a scaling law based on segment length and mass. The y (length) direction
of each segment is scaled according to Anthro joint center distances. The cross sectional
directions, x and z, are scaled according to the segment mass, and so is the strength.

John Rasmussen & Mark de Zee, 2004.11.23
------------------------------------------------------------------------------------------- */

// Scaling law.
// Main.HumanModel.Scaling

  AnyFolder MassScaling = {
    AnyFolder Pelvis = { AnyVar MassScale = ...Anthropometrics.SegmentMasses.Pelvis / ..StandardParameters.Pelvis.Mass; };
    AnyFolder Head = { AnyVar MassScale = (...Anthropometrics.SegmentMasses.Head) / (..StandardParameters.Head.Mass); };
    AnyFolder Cervical = { AnyVar MassScale = .Head.MassScale;};
    AnyFolder Thorax = { AnyVar MassScale = ( ...Anthropometrics.SegmentMasses.Thorax) / (..StandardParameters.Trunk.Mass  ); }; //Ratio of mass of thorax plus lumbar mass
    AnyFolder Lumbar = { AnyVar MassScale = (...Anthropometrics.SegmentMasses.Lumbar) / (..StandardParameters.Lumbar.Mass); };//Ratio of mass of lumbar mass
    AnyFolder Right = 
    {
      AnyFolder Clavicula = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Clavicle / (...StandardParameters.Clavicula.Mass + ...StandardParameters.Scapula.Mass); };   
      AnyFolder Scapula   = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Clavicle / (...StandardParameters.Clavicula.Mass + ...StandardParameters.Scapula.Mass); };       
      AnyFolder Humerus   = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.UpperArm / ...StandardParameters.Humerus.Mass; };
      AnyFolder Ulna      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.LowerArm / (...StandardParameters.Ulna.Mass+ ...StandardParameters.Radius.Mass); };
      AnyFolder Radius    = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.LowerArm / (...StandardParameters.Ulna.Mass+ ...StandardParameters.Radius.Mass);  };
      AnyFolder Hand      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Hand / ...StandardParameters.Hand.Mass; };    
    
      #if BM_HAND_MODEL != _HAND_MODEL_OFF_
      AnyFolder Thumb_MC      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Thumb_MC / ...StandardParameters.Hand.ThumbMCMass; }; 
      AnyFolder Thumb_PP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Thumb_PP / ...StandardParameters.Hand.ThumbPPMass; };
      AnyFolder Thumb_DP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Thumb_DP / ...StandardParameters.Hand.ThumbDPMass; };
      AnyFolder Finger_MC      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Finger_MC / ...StandardParameters.Hand.FingerMCMass; };
      AnyFolder Finger_PP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Finger_PP / ...StandardParameters.Hand.FingerPPMass; };
      AnyFolder Finger_MP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Finger_MP / ...StandardParameters.Hand.FingerMPMass; };
      AnyFolder Finger_DP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Finger_DP / ...StandardParameters.Hand.FingerDPMass; };
      #endif   
     
      AnyFolder Foot      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Foot  / ...StandardParameters.Right.Foot.Mass; };
      AnyFolder Talus     = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Talus / ...StandardParameters.Right.Talus.Mass; };  
      AnyFolder Shank     = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Shank / ...StandardParameters.Right.Shank.Mass; };
      AnyFolder Thigh     = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Right.Thigh / ...StandardParameters.Right.Thigh.Mass; };  
    };
    AnyFolder Left = 
    {
      AnyFolder Clavicula = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Clavicle / (...StandardParameters.Clavicula.Mass + ...StandardParameters.Scapula.Mass); };   
      AnyFolder Scapula   = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Clavicle / (...StandardParameters.Clavicula.Mass + ...StandardParameters.Scapula.Mass); };       
      AnyFolder Humerus   = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.UpperArm / ...StandardParameters.Humerus.Mass; };
      AnyFolder Ulna      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.LowerArm / (...StandardParameters.Ulna.Mass+ ...StandardParameters.Radius.Mass); };
      AnyFolder Radius    = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.LowerArm / (...StandardParameters.Ulna.Mass+ ...StandardParameters.Radius.Mass);  };
      AnyFolder Hand      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Hand / ...StandardParameters.Hand.Mass; }; 

      #if BM_HAND_MODEL != OFF
      AnyFolder Thumb_MC      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Thumb_MC / ...StandardParameters.Hand.ThumbMCMass; }; 
      AnyFolder Thumb_PP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Thumb_PP / ...StandardParameters.Hand.ThumbPPMass; };
      AnyFolder Thumb_DP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Thumb_DP / ...StandardParameters.Hand.ThumbDPMass; };
      AnyFolder Finger_MC      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Finger_MC / ...StandardParameters.Hand.FingerMCMass; };
      AnyFolder Finger_PP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Finger_PP / ...StandardParameters.Hand.FingerPPMass; };
      AnyFolder Finger_MP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Finger_MP / ...StandardParameters.Hand.FingerMPMass; };
      AnyFolder Finger_DP      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Finger_DP / ...StandardParameters.Hand.FingerDPMass; };
      #endif
      
      AnyFolder Foot      = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Foot  / ...StandardParameters.Left.Foot.Mass; };
      AnyFolder Talus     = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Talus / ...StandardParameters.Left.Talus.Mass; };  
      AnyFolder Shank     = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Shank / ...StandardParameters.Left.Shank.Mass; };
      AnyFolder Thigh     = { AnyVar MassScale = ....Anthropometrics.SegmentMasses.Left.Thigh / ...StandardParameters.Left.Thigh.Mass; };  
    };
  };
  
  
  AnyFolder StrengthScaling = {    
    AnyVar Rother = 0.5;          // Mass fraction in the body of organs, blood, skeleton, etc.
    AnyVar Rfat = ..Anthropometrics.FatPercent/100; // Fat ration in the entire body
    AnyVar Rfat0 = .StandardParameters.BodyParameters.FatPercent/100;
    AnyFolder Pelvis = { AnyVar StrengthScale = (..MassScaling.Pelvis.MassScale / ..GeometricalScaling.Pelvis.LengthScale) * ((1-.Rother-.Rfat)/(1-.Rother-.Rfat0)); };
    AnyFolder Spine = { AnyVar StrengthScale = (..MassScaling.Thorax.MassScale / ..GeometricalScaling.Thorax.LengthScale ) * ((1-.Rother-.Rfat)/(1-.Rother-.Rfat0)); };
    
    AnyFolder Right =
    { 
      AnyFolder Humerus = { AnyVar StrengthScale = (...MassScaling.Right.Humerus.MassScale / ...GeometricalScaling.Right.Humerus.LengthScale            ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
      AnyFolder Ulna    = { AnyVar StrengthScale = (...MassScaling.Right.Ulna.MassScale    / ...GeometricalScaling.Right.Ulna.LengthScale               ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
      AnyFolder Hand    = { AnyVar StrengthScale = (...MassScaling.Right.Hand.MassScale    / ...GeometricalScaling.Right.Hand.ScaleFunction.LengthScale ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
	  
      // Strength scaling factors for the leg strength is not normalized with respect to segment length because 
      // PSCA is calculated based on the muscle fibers which are scaled separately. 
      AnyFolder Foot = { AnyVar StrengthScale = (...MassScaling.Right.Foot.MassScale   ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
      AnyFolder Shank = { AnyVar StrengthScale = (...MassScaling.Right.Shank.MassScale ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0));};
      AnyFolder Thigh = { AnyVar StrengthScale = (...MassScaling.Right.Thigh.MassScale ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
    };
    AnyFolder Left =
    { 
      AnyFolder Humerus = { AnyVar StrengthScale = (...MassScaling.Left.Humerus.MassScale / ...GeometricalScaling.Left.Humerus.LengthScale            ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
      AnyFolder Ulna    = { AnyVar StrengthScale = (...MassScaling.Left.Ulna.MassScale    / ...GeometricalScaling.Left.Ulna.LengthScale               ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
      AnyFolder Hand    = { AnyVar StrengthScale = (...MassScaling.Left.Hand.MassScale    / ...GeometricalScaling.Left.Hand.ScaleFunction.LengthScale ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
	  
      // Strength scaling factors for the leg strength is not normalized with respect to segment length because 
      // PSCA is calculated based on the muscle fibers which are scaled separately. 
      AnyFolder Foot = { AnyVar StrengthScale = (...MassScaling.Left.Foot.MassScale   ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
      AnyFolder Shank = { AnyVar StrengthScale = (...MassScaling.Left.Shank.MassScale ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0));};
      AnyFolder Thigh = { AnyVar StrengthScale = (...MassScaling.Left.Thigh.MassScale ) * ((1-..Rother-..Rfat)/(1-..Rother-..Rfat0)); };
    };
  };
  
  AnyFolder FiberLengthScaling = {    
    AnyFolder Pelvis = { AnyVar FiberLengthScale = ...Anthropometrics.SegmentDimensions.PelvisWidth / ..StandardParameters.Pelvis.Width; };
    
    AnyFolder Right =
    {
      AnyFolder Foot = { AnyVar FiberLengthScale  = ....Anthropometrics.SegmentDimensions.Right.FootLength / ...StandardParameters.Right.Foot.Length; };
      AnyFolder Shank = { AnyVar FiberLengthScale  = ....Anthropometrics.SegmentDimensions.Right.ShankLength / ...StandardParameters.Right.Shank.Length; };
      AnyFolder Thigh = { AnyVar FiberLengthScale  = ....Anthropometrics.SegmentDimensions.Right.ThighLength / ...StandardParameters.Right.Thigh.Length; };
    };
    AnyFolder Left =
    {
      AnyFolder Foot = { AnyVar FiberLengthScale  = ....Anthropometrics.SegmentDimensions.Left.FootLength / ...StandardParameters.Left.Foot.Length; };         
      AnyFolder Shank = { AnyVar FiberLengthScale  = ....Anthropometrics.SegmentDimensions.Left.ShankLength / ...StandardParameters.Left.Shank.Length; };
      AnyFolder Thigh = { AnyVar FiberLengthScale  = ....Anthropometrics.SegmentDimensions.Left.ThighLength / ...StandardParameters.Left.Thigh.Length; };
    };
    
  };
  
  AnyFolder GeometricalScaling = {
    
    AnyFolder Right ={
      AnyFolder Clavicula = {
        AnyFunTransform3DLin ScaleFunction = {
          AnyMat33 ARel= {      {-0.280836,  0.188095, -0.941144},      {-0.004752,  0.980323, 0.197343},       { 0.959744, 0.059893,  -0.274416}}';
          ScaleMat = ARel*...Thorax.ScaleFunction.ScaleMat*(ARel');
          Offset = {0,0,0}*ARel';
        };
        
      };
      
      AnyFolder Scapula = {
        AnyFunTransform3DLin ScaleFunction = {
          AnyMat33 ARel ={{0.561294, -0.097357, -0.821870},    {0.151325, 0.988389, -0.013736},    {0.813664, -0.116660, 0.569509}}';
          ScaleMat = ARel*...Thorax.ScaleFunction.ScaleMat*(ARel');
          Offset = {0,0,0}*ARel';
        };
      };
      
      AnyFolder Humerus = {
        AnyVar LengthScale = ( ....Anthropometrics.SegmentDimensions.Right.UpperArmLength / ...StandardParameters.Humerus.Length);
        AnyVar ms = ...MassScaling.Right.Humerus.MassScale;
        AnyVar ls = LengthScale;      
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0,0,0};
        };
      };
      
      AnyFolder Ulna = {
        AnyVar LengthScale = ( ....Anthropometrics.SegmentDimensions.Right.LowerArmLength / ...StandardParameters.Ulna.Length);
        AnyVar ms = ...MassScaling.Right.Ulna.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0,0,0};
        };
      };
      
      AnyFolder Radius = {
        AnyFunTransform3DLin ScaleFunction = {
          //This Arel is used for mapping the scaling of ulna into the radius coordinate system 
          //which is rotated, Arel has been found for a neutral position
          AnyMat33 ARel = {{-0.145873, -0.989303, -0.000001}, { 0.989303, -0.145873,  0.000001}, {-0.000001, -0.000000, 1.000000}};
          AnyMat33 ScaleXYZ = ..Ulna.ScaleFunction.ScaleMat;
          ScaleMat = ARel'*ScaleXYZ*ARel;
          Offset = {0,0,0}*ARel;      };
      };
      
      AnyFolder Hand = {
        AnyVar HandLength = ....Anthropometrics.SegmentDimensions.Right.HandLength;
        AnyVar HandBreadth = ....Anthropometrics.SegmentDimensions.Right.HandBreadth;
        AnyFunTransform3DLin ScaleFunction = {
          AnyVar BreadthScale = .HandBreadth/....StandardParameters.Hand.HandBreadth;
          AnyVar LengthScale =  .HandLength/....StandardParameters.Hand.HandLength;        
          ScaleMat ={{LengthScale,0,0},{0,BreadthScale,0},{0,0,BreadthScale}};        
          Offset = {0,0,0};
        };

        #if BM_HAND_MODEL != OFF
        AnyFolder Carpals = {
          AnyVar CarpalsLength = .....Anthropometrics.SegmentDimensions.Right.Hand.CarpalsLength;     
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar ls =  .CarpalsLength/.....StandardParameters.Hand.CarpalsLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};        
            Offset = {0,0,0};
          };
        };
        
        AnyFolder Thumb_MC ={
          AnyVar  ThumbMCLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger1.MCLength;
          AnyVar StdSegmentLength = 0.049;
          AnyVar StdHandLength = 0.182;
          
          AnyVar ms = ....MassScaling.Right.Thumb_MC.MassScale;
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar  ls = .ThumbMCLength/.....StandardParameters.Hand.ThumbMCLength*(0.260844*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };
        
        AnyFolder Thumb_PP ={
          AnyVar  ThumbPPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger1.PPLength; 
          AnyVar StdSegmentLength = 0.034;
          AnyVar StdHandLength = 0.182;
          AnyVar ms = ....MassScaling.Right.Thumb_PP.MassScale;
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar  ls = .ThumbPPLength/.....StandardParameters.Hand.ThumbPPLength*(0.180002*.StdHandLength)/.StdSegmentLength;          
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };
        
        AnyFolder Thumb_DP ={
          AnyVar  ThumbDPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger1.DPLength; 
          AnyVar StdSegmentLength = 0.022;
          AnyVar StdHandLength = 0.182;
          AnyVar ms = ....MassScaling.Right.Thumb_DP.MassScale;
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar  ls = .ThumbDPLength/.....StandardParameters.Hand.ThumbDPLength*(0.138805*.StdHandLength)/.StdSegmentLength;             
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };
        
        AnyFolder Finger_MC ={
          AnyVar ms = ....MassScaling.Right.Finger_MC.MassScale;
          
          AnyVar Finger2MCLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger2.MCLength; 
          AnyVar Finger3MCLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger3.MCLength; 
          AnyVar Finger4MCLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger4.MCLength; 
          AnyVar Finger5MCLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger5.MCLength; 
          AnyVar StdSegmentLength = 0.07;
          AnyVar StdHandLength = 0.182;     
          
          AnyFunTransform3DLin ScaleFunction2 = { 
            AnyVar  ls = .Finger2MCLength/.....StandardParameters.Hand.Finger2MCLength*(0.375286*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = {
            AnyVar  ls = .Finger3MCLength/.....StandardParameters.Hand.Finger3MCLength*(0.360913*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = { 
            AnyVar  ls = .Finger4MCLength/.....StandardParameters.Hand.Finger4MCLength*(0.319616*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = { 
            AnyVar  ls = .Finger5MCLength/.....StandardParameters.Hand.Finger5MCLength*(0.292675*.StdHandLength)/.StdSegmentLength;        
            
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };  
        
        
        AnyFolder Finger_PP ={
          AnyVar ms = ....MassScaling.Right.Finger_PP.MassScale;
          
          AnyVar  Finger2PPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger2.PPLength; 
          AnyVar  Finger3PPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger3.PPLength; 
          AnyVar  Finger4PPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger4.PPLength;  
          AnyVar  Finger5PPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger5.PPLength; 
          AnyVar StdSegmentLength = 0.0521;
          AnyVar StdHandLength = 0.182;  
          
          AnyFunTransform3DLin ScaleFunction2 = { 
            AnyVar  ls = .Finger2PPLength/.....StandardParameters.Hand.Finger2PPLength*(0.253760*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = { 
            AnyVar  ls = .Finger3PPLength/.....StandardParameters.Hand.Finger3PPLength*(0.280868*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = {
            AnyVar  ls = .Finger4PPLength/.....StandardParameters.Hand.Finger4PPLength*(0.261700*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = {  
            AnyVar  ls = .Finger5PPLength/.....StandardParameters.Hand.Finger5PPLength*(0.208756*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };  
        
        
        AnyFolder Finger_MP ={
          AnyVar ms = ....MassScaling.Right.Finger_MP.MassScale;
          
          AnyVar  Finger2MPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger2.MPLength; 
          AnyVar  Finger3MPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger3.MPLength;  
          AnyVar  Finger4MPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger4.MPLength; 
          AnyVar  Finger5MPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger5.MPLength; 
          AnyVar StdSegmentLength = 0.03;
          AnyVar StdHandLength = 0.182; 
          
          AnyFunTransform3DLin ScaleFunction2 = {
            AnyVar  ls = .Finger2MPLength/.....StandardParameters.Hand.Finger2MPLength*(0.159697*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = { 
            AnyVar  ls = .Finger3MPLength/.....StandardParameters.Hand.Finger3MPLength*(0.194885*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = { 
            AnyVar  ls = .Finger4MPLength/.....StandardParameters.Hand.Finger4MPLength*(0.185207*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = {
            AnyVar  ls = .Finger5MPLength/.....StandardParameters.Hand.Finger5MPLength*(0.129041*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };  
        
        AnyFolder Finger_DP ={
          AnyVar ms = ....MassScaling.Right.Finger_DP.MassScale;
          
          AnyVar  Finger2DPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger2.DPLength; 
          AnyVar  Finger3DPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger3.DPLength; 
          AnyVar  Finger4DPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger4.DPLength; 
          AnyVar  Finger5DPLength = .....Anthropometrics.SegmentDimensions.Right.Hand.Finger5.DPLength; 
          AnyVar StdSegmentLength = 0.022;
          AnyVar StdHandLength = 0.182;
          
          AnyFunTransform3DLin ScaleFunction2 = {  
            AnyVar  ls = .Finger2DPLength/.....StandardParameters.Hand.Finger2DPLength*(0.104295*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = {  
            AnyVar  ls = .Finger3DPLength/.....StandardParameters.Hand.Finger3DPLength*(0.111260*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = {  
            AnyVar  ls = .Finger4DPLength/.....StandardParameters.Hand.Finger4DPLength*(0.113941*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = {
            AnyVar  ls = .Finger5DPLength/.....StandardParameters.Hand.Finger5DPLength*(0.099599*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };
        #endif

      };//Hand
      
      // Talus uses the scaling function of the Foot segment
      AnyFolder &Talus = Foot;
      
      AnyFolder Foot  = {
        AnyVar LengthScale = ....Anthropometrics.SegmentDimensions.Right.FootLength / ...StandardParameters.Right.Foot.Length;    
        AnyVar ms =  ...MassScaling.Right.Foot.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{.ls, 0, 0},{0, (.ms/.ls)^0.5, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0, 0, 0};            
        };
      };
      
      AnyFolder Shank = {
        AnyVar LengthScale = ....Anthropometrics.SegmentDimensions.Right.ShankLength / ...StandardParameters.Right.Shank.Length;
        AnyVar ms = ...MassScaling.Right.Shank.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}} ;
          Offset = {0, 0, 0};
        };
        
      };
      
      AnyFolder Thigh = {
        AnyVar LengthScale  = ....Anthropometrics.SegmentDimensions.Right.ThighLength / ...StandardParameters.Right.Thigh.Length;
        AnyVar ms = ...MassScaling.Right.Thigh.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}} ;
          Offset = {0, 0, 0};
        };
        
      };

      // Patella uses the scaling function of the Thigh segment.
      AnyFolder &Patella = Thigh;
      
    }; // Right
    
    AnyFolder Left ={
      AnyFolder Clavicula = {
        AnyFunTransform3DLin ScaleFunction = {
          AnyMat33 ARel= {      {-0.280836,  -0.188095, -0.941144},      {0.004752,  0.980323, -0.197343},       { 0.959744, -0.059893,  -0.274416}}';
          ScaleMat = ARel*...Thorax.ScaleFunction.ScaleMat*(ARel');
          Offset = {0,0,0}*ARel';
        };
        
      };
      
      AnyFolder Scapula = {
        AnyFunTransform3DLin ScaleFunction = {          
          AnyMat33 ARel ={{0.561294,  0.097357, -0.821870},    {-0.151325, 0.988389, 0.013736},    {0.813664, 0.116660, 0.569509}}';
          ScaleMat = ARel*...Thorax.ScaleFunction.ScaleMat*(ARel');
          Offset = {0,0,0}*ARel';
        };
      };
      
      AnyFolder Humerus = {
        AnyVar LengthScale = ( ....Anthropometrics.SegmentDimensions.Left.UpperArmLength / ...StandardParameters.Humerus.Length);
        AnyVar ms = ...MassScaling.Left.Humerus.MassScale;
        AnyVar ls = LengthScale;      
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0,0,0};
        };
      };
      
      AnyFolder Ulna = {
        AnyVar LengthScale = ( ....Anthropometrics.SegmentDimensions.Left.LowerArmLength / ...StandardParameters.Ulna.Length);
        AnyVar ms = ...MassScaling.Left.Ulna.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0,0,0};
        };
      };
      
      AnyFolder Radius = {
        AnyFunTransform3DLin ScaleFunction = {
          //This Arel is used for mapping the scaling of ulna into the radius coordinate system 
          //which is rotated, Arel has been found for a neutral position
          AnyMat33 ARel = {{-0.145873,  0.989303, -0.000001}, {-0.989303, -0.145873, -0.000001}, {-0.000001,  0.000000, 1.000000}};
          AnyMat33 ScaleXYZ = ..Ulna.ScaleFunction.ScaleMat;
          ScaleMat = ARel'*ScaleXYZ*ARel;
          Offset = {0,0,0}*ARel;      };
      };
      
      AnyFolder Hand = {
        AnyVar HandLength = ....Anthropometrics.SegmentDimensions.Left.HandLength;
        AnyVar HandBreadth = ....Anthropometrics.SegmentDimensions.Left.HandBreadth;
        AnyFunTransform3DLin ScaleFunction = {
          AnyVar BreadthScale = .HandBreadth/....StandardParameters.Hand.HandBreadth;
          AnyVar LengthScale =  .HandLength/....StandardParameters.Hand.HandLength;        
          ScaleMat ={{LengthScale,0,0},{0,BreadthScale,0},{0,0,BreadthScale}};        
          Offset = {0,0,0};
        };
        
        #if BM_HAND_MODEL != OFF
        AnyFolder Carpals = {
          AnyVar CarpalsLength = .....Anthropometrics.SegmentDimensions.Left.Hand.CarpalsLength;     
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar ls =  .CarpalsLength/.....StandardParameters.Hand.CarpalsLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};        
            Offset = {0,0,0};
          };
        };
        
        AnyFolder Thumb_MC ={
          AnyVar  ThumbMCLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger1.MCLength;
          AnyVar StdSegmentLength = 0.049;
          AnyVar StdHandLength = 0.182;
          AnyVar ms = ....MassScaling.Left.Thumb_MC.MassScale;
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar  ls = .ThumbMCLength/.....StandardParameters.Hand.ThumbMCLength*(0.260844*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };
        
        AnyFolder Thumb_PP ={
          AnyVar  ThumbPPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger1.PPLength; 
          AnyVar StdSegmentLength = 0.034;
          AnyVar StdHandLength = 0.182;
          AnyVar ms = ....MassScaling.Left.Thumb_PP.MassScale;
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar  ls = .ThumbPPLength/.....StandardParameters.Hand.ThumbPPLength*(0.180002*.StdHandLength)/.StdSegmentLength;          
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};  
            Offset = {0,0,0};
          }; 
        };
        
        AnyFolder Thumb_DP ={
          AnyVar  ThumbDPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger1.DPLength; 
          AnyVar StdSegmentLength = 0.022;
          AnyVar StdHandLength = 0.182;
          AnyVar ms = ....MassScaling.Left.Thumb_DP.MassScale;
          AnyFunTransform3DLin ScaleFunction = {
            AnyVar  ls = .ThumbDPLength/.....StandardParameters.Hand.ThumbDPLength*(0.138805*.StdHandLength)/.StdSegmentLength;             
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
        };
        
        AnyFolder Finger_MC ={
          AnyVar ms = ....MassScaling.Left.Finger_MC.MassScale;
          
          AnyVar Finger2MCLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger2.MCLength; 
          AnyVar Finger3MCLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger3.MCLength; 
          AnyVar Finger4MCLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger4.MCLength; 
          AnyVar Finger5MCLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger5.MCLength; 
          AnyVar StdSegmentLength = 0.07;
          AnyVar StdHandLength = 0.182; 
          AnyFunTransform3DLin ScaleFunction2 = { 
            AnyVar  ls = .Finger2MCLength/.....StandardParameters.Hand.Finger2MCLength*(0.375286*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = {
            AnyVar  ls = .Finger3MCLength/.....StandardParameters.Hand.Finger3MCLength*(0.360913*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = { 
            AnyVar  ls = .Finger4MCLength/.....StandardParameters.Hand.Finger4MCLength*(0.319616*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = { 
            AnyVar  ls = .Finger5MCLength/.....StandardParameters.Hand.Finger5MCLength*(0.292675*.StdHandLength)/.StdSegmentLength;                
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
        };  
        
        AnyFolder Finger_PP ={
          AnyVar ms = ....MassScaling.Left.Finger_PP.MassScale;
          
          AnyVar  Finger2PPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger2.PPLength; 
          AnyVar  Finger3PPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger3.PPLength; 
          AnyVar  Finger4PPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger4.PPLength;  
          AnyVar  Finger5PPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger5.PPLength; 
          AnyVar StdSegmentLength = 0.0521;
          AnyVar StdHandLength = 0.182;
          
          AnyFunTransform3DLin ScaleFunction2 = { 
            AnyVar  ls = .Finger2PPLength/.....StandardParameters.Hand.Finger2PPLength*(0.253760*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};  
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = { 
            AnyVar  ls = .Finger3PPLength/.....StandardParameters.Hand.Finger3PPLength*(0.280868*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = {
            AnyVar  ls = .Finger3PPLength/.....StandardParameters.Hand.Finger3PPLength*(0.280868*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = {  
            AnyVar  ls = .Finger5PPLength/.....StandardParameters.Hand.Finger5PPLength*(0.208756*.StdHandLength)/.StdSegmentLength;       
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
        };  
        
        AnyFolder Finger_MP ={
          AnyVar ms = ....MassScaling.Left.Finger_MP.MassScale;
          
          AnyVar  Finger2MPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger2.MPLength; 
          AnyVar  Finger3MPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger3.MPLength;  
          AnyVar  Finger4MPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger4.MPLength; 
          AnyVar  Finger5MPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger5.MPLength; 
          AnyVar StdSegmentLength = 0.03;
          AnyVar StdHandLength = 0.182; 
          
          AnyFunTransform3DLin ScaleFunction2 = {
            AnyVar  ls = .Finger2MPLength/.....StandardParameters.Hand.Finger2MPLength*(0.159697*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = { 
            AnyVar  ls = .Finger3MPLength/.....StandardParameters.Hand.Finger3MPLength*(0.194885*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = { 
            AnyVar  ls = .Finger4MPLength/.....StandardParameters.Hand.Finger4MPLength*(0.185207*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = {
            AnyVar  ls = .Finger5MPLength/.....StandardParameters.Hand.Finger5MPLength*(0.129041*.StdHandLength)/.StdSegmentLength;        
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
        };  
        
        AnyFolder Finger_DP ={
          AnyVar ms = ....MassScaling.Left.Finger_DP.MassScale;
          
          AnyVar  Finger2DPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger2.DPLength; 
          AnyVar  Finger3DPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger3.DPLength; 
          AnyVar  Finger4DPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger4.DPLength; 
          AnyVar  Finger5DPLength = .....Anthropometrics.SegmentDimensions.Left.Hand.Finger5.DPLength; 
          AnyVar StdSegmentLength = 0.022;
          AnyVar StdHandLength = 0.182;
          
          AnyFunTransform3DLin ScaleFunction2 = {  
            AnyVar  ls = .Finger2DPLength/.....StandardParameters.Hand.Finger2DPLength*(0.104295*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};  
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction3 = {  
            AnyVar  ls = .Finger3DPLength/.....StandardParameters.Hand.Finger3DPLength*(0.111260*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};  
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction4 = {  
            AnyVar  ls = .Finger4DPLength/.....StandardParameters.Hand.Finger4DPLength*(0.113941*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}};
            Offset = {0,0,0};
          }; 
          AnyFunTransform3DLin ScaleFunction5 = {
            AnyVar  ls = .Finger5DPLength/.....StandardParameters.Hand.Finger5DPLength*(0.099599*.StdHandLength)/.StdSegmentLength;     
            ScaleMat ={{...Hand.ScaleFunction.BreadthScale,0,0},{0,ls,0},{0,0,...Hand.ScaleFunction.BreadthScale}}; 
            Offset = {0,0,0};
          }; 
        };
        #endif
      };//Hand
      
      // Talus uses the scaling function of the Foot segment
      AnyFolder &Talus = Foot;
      
      AnyFolder Foot  = {
        AnyVar LengthScale  = ....Anthropometrics.SegmentDimensions.Left.FootLength / ...StandardParameters.Left.Foot.Length;    
        AnyVar ms = ...MassScaling.Left.Foot.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{.ls, 0, 0},{0, (.ms/.ls)^0.5, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0, 0, 0};
        };
      };
      
      AnyFolder Shank = {
        AnyVar LengthScale  = ....Anthropometrics.SegmentDimensions.Left.ShankLength / ...StandardParameters.Left.Shank.Length;
        AnyVar ms = ...MassScaling.Left.Shank.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}}';
          Offset = {0, 0, 0};
        };
      };
      
      AnyFolder Thigh = {
        AnyVar LengthScale  = ....Anthropometrics.SegmentDimensions.Left.ThighLength / ...StandardParameters.Left.Thigh.Length;
        AnyVar ms = ...MassScaling.Left.Thigh.MassScale;
        AnyVar ls = LengthScale;
        AnyFunTransform3DLin ScaleFunction = {
          ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
          Offset = {0, 0, 0};
        };
        
        
      };

      // Patella uses the scaling function of the Thigh segment.
      AnyFolder &Patella = Thigh;
      
    }; // Left
    
    // Common law for the upper body
    // *****************************
    
    AnyFolder Pelvis = {
      AnyVar LengthScale =  ...Anthropometrics.SegmentDimensions.PelvisWidth / ..StandardParameters.Pelvis.Width;
      //AnyVar DepthScale =  ...Anthropometrics.SegmentDimensions.PelvisDepth / ..StandardParameters.Pelvis.Depth;
      AnyVar ms = ..MassScaling.Pelvis.MassScale;
      AnyVar ls = LengthScale;
      //AnyVar ds = DepthScale;
      //      AnyFunTransform3DLin ScaleFunction = {
      //        ScaleMat = {{.ds, 0, 0},{0, (.ms/.ls)^0.5, 0},{0, 0, .ls}};
      //        Offset = {0, 0, 0};
      //      };
      
      AnyFunTransform3DLin ScaleFunction = 
      {
        ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, (.ms/.ls)^0.5, 0},{0, 0, .ls}};
        Offset = {0, 0, 0};
      };      
    };
    
    AnyFolder Thorax = {
      AnyVar LengthScale = ( ...Anthropometrics.SegmentDimensions.TrunkHeight / ..StandardParameters.Trunk.Height);
      AnyVar ms = ..MassScaling.Thorax.MassScale;
      AnyVar ls = LengthScale;
      AnyFunTransform3DLin ScaleFunction = {
        ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
        Offset = {0,0,0};
      };
    };
    
    AnyFolder Head = {
      AnyVar LengthScale = ( ...Anthropometrics.SegmentDimensions.HeadHeight / ..StandardParameters.Head.Height);
      AnyVar ms = ..MassScaling.Head.MassScale;
      AnyVar ls = LengthScale;
      AnyFunTransform3DLin ScaleFunction = {
        ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
        Offset = {0,0,0};
      };
    };
    
    
    AnyFolder Cervical = {
      AnyFunTransform3DLin &ScaleFunction = .Head.ScaleFunction;
    };
    
    AnyFolder &PelvisSeg = Pelvis;    
    AnyFolder &SkullSeg = Head;
    AnyFolder &ThoraxSeg = Thorax;
    
    AnyFolder &C1Seg = Cervical;
    AnyFolder &C2Seg = Cervical;
    AnyFolder &C3Seg = Cervical;
    AnyFolder &C4Seg = Cervical;
    AnyFolder &C5Seg = Cervical;
    AnyFolder &C6Seg = Cervical;
    AnyFolder &C7Seg = Cervical;
    
    AnyFolder &L1Seg = Lumbar;
    AnyFolder &L2Seg = Lumbar;
    AnyFolder &L3Seg = Lumbar;
    AnyFolder &L4Seg = Lumbar;
    AnyFolder &L5Seg = Lumbar;
    AnyFolder &SacrumSeg = PelvisSeg;
    
    AnyFolder Lumbar = {
      AnyVar LengthScale = ( ...Anthropometrics.SegmentDimensions.TrunkHeight / ..StandardParameters.Trunk.Height);
      AnyVar ms = ..MassScaling.Lumbar.MassScale;
      AnyVar ls = LengthScale;
      AnyFunTransform3DLin ScaleFunction = {
        ScaleMat = {{(.ms/.ls)^0.5, 0, 0},{0, .ls, 0},{0, 0, (.ms/.ls)^0.5}};
        Offset = {0,0,0};
      };
    };
        
  };
















