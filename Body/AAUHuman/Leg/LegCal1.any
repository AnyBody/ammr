//This is the calibration1 file it will calibrate all muscle in the legf
//the subsequent calibration 2 and 3 will overwrite some of the values
//obtained in this study

AnyFolder LegCal1 ={
  AnyComponentDefinition obj = {};  
  
  AnyFolder &Pelvis = ...BodyModel.Trunk.Segments.PelvisSeg; //This is a reference to the folder which holds the human part of the model
  AnyFolder &LegSegSide = .SideHumanFolderRef.Leg.Seg; //This is a reference to the folder which holds the human part of the model
  
  // Segments needed for the Psoas Major muscles
  AnySeg &L1 = ...BodyModel.Trunk.Segments.L1Seg;
  AnySeg &L2 = ...BodyModel.Trunk.Segments.L2Seg;
  AnySeg &L3 = ...BodyModel.Trunk.Segments.L3Seg;
  AnySeg &L4 = ...BodyModel.Trunk.Segments.L4Seg;
  AnySeg &L5 = ...BodyModel.Trunk.Segments.L5Seg;
  AnySeg &Sacrum = ...BodyModel.Trunk.Segments.SacrumSeg;
  AnyFolder &Thorax = ...BodyModel.Trunk.Segments.ThoraxSeg;
    
  
  AnyFolder &JointsSideCal = .SideHumanFolderRef.Leg.Jnt;
  AnyFolder &TrunkJoints = ...BodyModel.Trunk.Joints.Lumbar;

  AnyFolder &MusCalSide =  .SideHumanFolderRef.Leg.Mus;

  
  AnyFolder &MusParCalSide =  .SideHumanFolderRef.Leg.MuscleModels;
  
  
  
  AnyFixedRefFrame ground = {
    AnyRefNode node = { 
      ARel=..Pelvis.Axes0;
      sRel=..Pelvis.r0;
    };
  };
  
  AnyKinEq ThoraxFix = 
  {
    AnyKinMeasure& PelvisThoraxExtension = ....BodyModel.Interface.Trunk.PelvisThoraxExtension;
    AnyKinMeasure& PelvisThoraxLateralBending = ....BodyModel.Interface.Trunk.PelvisThoraxLateralBending;
    AnyKinMeasure& PelvisThoraxRotation = ....BodyModel.Interface.Trunk.PelvisThoraxRotation;
  };
  
  AnyKinEq PelvisFix = {
    AnyKinLinear Lin = {
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....BodyModel.Trunk.Segments.PelvisSeg;
    };
    AnyKinRotational Rot = {
      Type = RotAxesAngles;
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....BodyModel.Trunk.Segments.PelvisSeg;
    };
  };
  
  #if BM_FOOT_MODEL == _FOOT_MODEL_DEFAULT_
  AnyKinEqSimpleDriver JntDOFDriverSide = {
    AnyKinMeasure& Joint = ..SideHumanFolderRef.Leg.JntDOF;
    DriverPos = {1.57, 0.0, 0, 0, 0, 0};
    DriverVel = {0.0,0.0,0,0,0,0};
    Reaction.Type = {Off,Off,Off,Off,Off,Off};
  };
  #endif
  
};



// The study: Operations to be performed on the model
AnyBodyCalibrationStudy LegCalibrationStudy1 = {
  AnyFolder &ref=.LegCal1; 
  nStep = 1;
  InitialConditions.SolverType = KinSolDeterminate;
  Kinematics.SolverType = KinSolDeterminate;
  InitialConditions.SmallStepAssumptionOnOff = Off;
  Kinematics.SmallStepAssumptionOnOff = Off;
};

  