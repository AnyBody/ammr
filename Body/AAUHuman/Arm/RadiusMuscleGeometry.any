// Nodes for muscle attachments bml etc. data from MAYO sub2
AnyFolder &Radius=..Segments.Radius;
Radius = {


  AnyRefNode BicepsCyl = {

    sRel = .Scale(.Data.BicepsCyl_pos*.Mirror);
    AnyVec3 P2 = .Scale(.Data.BicepsCyl_P2_pos*.Mirror);										// TODO: ask MEL about the value, correction missing sRel
    AnyVec3 P3 = .Scale(.Data.BicepsCyl_P3_pos*.Mirror);

    //Calculate rot matrix for cylinder using three points and switch axes
    ARel=RotMat(sRel,P3,P2)*RotMat(-90/180*pi, z)*RotMat(-90/180*pi, y);

    AnySurfCylinder cyl = {
      Radius=vnorm(.sRel-.P3);
      Length= vnorm(.sRel-.P2);
    };
  };

  // Cylinder fitted to the radius bone for wrapping of pronator teres, not from literature.
  AnyRefNode PronatorTeresCyl = {

    AnyVec3  P2= .Scale(.Data.PronatorTeresCyl_P2_pos*.Mirror);								// TODO: check with MEL if known why sRel was not used for data
    AnyVec3  P3= .Scale(.Data.PronatorTeresCyl_P3_pos*.Mirror);
    sRel = .Scale(.Data.PronatorTeresCyl_pos*.Mirror);

    //Calculate orientation of the cylinder
    ARel=RotMat(sRel,P3,P2)*RotMat(-90/180*pi, z)*RotMat(-90/180*pi, y);

    AnySurfCylinder cyl = {
      Radius= vnorm(.sRel-.P3);
      Length= vnorm(.sRel-.P2);
    };
  };


  // Cylinder fitted to the radius bone for wrapping of supinator, not from literature.
  AnyRefNode SupinatorCyl = {																							// TODO: to be deleted

    AnyVec3 P2 =.Scale(.Data.SupinatorCyl_P2_pos*.Mirror);
    AnyVec3 P3 =.Scale(.Data.SupinatorCyl_P3_pos*.Mirror);
    sRel = .Scale(.Data.SupinatorCyl_pos*.Mirror);

    //Calculate orientation of the cylinder
    ARel=RotMat(sRel,P3,P2)*RotMat(-90/180*pi, z)*RotMat(-90/180*pi, y);

    AnySurfCylinder cyl = {
      Radius= vnorm(.sRel-.P3);
      Length= vnorm(.sRel-.P2);
      CapRatio=1;
    };
  };

  AnyRefNode RadiusMuscleCyl = {
    sRel = .Scale(.Data.RadiusMuscleCyl_pos*.Mirror);
    AnyVec3 P3 =.Scale(.Data.RadiusMuscleCyl_P3_pos*.Mirror);
    AnyVec3 P2 =.Scale(.Data.RadiusMuscleCyl_P2_pos*.Mirror);

    //Calculate orientation of the cylinder
    ARel=RotMat(sRel,P3,P2)*RotMat(-90/180*pi, z)*RotMat(-90/180*pi, y);


    AnySurfCylinder cyl = {
      Radius= vnorm(.sRel-.P3);
      Length= vnorm(.sRel-.P2);
      CapRatio=1;
    };
  };


   AnyRefNode MedialExtensorCyl = {


     AnyVar length = 3.3*vnorm(P1-P2)-cyl.CapRatio*2*cyl.Radius;
     sRel = P1+length/2*(P1-P2)/vnorm(P1-P2);
     AnyVec3 P1 = .Scale(.Data.MedialExtensorCyl_pos*.Mirror);
     AnyVec3 P3 = .Scale(.Data.MedialExtensorCyl_P3_pos*.Mirror);
     AnyVec3 P2 = .Scale(.Data.MedialExtensorCyl_P2_pos*.Mirror);
     //Calculate orientation of the cylinder
     ARel=RotMat(P1,P3,P2)*RotMat(-90/180*pi, z)*RotMat(-90/180*pi, y);
     AnySurfCylinder cyl = {
       Radius=vnorm(.P1-.P3);
       Length= .length;
       CapRatio = 0.5;
     };
   };




   //It is used for wrapping of the flexor muscles.
   AnyRefNode FlexorMuscleCyl = {


     AnyVar length = 0.075;
     sRel = P1+length/2*(P1-P2)/vnorm(P1-P2);
     AnyVec3 P1 = .Scale(.Data.WristFlexorCyl_pos*.Mirror);
     AnyVec3 P3 = .Scale(.Data.WristFlexorCyl_P3_pos*.Mirror);
     AnyVec3 P2 = .Scale(.Data.WristFlexorCyl_P2_pos*.Mirror);

     //Calculate orientation of the cylinder
     ARel=RotMat(P1,P3,P2)*RotMat(-90/180*pi, z)*RotMat(-90/180*pi, y);

     AnySurfCylinder cyl = {
       Radius=vnorm(.P1-.P3);
       Length= .length;
     };
   };


   // It is used for wrapping of flexor muscles at wrist
   AnyRefNode FlexorMuscleEllipsoid = {

     AnyVar DiameterXZ = vnorm(.Scale(.Data.Via_PalmarisLongus_pos*.Mirror - .Data.Via_ExtensorIndicis_pos*.Mirror));
     AnyVar DiameterY = vnorm(.Scale(.Data.PS_pos*.Mirror - .Data.rs_pos*.Mirror));
     sRel = P1;
     AnyVec3 P1 = .Scale(.Data.WristFlexorEllipsoid_pos*.Mirror);
     AnyVec3 P3 = .Scale(.Data.WristFlexorEllipsoid_P3_pos*.Mirror);
     AnyVec3 P2 = .Scale(.Data.WristFlexorEllipsoid_P2_pos*.Mirror);

     //Calculate orientation of ellipsoid
     ARel = RotMat(P1,P3,P2);

     AnySurfEllipsoid ellipsoid = {
       Radius = 0.5*{.DiameterXZ,.DiameterY,.DiameterXZ};
     };
   };

   // It is used for wrapping of extensor muscles at wrist
   AnyRefNode ExtensorMuscleEllipsoid = {

     AnyVar DiameterXZ = vnorm(.Scale(.Data.Via_PalmarisLongus_pos*.Mirror - .Data.Via_ExtensorIndicis_pos*.Mirror));
     AnyVar DiameterY = vnorm(.Scale(.Data.PS_pos*.Mirror - .Data.rs_pos*.Mirror));
     sRel = P1;
     AnyVec3 P1 = .Scale(.Data.WristExtensorEllipsoid_pos*.Mirror);
     AnyVec3 P3 = .Scale(.Data.WristExtensorEllipsoid_P3_pos*.Mirror);
     AnyVec3 P2 = .Scale(.Data.WristExtensorEllipsoid_P2_pos*.Mirror);

     //Calculate orientation of ellipsoid
     ARel = RotMat(P1,P3,P2);

     AnySurfEllipsoid ellipsoid = {
       Radius = 0.5*{.DiameterXZ,.DiameterY,.DiameterXZ};
     };
   };


  #include "../DrawSettings/SegmentAxes.any"

  AnyRefNode I_Biceps_LH                      = {sRel = .Scale(.Data.I_Biceps_LH_pos*.Mirror);};
  AnyRefNode I_Biceps_SH                      = {sRel = .Scale(.Data.I_Biceps_SH_pos*.Mirror);};
  AnyRefNode I_Brachioradialis1                    = {sRel = .Scale(.Data.I_Brachioradialis1_pos*.Mirror);};
  AnyRefNode I_Brachioradialis2                    = {sRel = .Scale(.Data.I_Brachioradialis2_pos*.Mirror);};
  AnyRefNode I_PronatorTeresHumeral1 = {sRel = .Scale(.Data.I_PronatorTeresHumeral1_pos*.Mirror);};
  AnyRefNode I_PronatorTeresHumeral2 = {sRel = .Scale(.Data.I_PronatorTeresHumeral2_pos*.Mirror);};
  AnyRefNode I_PronatorTeresUlnare  = {sRel = .Scale(.Data.I_PronatorTeresUlnare_pos*.Mirror);};
  AnyRefNode I_SupinatorHumerus1       = {sRel = .Scale(.Data.I_SupinatorHumerus1_pos*.Mirror);};
  AnyRefNode I_SupinatorHumerus2       = {sRel = .Scale(.Data.I_SupinatorHumerus2_pos*.Mirror);};
  AnyRefNode I_SupinatorUlnare1          = {sRel = .Scale(.Data.I_SupinatorUlnare1_pos*.Mirror);};
  AnyRefNode I_SupinatorUlnare2          = {sRel = .Scale(.Data.I_SupinatorUlnare2_pos*.Mirror);};
  AnyRefNode I_PronatorQuadratus1                   = {sRel = .Scale(.Data.I_PronatorQuadratus1_pos*.Mirror);};
  AnyRefNode I_PronatorQuadratus2                   = {sRel = .Scale(.Data.I_PronatorQuadratus2_pos*.Mirror);};
  AnyRefNode O_ExtensorPollicisBrevis       = {sRel = .Scale(.Data.O_ExtensorPollicisBrevis_pos*.Mirror);};
  AnyRefNode Via_ExtensorIndicis             = {sRel = .Scale(.Data.Via_ExtensorIndicis_pos*.Mirror);};
  AnyRefNode Via_ExtensorPollicisLongus     = {sRel = .Scale(.Data.Via_ExtensorPollicisLongus_pos*.Mirror);};
  AnyRefNode Via_ExtensorPollicisBrevis     = {sRel = .Scale(.Data.Via_ExtensorPollicisBrevis_pos*.Mirror);};
  AnyRefNode Via_AbductorPollicisLongus     = {sRel = .Scale(.Data.Via_AbductorPollicisLongus_pos*.Mirror);};
  AnyRefNode Via_FlexorCarpiUlnaris         = {sRel = .Scale(.Data.Via_FlexorCarpiUlnaris_pos*.Mirror);};
  AnyRefNode Via_FlexorCarpiRadialis        = {sRel = .Scale(.Data.Via_FlexorCarpiRadialis_pos*.Mirror);};
  AnyRefNode O_FlexorPollicisLongus         = {sRel = .Scale(.Data.O_FlexorPollicisLongus_pos*.Mirror);};
  AnyRefNode O_FlexorDigitorumSuperficialis          = {sRel = .Scale(.Data.O_FlexorDigitorumSuperficialispos*.Mirror);};
  AnyRefNode Via_ExtensorCarpiRadialisBrevis        = {sRel = .Scale(.Data.Via_ExtensorCarpiRadialisBrevis_pos*.Mirror);};
  AnyRefNode Via_ExtensorCarpiRadialisLongus        = {sRel = .Scale(.Data.Via_ExtensorCarpiRadialisLongus_pos*.Mirror);};
  AnyRefNode Via_ExtensorCarpiUlnaris         =        {sRel = .Scale(.Data.Via_ExtensorCarpiUlnaris_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumSuperficialisDigit2 = {sRel = .Scale(.Data.Via_FlexorDigitorumSuperficialisDigit2_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumSuperficialisDigit3 = {sRel = .Scale(.Data.Via_FlexorDigitorumSuperficialisDigit3_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumSuperficialisDigit4 = {sRel = .Scale(.Data.Via_FlexorDigitorumSuperficialisDigit4_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumSuperficialisDigit5 = {sRel = .Scale(.Data.Via_FlexorDigitorumSuperficialisDigit5_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumProfundusDigit2     = {sRel = .Scale(.Data.Via_FlexorDigitorumProfundusDigit2_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumProfundusDigit3     = {sRel = .Scale(.Data.Via_FlexorDigitorumProfundusDigit3_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumProfundusDigit4     = {sRel = .Scale(.Data.Via_FlexorDigitorumProfundusDigit4_pos*.Mirror);};
  AnyRefNode Via_FlexorDigitorumProfundusDigit5     = {sRel = .Scale(.Data.Via_FlexorDigitorumProfundusDigit5_pos*.Mirror);};
  AnyRefNode Via_ExtensorDigitorumDigit2             = {sRel = .Scale(.Data.Via_ExtensorDigitorumDigit2_pos*.Mirror);};
  AnyRefNode Via_ExtensorDigitorumDigit3             = {sRel = .Scale(.Data.Via_ExtensorDigitorumDigit3_pos*.Mirror);};
  AnyRefNode Via_ExtensorDigitorumDigit4             = {sRel = .Scale(.Data.Via_ExtensorDigitorumDigit4_pos*.Mirror);};
  AnyRefNode Via_ExtensorDigitorumDigit5             = {sRel = .Scale(.Data.Via_ExtensorDigitorumDigit5_pos*.Mirror);};
  AnyRefNode Via_ExtensorDigitiMinimi                = {sRel = .Scale(.Data.Via_ExtensorDigitiMinimi_pos*.Mirror);};
  AnyRefNode Via_FlexorPollicisLongus                = {sRel = .Scale(.Data.Via_FlexorPollicisLongus_pos*.Mirror);};
  AnyRefNode Via_PalmarisLongus                       = {sRel = .Scale(.Data.Via_PalmarisLongus_pos*.Mirror);};

};







