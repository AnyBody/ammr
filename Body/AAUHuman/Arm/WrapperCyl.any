AnyFolder Cylinder={
  
  //scaling factor to adjust size of the wrapping cylinder based on the boney landmarks
  AnyFloat ScaleFactor = vnorm(...Seg.Humerus.Data.I_subscapularis_1_pos - ...Seg.Humerus.Data.I_subscapularis_5_pos) 
                            /vnorm(...Seg.Humerus.I_subscapularis_1.sRel - ...Seg.Humerus.I_subscapularis_5.sRel);  
  
  AnySeg Cyl={
    Mass=1e-5;
    Jii={0,0,0};
    r0=..InitialPos;

    Axes0={
      {..InitialAxes[0][0],..InitialAxes[0][1],..InitialAxes[0][2]},
      {..InitialAxes[1][0],..InitialAxes[1][1],..InitialAxes[1][2]},
      {..InitialAxes[2][0],..InitialAxes[2][1],..InitialAxes[2][2]}}
    *..InitialRot;

    AnyVar TranslateOnZ=-0.1;  //non zero to improve starting pos guess
    
    AnyRefNode node1={
       sRel={0,0,-0.03+.TranslateOnZ}*..ScaleFactor;

      AnySurfCylinder CylSurf={
        Length=....LengthScale*vnorm(..node2.sRel-..node1.sRel);
        Radius=vnorm(..node2.sRel-..node3.sRel);
      };
    };
    AnyRefNode node2={ sRel=({0,0,0.12+.TranslateOnZ}+{0,0,0.05})*..ScaleFactor;      };
    AnyRefNode node3={ sRel=({0,0.014,0.12+.TranslateOnZ}+{0,0,0.05})*..ScaleFactor;  };

  };

  
  AnySphericalJoint SphericalJoint={
    AnyRefFrame &ref1=..StartNode;
    AnyRefFrame &ref2=.Cyl.node1;
  };

  AnyKinEqSimpleDriver CylRotDrv1={
    AnyKinLinear Lin={
      AnyRefFrame &ref1=..Cyl;
      AnyRefFrame &ref2=...EndNode;
      Ref=0;
    };
    DriverPos={0,0};
    DriverVel={0,0};
    MeasureOrganizer={0,1};
  };
  
  AnyKinEqSimpleDriver CylRotDrv2={
    AnyKinRotational Rot={
      AnyRefFrame &ref2=..Cyl;
      AnyRefFrame &ref1=...StartNode;
      Type=RotVector;
    };
    MeasureOrganizer={2};
    DriverPos={0}; 
    DriverVel={0};
  };
};

