//This is the definition of the joints in the arm

/// Definition of SternoClavicular (SC) joint
AnySphericalJoint SternoClavicularJoint = {
  AnyRefNode &thorax_sc = ....Trunk.Segments.SternumSeg._SIDE_.scj;
  AnyRefNode &clavicula_sc = ..Segments.Clavicula.scj;
  Orientation = {
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = x;
  };
};

/// Defintion of Acromial-Clavicular (AC) joint
AnySphericalJoint AcromioClavicularJoint = {
  AnyRefNode &clavicula_ac = ..Segments.Clavicula.acj;
  AnyRefNode &scapula_ac = ..Segments.Scapula.acj;
  Orientation = {
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = x;
  };
};



/// Definition of Glenohumeral (GH)joint
AnySphericalJoint GlenoHumeralJoint = {
  AnyRefNode &scapula_gh = ..Segments.Scapula.gh;
  AnyRefNode &humerus_gh = ..Segments.Humerus.gh;
  ///These are the reactions of the GH joint they are normally set to "Off Off Off" if the file
  ///GHReactions.any is being used, if this is not the case the reactions should be set to "On On On"
  Constraints.Reaction.Type={Off,Off,Off};
  Orientation = {
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = y;
  };
};

//This file includes the glenohumeral reactions created by a number of pushing muscles
//if this file is not include the normal reactions of the GH joint should be set to "On On On"6
#include "GHReactions.any"


///Definition of the HumeroUlnarJoint (elbow flexion extension).
AnyRevoluteJoint HumeroUlnarJoint = {
  Axis = x;
  AnyRefNode &HumerusFE = ..Segments.Humerus.HumeroUlnar;
  AnyRefNode &UlnaFE = ..Segments.Ulna.HumeroUlnar;
};

/// This binds one DOF between humerus and Radius. Since AnyBody
/// does not have a 5 DOF joint type we implement in manually here.
AnyFolder HumeroRadialJoint = {
   AnyKinLinear Linear = {
     ///This node is located on FE axis (HumeroUlnarJoint)
     AnyRefNode &RadiusPs = ...Segments.Radius.PointHumeralRadioJoint; 
     AnyRefNode &UlnaPs = ...Segments.Humerus.HumeroUlnar;
     Ref = 0;
   };
   AnyKinEq Constraints = {
     AnyKinMeasure& Linear  = .Linear;
     MeasureOrganizer = {0};
     // Reactions are implemented explicitly below
     Reaction.Type = {Off};
   };
   ///Reactions between ulna and radius, uses a proximal node on radius, 
   ///to allow forces being applied in correct position on bone (usefull for FE exports) 
   AnyReacForce ReacForce =    {
     AnyKinMeasureOrg RadiusHumerus = {
       MeasureOrganizer = {0};
       AnyKinLinear Lin =        {
         Ref = 0;
         AnyRefFrame &Radius = .....Segments.Radius.ProximalRadiusUlnaReac; //This node is located at proximal end of radius
         AnyRefFrame &Humerus = .....Segments.Humerus.HumeroUlnar;
       };
     };
   };
};



///Proximal radio ulnar joint, creates two constraints between radius and ulna
AnyTransSphericalJoint ProximalRadioUlnarJoint = {
  AnyRefNode &RadiusPs = ..Segments.Radius.ProximalRadioUlnar;
  AnyRefNode &UlnaPs = ..Segments.Ulna.ProximalRadioUlnar;
  // Reactions are disabled, because the are implemented explicitly below.
  Constraints.Reaction.Type={Off,Off};
  Axis = x;
  Orientation = {
    Axis1 = x;
    Axis2 = y;
    Axis3 = z;
    Type = RotAxesAngles;
  };
  AnyReacForce ReacForce =   {
    AnyKinMeasureOrg RadiusUlna = {
      MeasureOrganizer = {1,2};
      AnyKinLinear Lin = {
        Ref = 0;
        AnyRefFrame &Ulna = .....Segments.Ulna.ProximalRadiusUlnaReac;
        AnyRefFrame &Radius = .....Segments.Radius.ProximalRadiusUlnaReac;
      };
    };
  };
};


///Distal radio ulnar joint, creates two constraints between radius and ulna
AnyTransSphericalJoint DistalRadioUlnarJoint = {
    AnyRefNode &UlnaPs = ..Segments.Ulna.DistalRadioUlnar;
    AnyRefNode &RadiusPs = ..Segments.Radius.DistalRadioUlnar;
    Axis = x;
};








#ifndef WRIST_EXPERIMENTAL
////Definition of wrist joint

AnyRevoluteJoint RadioCarpalJointFlexion = {
  Axis = x; //flexion extension
  AnyRefNode &radius = ..Segments.Radius.RadioCarpal;
  AnyRefNode &hand =..Segments.WristJointSeg.RadioCarpalFlexion;
};//End WristJoint

AnyRevoluteJoint RadioCarpalJointDeviation = {
 Axis = z; //Radio ulnar deviation
 AnyRefNode &radius = ..Segments.WristJointSeg.RadioCarpalDeviation;
 AnyRefNode &hand =..Segments.Hand.HandRef.RadioCarpalDeviation;
};//End WristJoint
#endif

#ifdef WRIST_EXPERIMENTAL
AnyUniversalJoint RadioCarpalJointFlexionDeviation = {
  Axis1 = x; //flexion extension
  Axis2 = z; //abduction
  AnyRefNode &radius = ..Segments.Radius.RadioCarpal;
  AnyRefNode &hand =..Segments.Hand.HandRef.RadioCarpalFlexion;
};//End WristJoint

AnyKinMeasureOrg RadioCarpalJointFlexion ={
AnyRevoluteJoint &ref=.RadioCarpalJointFlexionDeviation;
MeasureOrganizer ={0};
};
AnyKinMeasureOrg RadioCarpalJointDeviation ={
AnyRevoluteJoint &ref=.RadioCarpalJointFlexionDeviation;
MeasureOrganizer ={1};
};
#endif





//Scapula thorax gliding plane

#if BM_ARM_THORAX_SCAPULA_CONTACT == _MULTIPLE_POINT_CONTACT_

#include "EllipsoidsForMultiPointScapulaContact.any"

AnyKinMeasureExpComb ScapulaAIThoraxMeasure =
{
  // A large negative number for the exponential combination measures causes
  // the measure to put emphasis on the "penetration" of the points,
  // while "ignoring" points far from the surface.
  a ??= -400;
  AnyKinPointSurface m1 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_0.ellipsoid;
  };
  AnyKinPointSurface m2 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_2.ellipsoid;
  };
  AnyKinPointSurface m3 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_3.ellipsoid;
  };
  AnyKinPointSurface m4 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_4.ellipsoid;
  };
  AnyKinPointSurface m5 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_5.ellipsoid;
  };
  AnyKinPointSurface m6 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_6.ellipsoid;
  };
};

AnyKinEqSimpleDriver ScapulaAIThoraxNodeDriver =
{
  AnyKinMeasure &Measure1 = .ScapulaAIThoraxMeasure;
  DriverPos = {0};
  DriverVel = {0};
  Reaction.Type = {Off};
};


// Driver on this measures is in the "ScapulaConoideumDriver.any" include
// It is only applied when the shoulder rhythm is disabled.
AnyKinMeasureExpComb ScapulaTSThoraxMeasure =
{
  a ??= .ScapulaAIThoraxMeasure.a;
  AnyKinPointSurface m1 = {
    #include "ThoraxScapulaContactPoints.any"
    AnyFolder &surf3 = ...Segments.Scapula.Ellipsoid_1.ellipsoid;
  };
 };

#endif




#if BM_ARM_THORAX_SCAPULA_CONTACT == _ELLIPSOID_CONTACT_

AnyKinEqSimpleDriver ScapulaAIThoraxNodeDriver = {

  AnyKinPointSurface Measure1 = {
    AnyRefFrame &r1 = ...Segments.Scapula.ScapulaSlidingAI;
    AnySurface &s1 = ...ThoraxSegRef.ScapulaContactSurface.ScapulaEllipsoidOrigin.Elipse;
  };

  DriverPos = {0};
  DriverVel = {0};
  Reaction.Type = {Off};
};

#endif



  AnyReacForce ScapulaAIThorax_ContactReaction = {
    Type = NonNegative;
    AnyKinMeasure &ref=.ScapulaAIThoraxNodeDriver.Measure1 ;
  };



//Simple muscle model
AnyMuscleModel ReactionMuscle = {
  F0 = 5000;
  /// This must have a value to work with volume weighted recruitment
  Vol0=1e-6;
};



//Definitions of ligaments
AnyKinPLine ConoideumLigament = {
  AnyRefNode &scapula = ..Segments.Scapula.conoid;
  AnyRefNode &clavicle = ..Segments.Clavicula.conoid;
  AnyDrawPLine drwline= {Thickness=0.005;RGB={1,0,0};};
};

//Artificial muscle will produce reaction in the ligament
AnyMuscleViaPoint ConoidLigamentReaction = {
  AnyRefNode &scapula = ..Segments.Scapula.conoid;
  AnyRefNode &clavicle = ..Segments.Clavicula.conoid;
  AnyMuscleModel &ref = .ReactionMuscle;
  viewMuscle = {RGB={0,0,1};MaxStress = 75000000.000000;};
  EXCLUDE_MUSCLE_METABOLISM

};
//Artificial muscle will produce reaction in the ligament
AnyMuscleViaPoint TrapezoidLigamentReaction = {
  AnyRefNode &scapula = ..Segments.Scapula.trapezoid;
  AnyRefNode &clavicle = ..Segments.Clavicula.trapezoid;
  AnyMuscleModel &ref = .ReactionMuscle;
  viewMuscle = {RGB={0,0,1};MaxStress = 75000000.000000;};
  EXCLUDE_MUSCLE_METABOLISM

};




#if BM_ARM_THORAX_SCAPULA_CONTACT == _ELLIPSOID_CONTACT_

//measure needs to always exist
AnyKinPointSurface  ScapulaTSThoraxMeasure = {
    AnyRefFrame &r1 = ..Segments.Scapula.ScapulaSlidingTS;
    AnySurface &s1 = ..ThoraxSegRef.ScapulaContactSurface.ScapulaEllipsoidOrigin.Elipse;
  };

#endif

AnyReacForce ScapulaTSThorax_ContactReaction = {
  Type = NonNegative;
  AnyKinMeasure &ref=.ScapulaTSThoraxMeasure ;
};

#if BM_ARM_CLAVICULA_ROTATION_RHYTHM
/// Rhythm to distribute axial rotation of the clavicula
/// equally between sterno-clavicula  and acromio-clavicual joint
AnyKinEq SternoClavicularRotationRhythm  =
{
  AnyKinMeasureLinComb CombinedMeasure =
  {
    AnyKinMeasure& SC = ...InterfaceFolder.SternoClavicular.BackwardAxialRotation;
    AnyKinMeasure& AC = ...InterfaceFolder.AcromioClavicular.PosteriorTilt;
    Coef = {{1,-1}};
    OutDim = 1;
  };

  // Use a template to create the weight function, so
  // the weight values can be overridden by the user.
  Template_AnyFunConst Weights (NUMBER_OF_ELEMENTS=..nDim) = {};
  WeightFun ={&Weights.Fun};
  #if (BM_ARM_SHOULDER_RHYTHM == _RHYTHM_SOFT_) | (BM_ARM_CLAVICULA_ROTATION_RHYTHM == _RHYTHM_SOFT_)
  CType= repmat(nDim,Soft);
  #endif

};
#endif



 #include "ScapulaConoideumDriver.any"


#if BM_ARM_SHOULDER_RHYTHM

  #include "ShoulderRhythm.any"

#endif


/// Deprecated, Use the name HumeroUlnarJoint instead. Left here for backwards Compatibility.
AnyFolder &FE = HumeroUlnarJoint;


/// Deprecated. Please use the forearm rotation measure
/// in the interface folder. Left here for backwards compatibility.
AnyKinMeasureOrg PS = {
  AnyKinRotational &Jnt = .ProximalRadioUlnarJoint.Orientation;
  MeasureOrganizer = {0};
};


//Deprecated use RadioCarpalJointFlexion instead, left here for backwards compatibility.
AnyRevoluteJoint &WristJointFlexion = RadioCarpalJointFlexion;
//Deprecated use RadioCarpalJointDeviation instead, left here for backwards compatibility.
AnyRevoluteJoint &WristJointDeviation = RadioCarpalJointDeviation;
