
// Adds wrapping surfaces for Pectoralis, Serratus, and Latisimus. 

#if (BM_TRUNK_THORACIC_MODEL == _THORACIC_MODEL_FLEXIBLE_ | BM_TRUNK_THORACIC_MODEL == _THORACIC_MODEL_FLEXIBLE_KINEMATIC_ONLY_)
  AnyFolder& ThoraxWrappingSufaceSegRef = ....Trunk.ThoracicCavity.Inertia.segment._SIDE_;
#else
  AnyFolder& ThoraxWrappingSufaceSegRef = ....Trunk.Segments.ThoraxSeg._SIDE_;
#endif
  
ThoraxWrappingSufaceSegRef = {

  AnySurfEllipsoid PectoralisWrappingEllipsoid = {
    // Use pectoralis origins to define the main axis of the ellip
    ARel = RotMat({0,0,0}, {0,0,1}, ...PectoralisMajorSternocostal.PectoralisMajorSternocostal4.Org.sRel-...PectoralisMajorSternocostal.PectoralisMajorSternocostal9.Org.sRel)*{{0,0,1},{0,-1,0},{1,0,0}};
    sRel = 0.5*...PectoralisMajorSternocostal.PectoralisMajorSternocostal6.Org.sRel + 0.5*......Trunk.Segments.T7Seg.T6T7JntNode.sRel;  
    Radius = { 
      0.98*(...PectoralisMajorSternocostal.PectoralisMajorSternocostal6.Org.sRel-sRel)*ARel'[0]',
      (......Trunk.Segments.T1Seg.T1C7JntNode.sRel-sRel)*ARel'[1]',
      #if _SIDE_==Right
      1.04*(......Trunk.Segments.Right.R5Seg.IC_R5_Ant_R_Ins.sRel-sRel)*ARel'[2]',
      #else 
      1.04*(sRel-......Trunk.Segments.Left.R5Seg.IC_R5_Ant_L_Ins.sRel)*ARel'[2]',
      #endif
    };
  };

  
  // Ellipsoids which are used for wrapping of serratus anterior.
  
  AnySurfEllipsoid SerratusAnteriorWrappingEllipsoid_1to5 = {
    ARel = RotMat({0,0,0}, {0,0,1}, ...SerratusAnterior.SerratusAnterior5.Org.sRel-...SerratusAnterior.SerratusAnterior1.Org.sRel)*{{0,0,1},{0,-1,0},{1,0,0}};
    sRel = {
      0.5*...SerratusAnterior.SerratusAnterior5.Org.sRel[0] + 0.5*......Trunk.Segments.T6Seg.T5T6JntNode.sRel[0],
      ...SerratusAnterior.SerratusAnterior5.Org.sRel[1], 
      0
    };
    Radius = abs({ 
      #if _SIDE_ == Right 
      0.98*(......Trunk.Segments.Right.R7Seg.IliocostalisLumborumR7NodeR.sRel-sRel)*ARel'[0]',
      #else
      0.98*(......Trunk.Segments.Left.R7Seg.IliocostalisLumborumR7NodeL.sRel-sRel)*ARel'[0]',
      #endif
      1.05*(......Trunk.Segments.T1Seg.T1C7JntNode.sRel-sRel)*ARel'[1]',
      1.0*(...SerratusAnterior.SerratusAnterior5.Org.sRel-sRel)*ARel'[2]',
    });
  };
    
  AnySurfEllipsoid SerratusAnteriorWrappingEllipsoid_6to9 = {
    ARel = RotMat({0,0,0}, {0,0,1}, ...SerratusAnterior.SerratusAnterior7.Org.sRel-...SerratusAnterior.SerratusAnterior1.Org.sRel)*{{0,0,1},{0,-1,0},{1,0,0}};
    sRel = {
      0.5*...SerratusAnterior.SerratusAnterior6.Org.sRel[0] + 0.5*......Trunk.Segments.T7Seg.T6T7JntNode.sRel[0],
      ...SerratusAnterior.SerratusAnterior6.Org.sRel[1], 
      0
    };
    Radius = abs({ 
      #if _SIDE_ == Right 
      1.2*(......Trunk.Segments.Right.R7Seg.IliocostalisLumborumR7NodeR.sRel-sRel)*ARel'[0]',
      #else
      1.2*(......Trunk.Segments.Left.R7Seg.IliocostalisLumborumR7NodeL.sRel-sRel)*ARel'[0]',
      #endif
      0.90*(......Trunk.Segments.T1Seg.T1C7JntNode.sRel-sRel)*ARel'[1]',
      1*(...SerratusAnterior.SerratusAnterior8.Org.sRel-sRel)*ARel'[2]',
    });
  };

    
  // Ellipsoid which are used for wrapping of Latisimus dorsi        
  AnyRefNode EllipsoidLatissimusOrigin = {
    ARel = RotMat(20*pi/180,z)*RotMat(20*pi/180*.Sign,y);
    sRel = .Scale({0.080, 0.27, .Sign*0.056});  
    
    AnyRefNode node_anterior =  {sRel=..Scale({0.115,0,0});};
    AnyRefNode node_posterior = {sRel=..Scale({-0.115,0,0});};
    AnyRefNode node_superior =  {sRel=..Scale({0,0.18,0});};
    AnyRefNode node_inferior =  {sRel=..Scale({0,-0.18,0});};
    AnyRefNode node_lateral =   {sRel=..Scale({0,0,..Sign*0.090});};
    AnyRefNode node_medial =    {sRel=..Scale({0,0,..Sign*-0.090});};
    
    AnySurfEllipsoid EllipsoidSurf = { 
      Radius = {
        abs(vnorm((.node_anterior.sRel-.node_posterior.sRel)/2)),
        abs(vnorm((.node_superior.sRel-.node_inferior.sRel)/2)),
        abs(vnorm((.node_lateral.sRel-.node_medial.sRel)/2)),
      }; 
    }; 
  };  

  AnyRefNode O_LatissimusDorsi1_via = {sRel = .Scale({-0.04598001-0.012, 0.20092+0.083, 0.08076-0.031})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi2_via = {sRel = .Scale({-0.04598001-0.011, 0.20092+0.071, 0.08076-0.027})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi3_via = {sRel = .Scale({-0.04598001-0.009, 0.20092+0.053, 0.08076-0.021})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi4_via = {sRel = .Scale({-0.04598001-0.007, 0.20092+0.035, 0.08076-0.015})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi5_via = {sRel = .Scale({-0.04598001-0.005, 0.20092+0.02, 0.08076-0.01})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi6_via = {sRel = .Scale({-0.04598001, 0.20092, 0.08076})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi7_via = {sRel = .Scale({-0.03664668, 0.1915867, 0.09309333})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi8_via = {sRel = .Scale({-0.02431334, 0.1822533, 0.1054267})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi9_via = {sRel = .Scale({-0.00331, 0.1705867, 0.11876})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi10_via = {sRel = .Scale({0.01264668, 0.1622533, 0.12376})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};
  AnyRefNode O_LatissimusDorsi11_via = {sRel = .Scale({0.02801999, 0.15392, 0.12876})*...WrappingSurfaces.TrunkNodeAttachement.Mirror;};

};