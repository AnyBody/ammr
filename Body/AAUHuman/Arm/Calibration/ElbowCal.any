AnyFolder ElbowCal ={
  AnyComponentDefinition obj = {};
  AnyFolder &ThoraxSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &CervicalSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &LumbarSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &LumbarSpineJoints =  ...BodyModel.Trunk.Joints.Lumbar;
  AnyFolder &LumbarCervicalJoints =  ...BodyModel.Trunk.Joints.Cervical;

  AnyFolder &SegSideCal = .SideHumanFolderRef.ShoulderArm.Segments;
  AnyFolder &JointsSideCal = .SideHumanFolderRef.ShoulderArm.Joints;

  AnyFolder &MusCalSide =  .SideHumanFolderRef.ShoulderArm.Muscles;
  AnyFolder &MusParCalSide =  .SideHumanFolderRef.ShoulderArm.MuscleModels;

  AnyFixedRefFrame ground = {
    AnyRefNode node = {
      ARel=.....BodyModel.Trunk.Segments.ThoraxSeg.Axes0;
      sRel=.....BodyModel.Trunk.Segments.ThoraxSeg.r0;
    };
  };

  AnySphericalJoint ThoraxGround={
    AnyRefFrame &Ground = .ground.node;
    AnyRefFrame &Sacrum= ....BodyModel.Trunk.Segments.ThoraxSeg;
  };

  AnyKinEqSimpleDriver  ThoraxGroundDriver = {
    AnySphericalJoint &Jnt = .ThoraxGround;
    DriverPos = {0,0,0};
    DriverVel = {0,0,0};
    Reaction.Type = {Off,Off,Off};
  };


  #if BM_ARM_SHOULDER_RHYTHM == ON
  #include "ExcludeShoulderRhythm.any"
  #endif


  AnyFloat Neutral = {  0,  0,   0,  -23,  12} * pi/180;

  AnyFloat ElbowExtended     = {  0,  0,  0, -23,  12} * pi/180;
  AnyFloat ElbowFlexed       = {  0,  0,  0, -23,  12} * pi/180;
  AnyFloat ElbowPronation    = {  0,  0,  0, -23,  12} * pi/180;
  AnyFloat ElbowSupination   = {  0,  0,  0, -23,  12} * pi/180;
  AnyFloat ShoulderStretch   = { 90,120,  0,   5,  20} * pi/180;
  AnyFloat PectoralisStretch = { 90,-10, 90, -23,  20} * pi/180;

  AnyFloat ElbowFlexPosData  = {  0,150, 90, 90,  0,  0} * pi/180;

  AnyFloat ElbowProPosData   = {  0,  0,-90, 90,  0,  0} * pi/180;

  AnyFloat GHPosData = repmat(1,{
      ElbowExtended,
      ElbowFlexed,
      ElbowPronation,
      ElbowSupination,
      ShoulderStretch,
      PectoralisStretch
  }');


  //Neck driver
  AnyKinEqSimpleDriver NeckJntDriver = {
    AnyKinMeasure& fe = ....BodyModel.Interface.Trunk.SkullThoraxFlexion;
    AnyKinMeasure& ar = ....BodyModel.Interface.Trunk.SkullThoraxRotation;
    AnyKinMeasure& lb = ....BodyModel.Interface.Trunk.SkullThoraxLateralBending;
    DriverPos = pi/180*{0,0,0};
    DriverVel = pi/180*{0,0,0};
    Reaction.Type = {Off, Off, Off};
  };

  AnyKinEqSimpleDriver ThoraxDriver ={
    AnyKinMeasure& Ref3 = ....BodyModel.Interface.Trunk.PelvisThoraxRotation;
    AnyKinMeasure& Ref1 = ....BodyModel.Interface.Trunk.PelvisThoraxExtension;
    AnyKinMeasure& Ref2 = ....BodyModel.Interface.Trunk.PelvisThoraxLateralBending;
    DriverPos = pi/180*{0,0,0};
    DriverVel = pi/180*{0,0,0};
    Reaction.Type = {On,On,On};
  };

  AnyKinEqSimpleDriver WristFlexionDriver ={
      AnyKinMeasure& WF = ..SideInterfaceFolderRef.WristFlexion;
      DriverPos = {0}*pi/180;
      DriverVel = {0};
      Reaction.Type={On};
    };

    AnyKinEqSimpleDriver WristAbductionDriver ={
      AnyKinMeasure& WA = ..SideInterfaceFolderRef.WristAbduction;
      DriverPos = {0}*pi/180;
      DriverVel = {0};
      Reaction.Type={On};
    };

  AnyKinEqInterPolDriver FEDriver ={
    AnyKinMeasure& FE = ..SideInterfaceFolderRef.ElbowFlexion;
    Type = PiecewiseLinear;
    T    = linspace(0,1,SizesOf(Data)[1]);
    Data = {.ElbowFlexPosData};
    Reaction.Type = {Off};
  };

  AnyKinEqInterPolDriver PSDriver ={
    AnyKinMeasure& PS = ..SideInterfaceFolderRef.ElbowPronation;
    Type = PiecewiseLinear;
    T    = linspace(0,1,SizesOf(Data)[1]);
    Data = {.ElbowProPosData};
    Reaction.Type = {Off};
  };


  AnyKinEqInterPolDriver GHDriver ={
    AnyKinMeasure& GHAbduction = ..SideInterfaceFolderRef.GlenohumeralAbduction;
    AnyKinMeasure& GHFlexion = ..SideInterfaceFolderRef.GlenohumeralFlexion;
    AnyKinMeasure& GHExtRot = ..SideInterfaceFolderRef.GlenohumeralExternalRotation;
    AnyKinMeasure& SCProtraction = ..SideInterfaceFolderRef.SternoClavicularProtraction;
    AnyKinMeasure& SCElevation = ..SideInterfaceFolderRef.SternoClavicularElevation;
    Type = PiecewiseLinear;
    T = linspace(0,1,SizesOf(Data)[1]);
    Data = .GHPosData;
    Reaction.Type = repmat(nDim,{Off});
  };


  //---------------------------------------------------------------------
  //Finger drivers
  #if BM_ARM_DETAILED_HAND
  //finger1
  AnyKinEqSimpleDriver CMC1Flexion={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.CMCFlexion;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };

  AnyKinEqSimpleDriver CMC1Abduction={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.CMCAbduction;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };



  AnyKinEqSimpleDriver MCP1Flexion=
  {
    AnyRevoluteJoint &ref=..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.MCPFlexion;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver MCP1Abduction=
  {
    AnyRevoluteJoint &ref=..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.MCPAbduction;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };

  AnyKinEqSimpleDriver DIP1={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.DIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };


 // finger2
//    AnyKinEqSimpleDriver CMC2={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP2={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP2={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP2={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };



 // finger3
//    AnyKinEqSimpleDriver CMC3={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP3={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP3={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP3={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };


  //finger4
//    AnyKinEqSimpleDriver CMC4={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.CMC;
//      DriverPos={0,0}*pi/180;
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP4={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP4={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP4={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.DIP;
      DriverPos={0}*pi/180;
      DriverVel={0};
    };


 // finger5
//    AnyKinEqSimpleDriver CMC5={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP5={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP5={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP5={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };
  #endif

  AnyString MusPrefix = CompleteNameOf(&.SideHumanFolderRef.ShoulderArm.Muscles);

  AnyObjectPtrArray BicepsBrachiiBreveMuscles = ObjSearch(MusPrefix+".*.BicepsBrachiiBreve", "AnyMuscle");
  AnyFloat  BicepsBrachiiBreveRmin = repmat(NumElemOf(BicepsBrachiiBreveMuscles), ..RMin);
  AnyFloat  BicepsBrachiiBreveRmax = repmat(NumElemOf(BicepsBrachiiBreveMuscles), ..RMax);

  AnyObjectPtrArray BicepsBrachiiLongumMuscles = ObjSearch(MusPrefix+".*.BicepsBrachiiLongum", "AnyMuscle");
  AnyFloat  BicepsBrachiiLongumRmin = repmat(NumElemOf(BicepsBrachiiLongumMuscles), ..RMin);
  AnyFloat  BicepsBrachiiLongumRmax = repmat(NumElemOf(BicepsBrachiiLongumMuscles), ..RMax);

  AnyObjectPtrArray BrachialisMuscles = ObjSearch(MusPrefix+".*.Brachialis*", "AnyMuscle");
  AnyFloat  BrachialisRmin = repmat(NumElemOf(BrachialisMuscles), ..RMin);
  AnyFloat  BrachialisRmax = repmat(NumElemOf(BrachialisMuscles), ..RMax);

  AnyObjectPtrArray TricepsMuscles = ObjSearch(MusPrefix+".*.Triceps*", "AnyMuscle");
  AnyFloat  TricepsRmin = repmat(NumElemOf(TricepsMuscles), ..RMin);
  AnyFloat  TricepsRmax = repmat(NumElemOf(TricepsMuscles), ..RMax);

  AnyObjectPtrArray BrachioradialisMuscles = ObjSearch(MusPrefix+".*.Brachioradialis*", "AnyMuscle");
  AnyFloat  BrachioradialisRmin = repmat(NumElemOf(BrachioradialisMuscles), ..RMin);
  AnyFloat  BrachioradialisRmax = repmat(NumElemOf(BrachioradialisMuscles), ..RMax);

  AnyObjectPtrArray AnconeusMuscles = ObjSearch(MusPrefix+".*.Anconeus*", "AnyMuscle");
  AnyFloat  AnconeusRmin = repmat(NumElemOf(AnconeusMuscles), ..RMin);
  AnyFloat  AnconeusRmax = repmat(NumElemOf(AnconeusMuscles), ..RMax);

  AnyObjectPtrArray PronatorTeresHumeralMuscles = ObjSearch(MusPrefix+".*.PronatorTeresHumeral*", "AnyMuscle");
  AnyFloat  PronatorTeresHumeralRmin = repmat(NumElemOf(PronatorTeresHumeralMuscles), ..RMin);
  AnyFloat  PronatorTeresHumeralRmax = repmat(NumElemOf(PronatorTeresHumeralMuscles), ..RMax);

  AnyObjectPtrArray PronatorTeresUlnareMuscles = ObjSearch(MusPrefix+".*.PronatorTeresUlnare", "AnyMuscle");
  AnyFloat  PronatorTeresUlnareRmin = repmat(NumElemOf(PronatorTeresUlnareMuscles), ..RMin);
  AnyFloat  PronatorTeresUlnareRmax = repmat(NumElemOf(PronatorTeresUlnareMuscles), ..RMax);

  AnyObjectPtrArray SupinatorHumerusMuscles = ObjSearch(MusPrefix+".*.SupinatorHumerus*", "AnyMuscle");
  AnyFloat  SupinatorHumerusRmin = repmat(NumElemOf(SupinatorHumerusMuscles), ..RMin);
  AnyFloat  SupinatorHumerusRmax = repmat(NumElemOf(SupinatorHumerusMuscles), ..RMax);

  AnyObjectPtrArray SupinatorUlnareMuscles = ObjSearch(MusPrefix+".*.SupinatorUlnare*", "AnyMuscle");
  AnyFloat  SupinatorUlnareRmin = repmat(NumElemOf(SupinatorUlnareMuscles), ..RMin);
  AnyFloat  SupinatorUlnareRmax = repmat(NumElemOf(SupinatorUlnareMuscles), ..RMax);

  AnyObjectPtrArray PronatorQuadratusMuscles = ObjSearch(MusPrefix+".*.PronatorQuadratus*", "AnyMuscle");
  AnyFloat  PronatorQuadratusRmin = repmat(NumElemOf(PronatorQuadratusMuscles), ..RMin);
  AnyFloat  PronatorQuadratusRmax = repmat(NumElemOf(PronatorQuadratusMuscles), ..RMax);

};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy ArmCalibrationStudyElbow = {
  AnyFolder &ref=.ElbowCal;
  nStep = SizesOf(ref.GHPosData)[1];

  Kinematics.SmallStepAssumptionOnOff = Off;
  Kinematics.PosAnalysisOnlyOnOff = On;
  InitialConditions.PosAnalysisOnlyOnOff = On;

  MuscleArr = arrcat(arrcat(
      ref.BicepsBrachiiBreveMuscles,
      ref.BicepsBrachiiLongumMuscles,
      ref.BrachialisMuscles,
      ref.TricepsMuscles,
      ref.BrachioradialisMuscles,
      ref.AnconeusMuscles,
      ref.PronatorTeresHumeralMuscles,
      ref.PronatorTeresUlnareMuscles,
      ref.SupinatorHumerusMuscles,
      ref.SupinatorUlnareMuscles),
      ref.PronatorQuadratusMuscles
  );
  RminMuscleFiber = arrcat(arrcat(
      ref.BicepsBrachiiBreveRmin,
      ref.BicepsBrachiiLongumRmin,
      ref.BrachialisRmin,
      ref.TricepsRmin,
      ref.BrachioradialisRmin,
      ref.AnconeusRmin,
      ref.PronatorTeresHumeralRmin,
      ref.PronatorTeresUlnareRmin,
      ref.SupinatorHumerusRmin,
      ref.SupinatorUlnareRmin),
      ref.PronatorQuadratusRmin
  );
  RmaxMuscleFiber = arrcat(arrcat(
      ref.BicepsBrachiiBreveRmax,
      ref.BicepsBrachiiLongumRmax,
      ref.BrachialisRmax,
      ref.TricepsRmax,
      ref.BrachioradialisRmax,
      ref.AnconeusRmax,
      ref.PronatorTeresHumeralRmax,
      ref.PronatorTeresUlnareRmax,
      ref.SupinatorHumerusRmax,
      ref.SupinatorUlnareRmax),
      ref.PronatorQuadratusRmax
  );



};


