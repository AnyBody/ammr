AnyFolder ShoulderCal ={
  AnyComponentDefinition obj = {};
  AnyFolder &ThoraxSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &CervicalSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &LumbarSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &LumbarSpineJoints =  ...BodyModel.Trunk.Joints.Lumbar;
  AnyFolder &LumbarCervicalJoints =  ...BodyModel.Trunk.Joints.Cervical;

  AnyFolder &SegSideCal = .SideHumanFolderRef.ShoulderArm.Segments;
  AnyFolder &JointsSideCal = .SideHumanFolderRef.ShoulderArm.Joints;

  AnyFolder &MusCalSide =  .SideHumanFolderRef.ShoulderArm.Muscles;
  AnyFolder &MusParCalSide =  .SideHumanFolderRef.ShoulderArm.MuscleModels;

  AnyFixedRefFrame ground = {
    AnyRefNode node = {
      ARel=.....BodyModel.Trunk.Segments.ThoraxSeg.Axes0;
      sRel=.....BodyModel.Trunk.Segments.ThoraxSeg.r0;
    };
  };



  AnySphericalJoint ThoraxGround={
    AnyRefFrame &Ground = .ground.node;
    AnyRefFrame &Sacrum= ....BodyModel.Trunk.Segments.ThoraxSeg;
  };

  AnyKinEqSimpleDriver  ThoraxGroundDriver = {
    AnySphericalJoint &Jnt = .ThoraxGround;
    DriverPos = {0,0,0};
    DriverVel = {0,0,0};
    Reaction.Type = {Off,Off,Off};
  };


  #if BM_ARM_SHOULDER_RHYTHM == ON
  #include "ExcludeShoulderRhythm.any"
  #endif

  AnyFloat Neutral = {  0,  0,   0,  -23,  12} * pi/180;

  //                               GH Abd   GH Flex      GH ext       SC Pro    SC Elev
  AnyFloat Abduction         = {       178,       0,          0,         -35,         40} * pi/180;
  AnyFloat ExternalRotation   ={        89,       0,         89,         -36,         15} * pi/180;
  AnyFloat InternalRotation  = {        89,       0,        -70,         -23,         15} * pi/180;
  AnyFloat HorizontalFlexion = {        89,     120,          0,          10,          9} * pi/180;
  AnyFloat Adduction         = {       -44,      25,        -25,          15,         -5} * pi/180;
  AnyFloat VertivalExtension = {         0,     -50,          0,         -28,          0} * pi/180;
  AnyFloat VerticalFlexion   = {         0,     160,        -15,         -10,         35} * pi/180;

  AnyFloat ElbowFlexPosData  = {0,90,90,0,0,0,0} * pi/180;

  AnyFloat GHPosData         = repmat(1,{
      Abduction,
      ExternalRotation,
      InternalRotation,
      HorizontalFlexion,
      Adduction,
      Neutral,
      VertivalExtension,
      VerticalFlexion
  }');


  //Neck driver
  AnyKinEqSimpleDriver NeckJntDriver = {
    AnyKinMeasure& fe = ....BodyModel.Interface.Trunk.SkullThoraxFlexion;
    AnyKinMeasure& ar = ....BodyModel.Interface.Trunk.SkullThoraxRotation;
    AnyKinMeasure& lb = ....BodyModel.Interface.Trunk.SkullThoraxLateralBending;
    DriverPos = pi/180*{0,0,0};
    DriverVel = pi/180*{0,0,0};
    Reaction.Type = {Off, Off, Off};
  };

  AnyKinEqSimpleDriver ThoraxDriver ={
    AnyKinMeasure& Ref3 = ....BodyModel.Interface.Trunk.PelvisThoraxRotation;
    AnyKinMeasure& Ref1 = ....BodyModel.Interface.Trunk.PelvisThoraxExtension;
    AnyKinMeasure& Ref2 = ....BodyModel.Interface.Trunk.PelvisThoraxLateralBending;
    DriverPos = pi/180*{0,0,0};
    DriverVel = pi/180*{0,0,0};
    Reaction.Type = {On,On,On};
  };

  AnyKinEqSimpleDriver WristFlexionDriver ={
      AnyKinMeasure& WF = ..SideInterfaceFolderRef.WristFlexion;
      DriverPos = {0}*pi/180;
      DriverVel = {0};
      Reaction.Type={On};
    };

    AnyKinEqSimpleDriver WristAbductionDriver ={
      AnyKinMeasure& WA = ..SideInterfaceFolderRef.WristAbduction;
      DriverPos = {0}*pi/180;
      DriverVel = {0};
      Reaction.Type={On};
    };

  AnyKinEqInterPolDriver FEDriver ={
    AnyKinMeasure& FE = ..SideInterfaceFolderRef.ElbowFlexion;
    Type = PiecewiseLinear;
    T    = linspace(0,1,SizesOf(Data)[1]);
    Data = {.ElbowFlexPosData};
    Reaction.Type = {Off};
  };

  AnyKinEqInterPolDriver PSDriver ={
    AnyKinMeasure& PS = ..SideInterfaceFolderRef.ElbowPronation;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0};
    Data = {{30,30}}*pi/180;
    Reaction.Type = {Off};
  };

  AnyKinEqInterPolDriver GHDriver ={
    AnyKinMeasure& GHAbduction = ..SideInterfaceFolderRef.GlenohumeralAbduction;
    AnyKinMeasure& GHFlexion = ..SideInterfaceFolderRef.GlenohumeralFlexion;
    AnyKinMeasure& GHExtRot = ..SideInterfaceFolderRef.GlenohumeralExternalRotation;
    AnyKinMeasure& SCProtraction = ..SideInterfaceFolderRef.SternoClavicularProtraction;
    AnyKinMeasure& SCElevation = ..SideInterfaceFolderRef.SternoClavicularElevation;
    Type = PiecewiseLinear;
    T = linspace(0,1,SizesOf(Data)[1]);
    Data = .GHPosData;
    Reaction.Type = repmat(nDim,{Off});
  };

  //---------------------------------------------------------------------
  //Finger drivers
  #if BM_ARM_DETAILED_HAND
  //finger1
  AnyKinEqSimpleDriver CMC1Flexion={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.CMCFlexion;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };

  AnyKinEqSimpleDriver CMC1Abduction={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.CMCAbduction;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };



  AnyKinEqSimpleDriver MCP1Flexion=
  {
    AnyRevoluteJoint &ref=..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.MCPFlexion;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver MCP1Abduction=
  {
    AnyRevoluteJoint &ref=..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.MCPAbduction;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };

  AnyKinEqSimpleDriver DIP1={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.DIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };


 // finger2
//    AnyKinEqSimpleDriver CMC2={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP2={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP2={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP2={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };



 // finger3
//    AnyKinEqSimpleDriver CMC3={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP3={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP3={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP3={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };


  //finger4
//    AnyKinEqSimpleDriver CMC4={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.CMC;
//      DriverPos={0,0}*pi/180;
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP4={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP4={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP4={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.DIP;
      DriverPos={0}*pi/180;
      DriverVel={0};
    };


 // finger5
//    AnyKinEqSimpleDriver CMC5={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP5={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP5={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP5={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };
  #endif

  AnyString MusPrefix = CompleteNameOf(&.SideHumanFolderRef.ShoulderArm.Muscles);

  AnyObjectPtrArray CoracobrachialisMuscles = ObjSearch(MusPrefix+".*.Coracobrachialis*", "AnyMuscle");
  AnyFloat  CoracobrachialisRmin = repmat(NumElemOf(CoracobrachialisMuscles), ..RMin);
  AnyFloat  CoracobrachialisRmax = repmat(NumElemOf(CoracobrachialisMuscles), ..RMax);

  #if BM_ARM_DELTOID_WRAPPING == _DELTOID_WRAPPING_RAKE_
  AnyObjectPtrArray deltoideus_scapularMuscles = ObjSearch(MusPrefix+".*.deltoideus_scapular*", "AnyMuscle");
  #else
  AnyObjectPtrArray deltoideus_scapularMuscles = arrcat(
    ObjSearch(MusPrefix+".*.DeltoideusPosterior*", "AnyMuscle"),
    ObjSearch(MusPrefix+".*.DeltoideusLateral*", "AnyMuscle")
  );
  #endif
  AnyFloat  deltoideus_scapularRmin = repmat(NumElemOf(deltoideus_scapularMuscles), ..RMin);
  AnyFloat  deltoideus_scapularRmax = repmat(NumElemOf(deltoideus_scapularMuscles), ..RMax);

  #if BM_ARM_DELTOID_WRAPPING == _DELTOID_WRAPPING_RAKE_
  AnyObjectPtrArray deltoideus_clavicularMuscles = ObjSearch(MusPrefix+".*.deltoideus_clavicular*", "AnyMuscle");
  #else
  AnyObjectPtrArray deltoideus_clavicularMuscles = ObjSearch(MusPrefix+".*.DeltoideusAnterior*", "AnyMuscle");
  #endif
  AnyFloat  deltoideus_clavicularRmin = repmat(NumElemOf(deltoideus_clavicularMuscles), ..RMin);
  AnyFloat  deltoideus_clavicularRmax = repmat(NumElemOf(deltoideus_clavicularMuscles), ..RMax);

  AnyObjectPtrArray InfraspinatusMuscles = ObjSearch(MusPrefix+".*.Infraspinatus*", "AnyMuscle");
  AnyFloat  InfraspinatusRmin = repmat(NumElemOf(InfraspinatusMuscles), ..RMin);
  AnyFloat  InfraspinatusRmax = repmat(NumElemOf(InfraspinatusMuscles), ..RMax);

  AnyObjectPtrArray PectoralisMajorSternocostalMuscles = ObjSearch(MusPrefix+".*.PectoralisMajorSternocostal*", "AnyMuscle");
  AnyFloat  PectoralisMajorSternocostalRmin = repmat(NumElemOf(PectoralisMajorSternocostalMuscles), ..RMin);
  AnyFloat  PectoralisMajorSternocostalRmax = repmat(NumElemOf(PectoralisMajorSternocostalMuscles), ..RMax);

  AnyObjectPtrArray PectoralisMajorClavicularMuscles = ObjSearch(MusPrefix+".*.PectoralisMajorClavicular*", "AnyMuscle");
  AnyFloat  PectoralisMajorClavicularRmin = repmat(NumElemOf(PectoralisMajorClavicularMuscles), ..RMin);
  AnyFloat  PectoralisMajorClavicularRmax = repmat(NumElemOf(PectoralisMajorClavicularMuscles), ..RMax);

  AnyObjectPtrArray PectoralisMinorMuscles = ObjSearch(MusPrefix+".*.PectoralisMinor*", "AnyMuscle");
  AnyFloat  PectoralisMinorRmin = repmat(NumElemOf(PectoralisMinorMuscles), ..RMin);
  AnyFloat  PectoralisMinorRmax = repmat(NumElemOf(PectoralisMinorMuscles), ..RMax);

  AnyObjectPtrArray RhomboideusMuscles = ObjSearch(MusPrefix+".*.Rhomboideus*", "AnyMuscle");
  AnyFloat  RhomboideusRmin = repmat(NumElemOf(RhomboideusMuscles), ..RMin);
  AnyFloat  RhomboideusRmax = repmat(NumElemOf(RhomboideusMuscles), ..RMax);

  AnyObjectPtrArray SerratusAnteriorMuscles = ObjSearch(MusPrefix+".*.SerratusAnterior*", "AnyMuscle");
  AnyFloat  SerratusAnteriorRmin = repmat(NumElemOf(SerratusAnteriorMuscles), ..RMin);
  AnyFloat  SerratusAnteriorRmax = repmat(NumElemOf(SerratusAnteriorMuscles), ..RMax);

  AnyObjectPtrArray SubscapularisMuscles = ObjSearch(MusPrefix+".*.Subscapularis*", "AnyMuscle");
  AnyFloat  SubscapularisRmin = repmat(NumElemOf(SubscapularisMuscles), ..RMin);
  AnyFloat  SubscapularisRmax = repmat(NumElemOf(SubscapularisMuscles), ..RMax);

  AnyObjectPtrArray SupraspinatusMuscles = ObjSearch(MusPrefix+".*.Supraspinatus*", "AnyMuscle");
  AnyFloat  SupraspinatusRmin = repmat(NumElemOf(SupraspinatusMuscles), ..RMin);
  AnyFloat  SupraspinatusRmax = repmat(NumElemOf(SupraspinatusMuscles), ..RMax);

  AnyObjectPtrArray TeresMajorMuscles = ObjSearch(MusPrefix+".*.TeresMajor*", "AnyMuscle");
  AnyFloat  TeresMajorRmin = repmat(NumElemOf(TeresMajorMuscles), ..RMin);
  AnyFloat  TeresMajorRmax = repmat(NumElemOf(TeresMajorMuscles), ..RMax);

  AnyObjectPtrArray TeresMinorMuscles = ObjSearch(MusPrefix+".*.TeresMinor*", "AnyMuscle");
  AnyFloat  TeresMinorRmin = repmat(NumElemOf(TeresMinorMuscles), ..RMin);
  AnyFloat  TeresMinorRmax = repmat(NumElemOf(TeresMinorMuscles), ..RMax);

  AnyObjectPtrArray TrapeziusScapularMuscles = ObjSearch(MusPrefix+".*.TrapeziusScapular*", "AnyMuscle");
  AnyFloat  TrapeziusScapularRmin = repmat(NumElemOf(TrapeziusScapularMuscles), ..RMin);
  AnyFloat  TrapeziusScapularRmax = repmat(NumElemOf(TrapeziusScapularMuscles), ..RMax);


};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy ArmCalibrationStudyShoulder = {
  AnyFolder &ref=.ShoulderCal;
  nStep = SizesOf(ref.GHPosData)[1]*6;
  FiberAndTendonLengthAdjustment.CalibrateTendonAtMaxForceOnOff = On;
  Kinematics.SmallStepAssumptionOnOff = On;
  Kinematics.PosAnalysisOnlyOnOff = On;
  InitialConditions.PosAnalysisOnlyOnOff = On;

  MuscleArr = arrcat(arrcat(
      ref.CoracobrachialisMuscles,
      ref.deltoideus_scapularMuscles,
      ref.deltoideus_clavicularMuscles,
      ref.InfraspinatusMuscles,
      ref.PectoralisMajorSternocostalMuscles,
      ref.PectoralisMajorClavicularMuscles,
      ref.PectoralisMinorMuscles,
      ref.RhomboideusMuscles,
      ref.SerratusAnteriorMuscles),
      ref.SubscapularisMuscles,
      ref.SupraspinatusMuscles,
      ref.TeresMajorMuscles,
      ref.TeresMinorMuscles,
      ref.TrapeziusScapularMuscles
  );
  RminMuscleFiber = arrcat(arrcat(
      ref.CoracobrachialisRmin,
      ref.deltoideus_scapularRmin,
      ref.deltoideus_clavicularRmin,
      ref.InfraspinatusRmin,
      ref.PectoralisMajorSternocostalRmin,
      ref.PectoralisMajorClavicularRmin,
      ref.PectoralisMinorRmin,
      ref.RhomboideusRmin,
      ref.SerratusAnteriorRmin),
      ref.SubscapularisRmin,
      ref.SupraspinatusRmin,
      ref.TeresMajorRmin,
      ref.TeresMinorRmin,
      ref.TrapeziusScapularRmin
  );
  RmaxMuscleFiber = arrcat(arrcat(
      ref.CoracobrachialisRmax,
      ref.deltoideus_scapularRmax,
      ref.deltoideus_clavicularRmax,
      ref.InfraspinatusRmax,
      ref.PectoralisMajorSternocostalRmax,
      ref.PectoralisMajorClavicularRmax,
      ref.PectoralisMinorRmax,
      ref.RhomboideusRmax,
      ref.SerratusAnteriorRmax),
      ref.SubscapularisRmax,
      ref.SupraspinatusRmax,
      ref.TeresMajorRmax,
      ref.TeresMinorRmax,
      ref.TrapeziusScapularRmax
  );



};


