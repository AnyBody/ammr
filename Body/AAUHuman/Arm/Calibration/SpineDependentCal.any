AnyFolder SpineDependentCal ={
  AnyComponentDefinition obj = {};
  AnyFolder &ThoraxSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &CervicalSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &LumbarSegs =  ...BodyModel.Trunk.Segments;
  AnyFolder &LumbarSpineJoints =  ...BodyModel.Trunk.Joints.Lumbar;
  AnyFolder &LumbarCervicalJoints =  ...BodyModel.Trunk.Joints.Cervical;

  AnyFolder &SegSideCal = .SideHumanFolderRef.ShoulderArm.Segments;
  AnyFolder &JointsSideCal = .SideHumanFolderRef.ShoulderArm.Joints;

  AnyFolder &MusCalSide =  .SideHumanFolderRef.ShoulderArm.Muscles;
  AnyFolder &MusParCalSide =  .SideHumanFolderRef.ShoulderArm.MuscleModels;

  AnyFixedRefFrame ground =
  {
    AnyRefNode node =
    {
      ARel=..LumbarSegs.PelvisSeg.Axes0;
      sRel=..LumbarSegs.PelvisSeg.r0;
    };
  };

  AnyKinEq PelvisFix =
  {
    AnyKinLinear Lin =
    {
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....BodyModel.Trunk.Segments.PelvisSeg;
    };
    AnyKinRotational Rot =
    {
      Type = RotAxesAngles;
      AnyRefFrame &ground = ..ground.node;
      AnyRefFrame &Pelvis = .....BodyModel.Trunk.Segments.PelvisSeg;
    };
  };

  #if BM_ARM_SHOULDER_RHYTHM == ON
  #include "ExcludeShoulderRhythm.any"
  #endif

  // Posture data
  AnyFloat NeckFlexion          = { 45,  0,         0} * pi/180;
  AnyFloat NeckLateralFlexRight = {  0,  0, .Sign* 45} * pi/180;
  AnyFloat NeckExtension        = {-45,  0,         0} * pi/180;
  AnyFloat NeckLateralFlexLeft  = {  0,  0, .Sign*-45} * pi/180;

  AnyFloat ThoraxLateralRight    = {  0,.Sign* 30,  0} * pi/180;
  AnyFloat ThoraxLateralLeft     = {  0,.Sign*-30,  0} * pi/180;

  AnyFloat GlenohumeralAbd      = {140,  0,  0, -40,  30} * pi/180;
  AnyFloat GlenohumeralAdd      = {-10,-20,  0,  10,   0} * pi/180;

  AnyFloat NeckPosData = repmat(1,2,{
      NeckFlexion,
      NeckLateralFlexRight,
      NeckExtension,
      NeckLateralFlexLeft
  }'); // columns repeated twice

  AnyFloat ThoraxPosData = arrcat(
      repmat(1,4,ThoraxLateralRight)',
      repmat(1,4,ThoraxLateralLeft)'
  )'; // Four right lat positions followed by 4 left

  AnyFloat GlenohumeralPosData = arrcat(
      repmat(1,4,GlenohumeralAdd)',
      repmat(1,4,GlenohumeralAbd)'
  )'; // Four right Add positions followed by 4 Abd



  AnyKinEqInterPolDriver NeckJntDriver = {
    AnyKinMeasure& fe = ....BodyModel.Interface.Trunk.SkullThoraxFlexion;
    AnyKinMeasure& ar = ....BodyModel.Interface.Trunk.SkullThoraxRotation;
    AnyKinMeasure& lb = ....BodyModel.Interface.Trunk.SkullThoraxLateralBending;
    Type = PiecewiseLinear;
    T    = linspace(0,1,SizesOf(Data)[1]);
    Data = .NeckPosData;
    Reaction.Type = {Off, Off, Off};
  };


  AnyKinEqInterPolDriver ThoraxDriver = {
    AnyKinMeasure& PelvisThoraxExtension = ....BodyModel.Interface.Trunk.PelvisThoraxExtension;
    AnyKinMeasure& PelvisThoraxLateralBending = ....BodyModel.Interface.Trunk.PelvisThoraxLateralBending;
    AnyKinMeasure& PelvisThoraxRotation = ....BodyModel.Interface.Trunk.PelvisThoraxRotation;
    Type = PiecewiseLinear;
    T    = linspace(0,1,SizesOf(Data)[1]);
    Data = .ThoraxPosData;
    Reaction.Type = {On,On,On};
  };

  AnyKinEqInterPolDriver WristFlexionDriver ={
      AnyKinMeasure& WF = ..SideInterfaceFolderRef.WristFlexion;
      Type = PiecewiseLinear;
      T =      {0.0, 1.0};
      Data = {{0,0}}*pi/180;
      Reaction.Type={On};
    };

    AnyKinEqInterPolDriver WristAbductionDriver ={
      AnyKinMeasure& WA = ..SideInterfaceFolderRef.WristAbduction;
      Type = PiecewiseLinear;
      T =      {0.0, 1.0};
      Data = {{0,0}}*pi/180;
      Reaction.Type={On};
    };

  AnyKinEqInterPolDriver FEDriver ={
    AnyKinMeasure& FE = ..SideInterfaceFolderRef.ElbowFlexion;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0};
    Data = {{0,0}}*pi/180;
    Reaction.Type = {Off};
  };

  AnyKinEqInterPolDriver PSDriver ={
    AnyKinMeasure& PS = ..SideInterfaceFolderRef.ElbowPronation;
    Type = PiecewiseLinear;
    T =      {0.0, 1.0};
    Data = {{0,0}}*pi/180;
    Reaction.Type = {Off};
  };

  AnyKinEqInterPolDriver GHDriver ={
    AnyKinMeasure& GHAbduction = ..SideInterfaceFolderRef.GlenohumeralAbduction;
    AnyKinMeasure& GHFlexion = ..SideInterfaceFolderRef.GlenohumeralFlexion;
    AnyKinMeasure& GHExtRot = ..SideInterfaceFolderRef.GlenohumeralExternalRotation;
    AnyKinMeasure& SCProtraction = ..SideInterfaceFolderRef.SternoClavicularProtraction;
    AnyKinMeasure& SCElevation = ..SideInterfaceFolderRef.SternoClavicularElevation;
    Type = PiecewiseLinear;
    T = linspace(0,1,SizesOf(Data)[1]);
    Data = .GlenohumeralPosData;
    Reaction.Type = repmat(nDim,{Off});
  };

  //---------------------------------------------------------------------
  //Finger drivers
  #if BM_ARM_DETAILED_HAND
  //finger1
  AnyKinEqSimpleDriver CMC1Flexion={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.CMCFlexion;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };

  AnyKinEqSimpleDriver CMC1Abduction={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.CMCAbduction;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };



  AnyKinEqSimpleDriver MCP1Flexion=
  {
    AnyRevoluteJoint &ref=..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.MCPFlexion;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver MCP1Abduction=
  {
    AnyRevoluteJoint &ref=..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.MCPAbduction;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };

  AnyKinEqSimpleDriver DIP1={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger1.Jnt.DIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };


 // finger2
//    AnyKinEqSimpleDriver CMC2={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP2={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP2={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP2={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger2.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };



 // finger3
//    AnyKinEqSimpleDriver CMC3={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP3={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP3={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP3={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger3.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };


  //finger4
//    AnyKinEqSimpleDriver CMC4={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.CMC;
//      DriverPos={0,0}*pi/180;
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP4={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP4={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP4={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger4.Jnt.DIP;
      DriverPos={0}*pi/180;
      DriverVel={0};
    };


 // finger5
//    AnyKinEqSimpleDriver CMC5={
//      AnyUniversalJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.CMC;
//      DriverPos={0,0};
//      DriverVel={0,0};
//    };
  AnyKinEqSimpleDriver MCP5={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.MCP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
  AnyKinEqSimpleDriver PIP5={
    AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.PIP;
    DriverPos={0}*pi/180;
    DriverVel={0};
  };
    AnyKinEqSimpleDriver DIP5={
      AnyRevoluteJoint &ref=  ..SideHumanFolderRef.ShoulderArm.Segments.Hand.Finger5.Jnt.DIP;
      DriverPos={0};
      DriverVel={0};
    };
  #endif

  AnyString MusPrefix = CompleteNameOf(&.SideHumanFolderRef.ShoulderArm.Muscles);

  AnyObjectPtrArray LevatorScapulaeMuscles = ObjSearch(MusPrefix+".*.LevatorScapulae*", "AnyMuscle");
  AnyFloat  LevatorScapulaeRmin = repmat(NumElemOf(LevatorScapulaeMuscles), ..RMin);
  AnyFloat  LevatorScapulaeRmax = repmat(NumElemOf(LevatorScapulaeMuscles), ..RMax);

  AnyObjectPtrArray TrapeziusClavicularMuscles = ObjSearch(MusPrefix+".*.TrapeziusClavicular*", "AnyMuscle");
  AnyFloat  TrapeziusClavicularRmin = repmat(NumElemOf(TrapeziusClavicularMuscles), ..RMin);
  AnyFloat  TrapeziusClavicularRmax = repmat(NumElemOf(TrapeziusClavicularMuscles), ..RMax);

  AnyObjectPtrArray SternocleidomastoidMuscles = ObjSearch(MusPrefix+".*.Sternocleidomastoid*", "AnyMuscle");
  AnyFloat  SternocleidomastoidRmin = repmat(NumElemOf(SternocleidomastoidMuscles), ..RMin);
  AnyFloat  SternocleidomastoidRmax = repmat(NumElemOf(SternocleidomastoidMuscles), ..RMax);

  AnyObjectPtrArray LatissimusDorsiMuscles = ObjSearch(MusPrefix+".*.LatissimusDorsi*", "AnyMuscle");
  AnyFloat  LatissimusDorsiRmin = repmat(NumElemOf(LatissimusDorsiMuscles), ..RMin);
  AnyFloat  LatissimusDorsiRmax = repmat(NumElemOf(LatissimusDorsiMuscles), ..RMax);

};

// The study: Operations to be performed on the model
AnyBodyCalibrationStudy ArmCalibrationStudySpineDependent = {
  AnyFolder &ref=.SpineDependentCal;
  nStep = 8;

  Kinematics.SmallStepAssumptionOnOff = On;
  Kinematics.PosAnalysisOnlyOnOff = On;
  InitialConditions.PosAnalysisOnlyOnOff = On;

  MuscleArr = arrcat(
      ref.LevatorScapulaeMuscles,
      ref.TrapeziusClavicularMuscles,
      ref.SternocleidomastoidMuscles,
      ref.LatissimusDorsiMuscles
  );
  RminMuscleFiber = arrcat(
      ref.LevatorScapulaeRmin,
      ref.TrapeziusClavicularRmin,
      ref.SternocleidomastoidRmin,
      ref.LatissimusDorsiRmin
  );
  RmaxMuscleFiber = arrcat(
      ref.LevatorScapulaeRmax,
      ref.TrapeziusClavicularRmax,
      ref.SternocleidomastoidRmax,
      ref.LatissimusDorsiRmax
  );


};


