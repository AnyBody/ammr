//This file is used for making a interface to the dof of the arm
//the added rotnodes are needed to be able to have the same rotation definitions for
//the right and left side



AnyKinMeasureOrg SternoClavicularProtraction = {

  AnyRefNode &sternum = ....Trunk.Segments.SternumSeg._SIDE_;
  sternum = {
    scj = {
      AnyRefNode RotNode={ARel={{0, 0, ..Sign*-1}, {0, ..Sign*1, 0}, {1, 0, 0}};};
    };
  };
  AnyFolder &clavicula_scj=..Segments.Clavicula.scj ;
  clavicula_scj={
    AnyRefNode RotNode={ ARel={{....Sign*1,0,0},{0,1,0},{0,0,....Sign*1}};};
  };

  AnyKinRotational SCRot = {
    AnyRefNode &thorax_sc = .sternum.scj.RotNode;
    AnyRefNode &clavicula_sc = .clavicula_scj.RotNode;
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = x;
  };
  MeasureOrganizer ={0};
};

AnyKinMeasureOrg SternoClavicularElevation ={
  AnyKinRotational &ref=.SternoClavicularProtraction.SCRot;
  MeasureOrganizer ={1};
};

AnyKinMeasureOrg SternoClavicularAxialRotation ={
  AnyKinRotational &ref=.SternoClavicularProtraction.SCRot;
  MeasureOrganizer ={2};
};


//This measure will be zero when SC protraction and SternoClavicularProtraction is -23 and SternoClavicularElevationVec is 11.5
AnyKinMeasureOrg SternoClavicularProtraction_Postural = {
  AnyRefNode &sternum = ....Trunk.Segments.SternumSeg._SIDE_;
  sternum = {
    //introduce a node used for rotational measure, no scaling needed copies sRel and ARel 
    AnyRefNode PosturalFrame = {
       sRel=.scj.sRel;
       ARel=......Trunk.Segments.SternumSeg.ScalingNode.ARel; 
     };
   };
  AnyKinRotational SCRot = {
    AnyRefNode &sternum= .....Trunk.Segments.SternumSeg._SIDE_.PosturalFrame;
    AnyRefNode &clavicula = ...Segments.Clavicula.PosturalFrame;
    Type = RotAxesAngles;
    Axis1 = x;
    Axis2 = y;
    Axis3 = z;
  };
  MeasureOrganizer ={0};
};
AnyKinMeasureOrg SternoClavicularElevation_Postural ={
  AnyKinRotational &ref=.SternoClavicularProtraction_Postural.SCRot;
  MeasureOrganizer ={1};
};
AnyKinMeasureOrg SternoClavicularAxialRotation_Postural ={
  AnyKinRotational &ref=.SternoClavicularProtraction_Postural.SCRot;
  MeasureOrganizer ={2};
};





AnyFolder SternoClavicular = {
  ///^ Follows the ISB recommendations for definitions of joint coordinate systems of the Sterno Clavicula Joint
  ///^ See section 2.4.2 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureLinComb Protraction ={

    AnyRefNode &sternum_scj = .....Trunk.Segments.SternumSeg._SIDE_.scj;
    sternum_scj={
      AnyRefNode ISB_Coord={};
    };

    AnyKinRotational SCRot = {
      AnyRefNode &thorax_sc = .sternum_scj.ISB_Coord;
      AnyRefNode &clavicula_sc = ....Segments.Clavicula.AnatomicalFrame.ISB_Coord;
      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = z;
    };
    OutDim = 1;
    Coef = {{...Sign*1,0,0}};
  };

  AnyKinMeasureLinComb Elevation ={
    AnyKinRotational &ref=.Protraction.SCRot;
    OutDim = 1;
    Coef = {{0,...Sign*-1,0}};
  };

  AnyKinMeasureLinComb BackwardAxialRotation ={
    AnyKinRotational &ref=.Protraction.SCRot;
    OutDim = 1;
    Coef = {{0,0,1}};
  };

};



AnyFolder AcromioClavicular = {
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the Acromio-clavicula joint
  //^ See section 2.4.3 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureLinComb Protraction ={

    AnyFolder& Calvicula = ...Segments.Clavicula;
    Calvicula.AnatomicalFrame = {
      AnyRefNode ISB_Coord ={     };
    };

    AnyFolder& Scapula = ...Segments.Scapula;
    Scapula.AnatomicalFrame = {
      AnyRefNode ISB_Coord = {      };
    };
    Calvicula.acj = {
      AnyRefNode RotNode={ ARel={{......Sign*1,0,0},  {0,1,0},      {0,0,......Sign*1}};};
    };
    Scapula.acj = {
      AnyRefNode RotNode={ ARel={{......Sign*1,0,0},  {0,1,0},      {0,0,......Sign*1}};};
    };

    AnyKinRotational ACRot = {
      AnyRefNode &clavicula = .Calvicula.AnatomicalFrame.ISB_Coord;
      AnyRefNode &scapula = .Scapula.AnatomicalFrame.ISB_Coord;
      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = z;
    };
    OutDim = 1;
    Coef = {{...Sign*1,0,0}};
  };

  AnyKinMeasureLinComb MedialRotation ={
    AnyKinRotational &ref=.Protraction.ACRot;
    OutDim = 1;
    Coef = {{0,...Sign*1,0}};
  };

  AnyKinMeasureLinComb PosteriorTilt ={
    AnyKinRotational &ref=.Protraction.ACRot;
    OutDim = 1;
    Coef = {{0,0,1}};
  };
};

AnyFolder ScapulaHumerus =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the Acromio-clavicula joint
  //^ See section 2.4.4 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureLinComb PlaneOfElevation ={

    AnyFolder& Humerus = ...Segments.Humerus.AnatomicalFrame;
    Humerus = {
      AnyRefNode ISB_Coord = {
      //  ARel={{ 0, 0, 1}, { 0, 1,0}, {-1, 0,0}};
      };
    };
    AnyKinRotational GHRot = {
      AnyRefNode &scapula = ....Segments.Scapula.AnatomicalFrame.ISB_Coord;
      AnyRefNode &Humerus = .Humerus.ISB_Coord;
      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = y;
    };
    OutDim = 1;
    Coef = {{...Sign*1,0,0}};
  };

  AnyKinMeasureLinComb Elevation ={
    AnyKinRotational &ref=.PlaneOfElevation.GHRot;
    OutDim = 1;
    Coef = {{0,...Sign*-1,0}};
  };

  AnyKinMeasureLinComb InternalAxialRotation ={
    AnyKinRotational &ref=.PlaneOfElevation.GHRot;
    OutDim = 1;
    Coef = {{0,0,...Sign*1}};
  };
};

AnyFolder ThoraxScapula =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the scapula relative to thorax
  //^ See section 2.4.6 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureLinComb Protraction ={

    AnyKinRotational ACRot = {
      AnyRefNode &thorax  = ......Trunk.Segments.SternumSeg._SIDE_.scj.ISB_Coord;

      AnyRefNode &scapula = ....Segments.Scapula.AnatomicalFrame.ISB_Coord;
      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = z;
    };
    OutDim = 1;
    Coef = {{...Sign*1,0,0}};
  };

  AnyKinMeasureLinComb MedialRotation ={
    AnyKinRotational &ref=.Protraction.ACRot;
    OutDim = 1;
    Coef = {{0,...Sign*1,0}};
  };

  AnyKinMeasureLinComb PosteriorTilt ={
    AnyKinRotational &ref=.Protraction.ACRot;
    OutDim = 1;
    Coef = {{0,0,1}};
  };
};


AnyFolder ThoraxHumerus =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the Humerus relative to Thorax
  //^ See section 2.4.7 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureOrg PlaneOfElevation ={
    AnyRefNode &ThoraxShoulderISB  = .....Trunk.Segments.SternumSeg._SIDE_.scj.ISB_Coord;

    AnyRefNode& HumerusISB = ...Segments.Humerus.AnatomicalFrame.ISB_Coord;

    AnyFolder& gh=...Segments.Humerus.gh ;

    ThoraxShoulderISB = {
      AnyRefNode RotNodeElevPlane={ARel={{.....Sign*-1,0,0},{0,.....Sign*1,0},{0,0,-1}};};
    };
    HumerusISB = {
      AnyRefNode RotNodeElevPlane={ARel={{.....Sign*-1,0,0},{0,.....Sign*1,0},{0,0,-1}}; };
    };

    AnyKinRotational GHRot = {
      
      #if (ANYBODY_V1 > 8) | (ANYBODY_V1 == 8 & ANYBODY_V2 > 2) | (ANYBODY_V1 == 8 & ANYBODY_V2 == 2 & ANYBODY_V4 >= 13305 )
      Pos0 = {0,1,0}; //we would like to have a solution with positive elevation this starting guess makes this more "likely"
      #endif
      
      AnyRefNode& ref1  = .ThoraxShoulderISB.RotNodeElevPlane;
      AnyRefNode& ref2  = .HumerusISB.RotNodeElevPlane;

      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = y;
    };
    MeasureOrganizer = {0};
  };

  AnyKinMeasureOrg Elevation ={
    AnyKinRotational& ref=.PlaneOfElevation.GHRot;
    MeasureOrganizer = {1};
  };

  AnyKinMeasureOrg InternalAxialRotation ={
    AnyKinRotational& ref=.PlaneOfElevation.GHRot;
    MeasureOrganizer ={2};
  };

};


///This is a "processed" version of the ThoraxHumerus measure
///If Elevation is negative it will flip all signs on the ThoraxHumerus measure
///This can be needed since a rotational measure can return multiple solutions for same position, and if used e.g. in rhythms this cause issues
AnyFolder ThoraxHumerus_Positive_Elevation ={
  
  AnyFunInterpol SignFunction ={
    T ??= farr(-400.0,0.5,1600)*pi/180;
    Data ??=  {iffun(gteqfun(T,0),1.0,-1.0)};
    Type = Bspline;
  };
  
  AnyKinMeasureFunComb1 ElevationSign =   {
    AnyKinMeasure& HumerusElevationPlane = ..ThoraxHumerus.Elevation;
    Functions = {&.SignFunction};
  };
  
  AnyKinMeasureQuadComb FlipSigns = {
    AnyKinMeasureOrg &ref1 = ..ThoraxHumerus.PlaneOfElevation;
    AnyKinMeasureOrg &ref2 = ..ThoraxHumerus.Elevation;
    AnyKinMeasureOrg &ref3 = ..ThoraxHumerus.InternalAxialRotation;
    AnyKinMeasureFunComb1 &ref4 =   .ElevationSign;
    
    OutDim = 3;
    CoefQuadTensor = {
      {
        {0,0,0,1},
        {0,0,0,0},
        {0,0,0,0},
        {0,0,0,0} 
      },
      {
        {0,0,0,0},
        {0,0,0,1},
        {0,0,0,0},
        {0,0,0,0} 
      },
      {
        {0,0,0,0},
        {0,0,0,0},
        {0,0,0,1},
        {0,0,0,0} 
      }
    };      
  };
  AnyKinMeasureOrg PlaneOfElevation ={       AnyKinMeasureQuadComb& ref=.FlipSigns;    MeasureOrganizer = {0};  };
  AnyKinMeasureOrg Elevation ={              AnyKinMeasureQuadComb& ref=.FlipSigns;    MeasureOrganizer = {1};  };
  AnyKinMeasureOrg InternalAxialRotation ={  AnyKinMeasureQuadComb& ref=.FlipSigns;    MeasureOrganizer = {2};  };
  
  
};



AnyKinMeasureOrg ScapulaThoraxProtraction =
{
  AnyRefFrame &Scapula = ..Segments.Scapula;
  Scapula = {
    AnyRefNode RotNode={ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};};
  };
  MeasureOrganizer = {0};
  AnyKinRotational Rot =
  {
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = x;
    AnyRefFrame &Thorax = ...ShoulderRef;
    AnyRefFrame &Scapula = ...Segments.Scapula.RotNode;
  };
};

AnyKinMeasureOrg ScapulaThoraxElevation =
{
  MeasureOrganizer = {1};
  AnyKinRotational &Rot = .ScapulaThoraxProtraction.Rot;
};




AnyKinMeasureOrg GlenohumeralFlexion ={
  AnyRefNode &ShoulderNodeRef  = ..ShoulderRef;
  ShoulderNodeRef={
    AnyRefNode RotNode={ARel={{0,0,1},{0,....Sign*-1,0},{....Sign*1,0,0}};
    };
  };
  AnyFolder &gh=..Segments.Humerus.gh ;
  gh ={
    AnyRefNode RotNode={
      ARel={{0,0,1},{0,....Sign*-1,0},{....Sign*1,0,0}};
    };
  };
  AnyKinRotational GHMeasure ={
    AnyRefNode &ref1  = ...ShoulderRef.RotNode;
    AnyRefNode &ref2  = ...Segments.Humerus.gh.RotNode;

    Type=RotAxesAngles;
    Axis1=x;
    Axis2=z;
    Axis3=y;

  };
  MeasureOrganizer ={1};


};

AnyKinMeasureOrg GlenohumeralExternalRotation ={
  AnyKinRotational &ref=.GlenohumeralFlexion.GHMeasure;
  MeasureOrganizer ={2};
};
AnyKinMeasureOrg GlenohumeralAbduction ={
  AnyKinRotational &ref=.GlenohumeralFlexion.GHMeasure;
  MeasureOrganizer ={0};
};




AnyKinMeasureOrg GlenohumeralFlexionJntMus ={
  AnyRefNode &Scapula  = ..Segments.Scapula.gh ;
  Scapula={
    AnyRefNode RotNode={
      ARel={{0,0,....Sign*1},{0,-1,0},{....Sign*1,0,0}};
    };
  };
  AnyKinRotational GHMeasure ={
    AnyRefNode &ref1  = ...Segments.Scapula.gh.RotNode;
    AnyRefNode &ref2  = ...Segments.Humerus.gh.RotNode;

    AngVelOnOff = On;
    Type=RotAxesAngles;
    //    Axis1=x;
    //    Axis2=z;
    //    Axis3=y;
    // The measure is now a xyz vector

  };
  MeasureOrganizer ={2};
};

AnyKinMeasureOrg GlenohumeralExternalRotationJntMus ={
  AnyKinRotational &ref=.GlenohumeralFlexionJntMus.GHMeasure;
  MeasureOrganizer ={1};
};
AnyKinMeasureOrg GlenohumeralAbductionJntMus ={
  AnyKinRotational &ref=.GlenohumeralFlexionJntMus.GHMeasure;
  MeasureOrganizer ={0};
};







AnyKinMeasureOrg ElbowFlexion= {
  AnyFolder &humerus=..Segments.Humerus.HumeroUlnar ;
  humerus ={
    AnyRefNode RotNode={
      ARel=RotMat(pi,y);
    };
  };
  AnyFolder &ulna=..Segments.Ulna.HumeroUlnar ;
  ulna ={
    AnyRefNode RotNode={
      ARel=RotMat(pi,y);
    };
  };
  AnyKinRotational  Rot= {
    AnyRefNode &HumerusFE = ...Segments.Humerus.HumeroUlnar.RotNode;
    AnyRefNode &UlnaFE = ...Segments.Ulna.HumeroUlnar.RotNode;

    Type=RotAxesAngles;
  };
  MeasureOrganizer={2}; //this cooresponds to x  (remember z y x is default)

};


AnyKinMeasureOrg ElbowPronation={


  AnyKinRotational PSRotProximal = {
    Type = RotAxesAngles;

    AnyRefNode &Ulna_Radius_RadioUlnar = ...Segments.Ulna.ProximalRadioUlnar;
    AnyRefNode &Radius_RadioUlnar = ...Segments.Radius.ProximalRadioUlnar;

  };//End pronation supination joint proximal
  MeasureOrganizer = {2}; //this cooresponds to x  (remember z y x is default)
};

#ifndef WRIST_EXPERIMENTAL
////Definition of wrist joint
AnyKinMeasureOrg WristFlexion ={
  AnyFolder &radius=..Segments.Radius.RadioCarpal;
  radius ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,-1,0},{0,0,....Sign*-1}};
    };
  };
  AnyFolder &WristJointSeg= ..Segments.WristJointSeg.RadioCarpalFlexion;
  WristJointSeg ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,-1,0},{0,0,....Sign*-1}};
    };
  };
  AnyKinRotational WristJoint = {
    Type=RotAxesAngles;
    Axis1=x;
    Axis2=y;
    Axis3=z;
    AnyRefNode &radius_wj = ...Segments.Radius.RadioCarpal.RotNode;
    AnyRefNode &hand_wj = ...Segments.WristJointSeg.RadioCarpalFlexion.RotNode;
  };//End WristJoint
  MeasureOrganizer ={0};
};

AnyKinMeasureOrg &WristAbduction=.Joints.RadioCarpalJointDeviation;
#endif


#ifdef WRIST_EXPERIMENTAL
////Definition of wrist joint
AnyKinMeasureOrg WristFlexion ={
  AnyFolder &radius=..Segments.Radius.RadioCarpal;
  radius ={
    AnyRefNode RotNode={
      ARel=RotMat(0.5*(1-....Sign)*pi,y)*RotMat(0.5*(1+....Sign)*pi,x);   //Right side pi Left side 0
    };
  };
  AnyFolder &Hand= ..Segments.Hand.HandRef.RadioCarpalFlexion;
  Hand ={
    AnyRefNode RotNode={
      ARel=RotMat(0.5*(1-....Sign)*pi,y)*RotMat(0.5*(1+....Sign)*pi,x);   //Right side pi Left side 2pi  0
    };
  };
  AnyKinRotational WristJoint = {
    Type=RotAxesAngles;
    Axis1=x;
    Axis2=y;
    Axis3=z;
    AnyRefNode &radius = ...Segments.Radius.RadioCarpal.RotNode;
    AnyRefNode &hand = ...Segments.Hand.HandRef.RadioCarpalFlexion.RotNode;
  };//End WristJoint
  MeasureOrganizer ={0};
};

AnyKinMeasureOrg WristAbduction ={
  AnyFolder &radius=..Segments.Radius.RadioCarpal;
  AnyKinRotational WristJoint = {
    Type=RotAxesAngles;
    Axis1=z;
    Axis2=y;
    Axis3=x;
    AnyRefNode &radius = ...Segments.Radius.RadioCarpal.RotNode;
    AnyRefNode &hand = ...Segments.Hand.HandRef.RadioCarpalFlexion.RotNode;
  };//End WristJoint
  MeasureOrganizer ={0};
};



#endif



/// RotVec based measures of joint angles. These measures are used for
/// exporting and importing joint angles. They do not make sense from
/// clinical point of view.
RotVectorMeasures =
{
  AnyKinRotational ThoraxClavicula =
  {
    Type = RotVector;
    AnyRefNode &ref1  = .....Trunk.Segments.SternumSeg._SIDE_.scj.RotNode;
    AnyRefFrame &ref2 = ...Segments.Clavicula.scj.RotNode;
  };
  AnyKinRotational ClaviculaScapula =
  {
    Type = RotVector;
    AnyRefFrame &ref1 = ...Segments.Clavicula.acj.RotNode;
    AnyRefFrame &ref2 = ...Segments.Scapula.acj.RotNode;
  };
  AnyKinRotational ScapulaHumerus =
  {
    Type = RotVector;
    AnyRefFrame &ref1 = ...Segments.Scapula.gh.RotNode;
    AnyRefFrame &ref2 = ...Segments.Humerus.gh.RotNode;
  };
  AnyKinMeasureOrg HumerusUlna = {
    AnyKinRotational rot =
    {
      Type = RotVector;
      AnyRefFrame &ref1 = ....Segments.Humerus.HumeroUlnar;
      AnyRefFrame &ref2 = ....Segments.Ulna.HumeroUlnar;
    };
    MeasureOrganizer = {0};
  };
  AnyKinMeasureOrg UlnaRadius = {
    AnyKinRotational rot =
    {
      Type = RotVector;
      AnyRefNode &Ulna_Radius_RadioUlnar = ....Segments.Ulna.ProximalRadioUlnar;
      AnyRefNode &Radius_RadioUlnar = ....Segments.Radius.ProximalRadioUlnar;


    };
    MeasureOrganizer = {0};
  };

  #ifndef WRIST_EXPERIMENTAL

  AnyKinMeasureOrg RadiusWrist = {
    AnyKinRotational rot =
    {
      Type = RotVector;
      AnyRefNode &radius = ....Segments.Radius.RadioCarpal;
      AnyRefNode &hand =....Segments.WristJointSeg.RadioCarpalFlexion;


    };
    MeasureOrganizer = {0};
  };


  AnyKinMeasureOrg WristHand = {
    AnyKinRotational rot =      {
      Type = RotVector;
      AnyRefFrame &ref1 = ....Segments.WristJointSeg.RadioCarpalDeviation;
      AnyRefFrame &ref2 = ....Segments.Hand.HandRef.RadioCarpalDeviation;
    };
    MeasureOrganizer = {2};
  };
  #endif




  #ifdef WRIST_EXPERIMENTAL
  AnyKinMeasureOrg RadiusWrist = {
    AnyKinRotational rot =       {
      Type = RotVector;
      AnyRefNode &radius = ....Segments.Radius.RadioCarpal;
      AnyRefNode &hand =....Segments.Hand.HandRef.RadioCarpalFlexion;
    };
    MeasureOrganizer = {0};
  };

  AnyKinMeasureOrg WristHand = {
    AnyKinRotational rot =  {
      Type = RotVector;
      AnyRefNode &radius = ....Segments.Radius.RadioCarpal;
      AnyRefNode &hand =....Segments.Hand.HandRef.RadioCarpalFlexion;
    };
    MeasureOrganizer = {2};
  };
  #endif


};


#if !((BM_ARM_RIGHT & BM_ARM_DETAILED_HAND) | (BM_ARM_LEFT & BM_ARM_DETAILED_HAND))
AnyRefNode &GloveContactNode =.Segments.Glove.ContactNode;
AnyRefNode  &WristContactNode=.Segments.Glove.WristContactNode;
AnyRefNode  &PalmLateralContactNode=.Segments.Glove.PalmLateralContactNode;
AnyRefNode  &PalmMedialContactNode=.Segments.Glove.PalmMedialContactNode;
#endif
