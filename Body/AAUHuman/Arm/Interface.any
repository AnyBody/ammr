//This file is used for making a interface to the dof of the arm
//the added rotnodes are needed to be able to have the same rotation definitions for
//the right and left side

AnyKinMeasureOrg SternoClavicularProtraction = {

  AnyRefNode &sternum = ....Trunk.Segments.SternumSeg._SIDE_;
  sternum = {
    scj = {
      AnyRefNode RotNode={ARel={{0, 0, ..Sign*-1}, {0, ..Sign*1, 0}, {1, 0, 0}};};
    };
  };
  AnyFolder &clavicula_scj=..Segments.Clavicula.scj ;
  clavicula_scj={
    AnyRefNode RotNode={ ARel={{....Sign*1,0,0},{0,1,0},{0,0,....Sign*1}};};
  };

  AnyKinRotational SCRot = {
    AnyRefNode &thorax_sc = .sternum.scj.RotNode;
    AnyRefNode &clavicula_sc = .clavicula_scj.RotNode;
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = x;
  };
  MeasureOrganizer ={0};
};

AnyKinMeasureOrg SternoClavicularElevation ={
  AnyKinRotational &ref=.SternoClavicularProtraction.SCRot;
  MeasureOrganizer ={1};
};

AnyKinMeasureOrg SternoClavicularAxialRotation ={
  AnyKinRotational &ref=.SternoClavicularProtraction.SCRot;
  MeasureOrganizer ={2};
};


AnyFolder SternoClavicular = {
  ///^ Follows the ISB recommendations for definitions of joint coordinate systems of the Sterno Clavicula Joint
  ///^ See section 2.4.2 in https://doi.org/10.1016/j.jbiomech.2004.05.042

AnyKinMeasureLinComb Protraction ={

  AnyRefNode &sternum_scj = .....Trunk.Segments.SternumSeg._SIDE_.scj;
     sternum_scj={
       AnyRefNode ISB_Coord={};
     };

    AnyKinRotational SCRot = {
      AnyRefNode &thorax_sc = .sternum_scj.ISB_Coord;
      AnyRefNode &clavicula_sc = ....Segments.Clavicula.AnatomicalFrame.ISB_Coord;
      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = z;
    };
    OutDim = 1;
    Coef = {{...Sign*1,0,0}};
  };

  AnyKinMeasureLinComb Elevation ={
    AnyKinRotational &ref=.Protraction.SCRot;
       OutDim = 1;
       Coef = {{0,...Sign*-1,0}};
  };

  AnyKinMeasureLinComb BackwardAxialRotation ={
    AnyKinRotational &ref=.Protraction.SCRot;
       OutDim = 1;
       Coef = {{0,0,1}};
  };

};



AnyFolder AcromioClavicular =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the Acromio-clavicula joint
  //^ See section 2.4.3 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureLinComb Protraction ={

     AnyFolder& Calvicula = ...Segments.Clavicula;
     Calvicula.AnatomicalFrame = {
       AnyRefNode ISB_Coord ={ ARel={{0, 0,......Sign*1}, {0, ......Sign*1,0}, {-1, 0,0}};};
     };

     AnyFolder& Scapula = ...Segments.Scapula;
     Scapula.AnatomicalFrame = {
       AnyRefNode ISB_Coord = {ARel={{ 0, 0,......Sign*1}, { 0, ......Sign*1,0}, {-1, 0,0}}; };
     };
     Calvicula.acj = {
          AnyRefNode RotNode={ ARel={{......Sign*1,0,0},  {0,1,0},      {0,0,......Sign*1}};};
       };
     Scapula.acj = {
          AnyRefNode RotNode={ ARel={{......Sign*1,0,0},  {0,1,0},      {0,0,......Sign*1}};};
       };

     AnyKinRotational ACRot = {
       AnyRefNode &clavicula = .Calvicula.AnatomicalFrame.ISB_Coord;
       AnyRefNode &scapula = .Scapula.AnatomicalFrame.ISB_Coord;
       Type = RotAxesAngles;
       Axis1 = y;
       Axis2 = x;
       Axis3 = z;
     };
     OutDim = 1;
     Coef = {{...Sign*1,0,0}};
   };

   AnyKinMeasureLinComb MedialRotation ={
     AnyKinRotational &ref=.Protraction.ACRot;
     OutDim = 1;
     Coef = {{0,...Sign*1,0}};
   };

   AnyKinMeasureLinComb PosteriorTilt ={
     AnyKinRotational &ref=.Protraction.ACRot;
     OutDim = 1;
     Coef = {{0,0,1}};
   };
};

AnyFolder ScapulaHumerus =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the Acromio-clavicula joint
  //^ See section 2.4.4 in https://doi.org/10.1016/j.jbiomech.2004.05.042

   AnyKinMeasureLinComb PlaneOfElevation ={

     AnyFolder& Humerus = ...Segments.Humerus.AnatomicalFrame;
     Humerus = {
       AnyRefNode ISB_Coord = {ARel={{ 0, 0, 1}, { 0, 1,0}, {-1, 0,0}}; };
     };
     AnyKinRotational GHRot = {
       AnyRefNode &scapula = ....Segments.Scapula.AnatomicalFrame.ISB_Coord;
       AnyRefNode &Humerus = .Humerus.ISB_Coord;
       Type = RotAxesAngles;
       Axis1 = y;
       Axis2 = x;
       Axis3 = y;
     };
     OutDim = 1;
     Coef = {{...Sign*1,0,0}};
   };

   AnyKinMeasureLinComb Elevation ={
     AnyKinRotational &ref=.PlaneOfElevation.GHRot;
     OutDim = 1;
     Coef = {{0,...Sign*-1,0}};
   };

   AnyKinMeasureLinComb InternalAxialRotation ={
     AnyKinRotational &ref=.PlaneOfElevation.GHRot;
     OutDim = 1;
     Coef = {{0,0,...Sign*1}};
   };
};

AnyFolder ThoraxScapula =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the scapula relative to thorax
  //^ See section 2.4.6 in https://doi.org/10.1016/j.jbiomech.2004.05.042

   AnyKinMeasureLinComb Protraction ={

     AnyKinRotational ACRot = {
       AnyRefNode &thorax  = ......Trunk.Segments.SternumSeg._SIDE_.scj.ISB_Coord;

       AnyRefNode &scapula = ....Segments.Scapula.AnatomicalFrame.ISB_Coord;
       Type = RotAxesAngles;
       Axis1 = y;
       Axis2 = x;
       Axis3 = z;
     };
     OutDim = 1;
     Coef = {{...Sign*1,0,0}};
   };

   AnyKinMeasureLinComb MedialRotation ={
     AnyKinRotational &ref=.Protraction.ACRot;
     OutDim = 1;
     Coef = {{0,...Sign*1,0}};
   };

   AnyKinMeasureLinComb PosteriorTilt ={
     AnyKinRotational &ref=.Protraction.ACRot;
     OutDim = 1;
     Coef = {{0,0,1}};
   };
};


AnyFolder ThoraxHumerus =
{
  //^ Follows the ISB recommendations for definitions of joint coordinate systems of the Humerus relative to Thorax
  //^ See section 2.4.7 in https://doi.org/10.1016/j.jbiomech.2004.05.042

  AnyKinMeasureOrg PlaneOfElevation ={
    AnyRefNode &ThoraxShoulderISB  = .....Trunk.Segments.SternumSeg._SIDE_.scj.ISB_Coord;

    AnyRefNode& HumerusISB = ...Segments.Humerus.AnatomicalFrame.ISB_Coord;

    AnyFolder& gh=...Segments.Humerus.gh ;

    ThoraxShoulderISB = {
      AnyRefNode RotNodeElevPlane={ARel={{.....Sign*-1,0,0},{0,.....Sign*1,0},{0,0,-1}};};
    };
    HumerusISB = {
      AnyRefNode RotNodeElevPlane={ARel={{.....Sign*-1,0,0},{0,.....Sign*1,0},{0,0,-1}}; };
    };

    AnyKinRotational GHRot = {
      AnyRefNode& ref1  = .ThoraxShoulderISB.RotNodeElevPlane;
      AnyRefNode& ref2  = .HumerusISB.RotNodeElevPlane;

      Type = RotAxesAngles;
      Axis1 = y;
      Axis2 = x;
      Axis3 = y;
    };
    MeasureOrganizer = {0};
  };

   AnyKinMeasureOrg Elevation ={
    AnyKinRotational& ref=.PlaneOfElevation.GHRot;
    MeasureOrganizer = {1};
   };

  AnyKinMeasureOrg InternalAxialRotation ={
    AnyKinRotational& ref=.PlaneOfElevation.GHRot;
    MeasureOrganizer ={2};
  };

};

AnyKinMeasureOrg ScapulaThoraxProtraction =
{
  AnyRefFrame &Scapula = ..Segments.Scapula;
  Scapula = {
    AnyRefNode RotNode={ARel={{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};};
  };
  MeasureOrganizer = {0};
  AnyKinRotational Rot =
  {
    Type = RotAxesAngles;
    Axis1 = y;
    Axis2 = z;
    Axis3 = x;
    AnyRefFrame &Thorax = ...ShoulderRef;
    AnyRefFrame &Scapula = ...Segments.Scapula.RotNode;
  };
};

AnyKinMeasureOrg ScapulaThoraxElevation =
{
  MeasureOrganizer = {1};
  AnyKinRotational &Rot = .ScapulaThoraxProtraction.Rot;
};




AnyKinMeasureOrg GlenohumeralFlexion ={
  AnyRefNode &ShoulderNodeRef  = ..ShoulderRef;
  ShoulderNodeRef={
    AnyRefNode RotNode={ARel={{0,0,1},{0,....Sign*-1,0},{....Sign*1,0,0}};
    };
  };
  AnyFolder &gh=..Segments.Humerus.gh ;
  gh ={
    AnyRefNode RotNode={
      ARel=.ARel*{{0,0,1},{0,....Sign*-1,0},{....Sign*1,0,0}};
    };
  };
  AnyKinRotational GHMeasure ={
    AnyRefNode &ref1  = ...ShoulderRef.RotNode;
    AnyRefNode &ref2  = ...Segments.Humerus.gh.RotNode;

    Type=RotAxesAngles;
    Axis1=x;
    Axis2=z;
    Axis3=y;

  };
  MeasureOrganizer ={1};


};

AnyKinMeasureOrg GlenohumeralExternalRotation ={
  AnyKinRotational &ref=.GlenohumeralFlexion.GHMeasure;
  MeasureOrganizer ={2};
};
AnyKinMeasureOrg GlenohumeralAbduction ={
  AnyKinRotational &ref=.GlenohumeralFlexion.GHMeasure;
  MeasureOrganizer ={0};
};




AnyKinMeasureOrg GlenohumeralFlexionJntMus ={
  AnyRefNode &Scapula  = ..Segments.Scapula.gh ;
  Scapula={
    AnyRefNode RotNode={
      ARel={{0,0,....Sign*1},{0,-1,0},{....Sign*1,0,0}};
    };
  };
  AnyKinRotational GHMeasure ={
    AnyRefNode &ref1  = ...Segments.Scapula.gh.RotNode;
    AnyRefNode &ref2  = ...Segments.Humerus.gh.RotNode;

    AngVelOnOff = On;
    Type=RotAxesAngles;
//    Axis1=x;
//    Axis2=z;
//    Axis3=y;
// The measure is now a xyz vector

  };
  MeasureOrganizer ={2};
};

AnyKinMeasureOrg GlenohumeralExternalRotationJntMus ={
  AnyKinRotational &ref=.GlenohumeralFlexionJntMus.GHMeasure;
  MeasureOrganizer ={1};
};
AnyKinMeasureOrg GlenohumeralAbductionJntMus ={
  AnyKinRotational &ref=.GlenohumeralFlexionJntMus.GHMeasure;
  MeasureOrganizer ={0};
};







AnyKinMeasureOrg ElbowFlexion= {
  AnyFolder &humerus=..Segments.Humerus.fe ;
  humerus ={
    AnyRefNode RotNode={
      ARel=.ARel*{{1,0,0},{0,....Sign*-1,0},{0,0,....Sign*-1}};
    };
  };
  AnyFolder &ulna=..Segments.Ulna.fe ;
  ulna ={
    AnyRefNode RotNode={
      ARel=.ARel*{{....Sign*-1,0,0},{0,1,0},{0,0,....Sign*-1}};
    };
  };
  AnyKinRotational  Rot= {
    AnyRefNode &HumerusFE = ...Segments.Humerus.fe.RotNode;
    AnyRefNode &UlnaFE = ...Segments.Ulna.fe.RotNode;
    Type=RotAxesAngles;
  };
  MeasureOrganizer={2}; //this cooresponds to x  (remember z y x is default)

};


AnyKinMeasureOrg ElbowPronation={

  AnyFolder &ulna=..Segments.Ulna.ps2 ;
  ulna ={
    AnyRefNode RotNode={
      ARel={{0,1,0},{-....Sign*1,0,0},{0,0,....Sign*1}};
    };
  };
  AnyFolder &radius=..Segments.Radius.PointPS2;
  radius ={
    AnyRefNode RotNode={
      ARel=RotMat(-....Sign*30*pi/180,x)*{{....Sign*1,0,0},{0,....Sign*1,0},{0,0,1}};
    };
  };

  AnyKinRotational PSRotProximal = {
    Type = RotAxesAngles;
    AnyRefNode &UlnaPs = ...Segments.Ulna.ps2.RotNode;
    AnyRefNode &RadiusPs = ...Segments.Radius.PointPS2.RotNode;

  };//End pronation supination joint proximal
  MeasureOrganizer = {2}; //this cooresponds to x  (remember z y x is default)



};


//Definition of wrist joint
AnyKinMeasureOrg WristFlexion ={
  AnyFolder &radius=..Segments.Radius.wj;
  radius ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,-1,0},{0,0,....Sign*-1}};
    };
  };
  AnyFolder &WristJointSeg= ..Segments.WristJointSeg.FlexionExtensionAxis;
  WristJointSeg ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,-1,0},{0,0,....Sign*-1}};
    };
  };

  AnyKinRotational WristJoint = {
    Type=RotAxesAngles;
    AnyRefNode &radius_wj = ...Segments.Radius.wj.RotNode;
    AnyRefNode &hand_wj = ...Segments.WristJointSeg.FlexionExtensionAxis.RotNode;
  };//End WristJoint
  MeasureOrganizer ={1};
};


AnyKinMeasureOrg WristAbduction ={
  MeasureOrganizer ={0};


  AnyFolder &WristJointSeg=..Segments.WristJointSeg.RadialUlnarDeviation;
  WristJointSeg ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,1,0},{0,0,....Sign*1}};
    };
  };
  AnyFolder &HandSeg= ..Segments.Hand.HandRef.wj;
  HandSeg ={
    AnyRefNode RotNode={
      ARel={{....Sign*1,0,0},{0,1,0},{0,0,....Sign*1}};
    };
  };
  AnyKinRotational WristJoint = {
    Type=RotAxesAngles;
    AnyRefNode &radius_wj = ...Segments.WristJointSeg.RadialUlnarDeviation.RotNode;
    AnyRefNode &hand_wj =...Segments.Hand.HandRef.wj.RotNode;
  };//End WristJoint

};

/// RotVec based measures of joint angles. These measures are used for
/// exporting and importing joint angles. They do not make sense from
/// clinical point of view.
RotVectorMeasures =
{
    AnyKinRotational ThoraxClavicula =
    {
       Type = RotVector;
       AnyRefNode &ref1  = .....Trunk.Segments.SternumSeg._SIDE_.scj;

       AnyRefFrame &ref2 = ...Segments.Clavicula.scj.RotNode;
    };
    AnyKinRotational ClaviculaScapula =
    {
       Type = RotVector;
       AnyRefFrame &ref1 = ...Segments.Clavicula.acj.RotNode;
       AnyRefFrame &ref2 = ...Segments.Scapula.acj.RotNode;
    };
    AnyKinRotational ScapulaHumerus =
    {
       Type = RotVector;
       AnyRefFrame &ref1 = ...Segments.Scapula.gh.RotNode;
       AnyRefFrame &ref2 = ...Segments.Humerus.gh.RotNode;
    };
    AnyKinMeasureOrg HumerusUlna = {
      AnyKinRotational rot =
      {
         Type = RotVector;
         AnyRefFrame &ref1 = ....Segments.Humerus.fe.RotNode;
         AnyRefFrame &ref2 = ....Segments.Ulna.fe.RotNode;
      };
      MeasureOrganizer = {0};
    };
    AnyKinMeasureOrg UlnaRadius = {
      AnyKinRotational rot =
      {
         Type = RotVector;
         AnyRefFrame &ref1 = ....Segments.Ulna.ps2.RotNode;
         AnyRefFrame &ref2 = ....Segments.Radius.PointPS2.RotNode;
      };
      MeasureOrganizer = {0};
    };
    AnyKinMeasureOrg RadiusWrist = {
      AnyKinRotational rot =
      {
         Type = RotVector;
         AnyRefFrame &ref1 = ....Segments.Radius.wj.RotNode;
         AnyRefFrame &ref2 = ....Segments.WristJointSeg.FlexionExtensionAxis.RotNode;
      };
      MeasureOrganizer = {1};
    };
    AnyKinMeasureOrg WristHand = {
      AnyKinRotational rot =
      {
         Type = RotVector;
         AnyRefFrame &ref1 = ....Segments.WristJointSeg.RadialUlnarDeviation.RotNode;
         AnyRefFrame &ref2 = ....Segments.Hand.HandRef.wj.RotNode;
      };
      MeasureOrganizer = {2};
    };


  };


#if !((BM_ARM_RIGHT & BM_ARM_DETAILED_HAND) | (BM_ARM_LEFT & BM_ARM_DETAILED_HAND))
AnyRefNode &GloveContactNode =.Segments.Glove.ContactNode;
AnyRefNode  &WristContactNode=.Segments.Glove.WristContactNode;
AnyRefNode  &PalmLateralContactNode=.Segments.Glove.PalmLateralContactNode;
AnyRefNode  &PalmMedialContactNode=.Segments.Glove.PalmMedialContactNode;
#endif
