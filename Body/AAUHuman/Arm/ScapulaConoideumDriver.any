//This is included from jnt.any when no shoulder rhythm is included.
// This is its own include file, so the calibration studies can reuse
// the drivers.

//Replaced by rhythm.
AnyKinEqSimpleDriver ScapulaTSThoraxNodeDriver =
{
  AnyKinMeasure &Measure1=.ScapulaTSThoraxMeasure ;
  DriverPos = {0};
  DriverVel = {0};
  Reaction.Type = {Off};
};


//Replaced by rhythm
AnyKinEqSimpleDriver ConoideumLigamentDrv ={

  //This the position of conoid node on clavicual unscaled
  AnyVec3  ClaviculaConoidR=..Segments.Clavicula.Data.NeutralPositionOnPelvis_pos+ ..Segments.Clavicula.Data.conoid_pos*..Segments.Clavicula.Data.Clavicula_Axes';

  //This the position of conoid node on clavicual unscaled
  AnyVec3  ScapulaConoidR=..Segments.Scapula.Data.NeutralPositionOnPelvis_pos+ ..Segments.Scapula.Data.conoid_pos*..Segments.Scapula.Data.Scapula_Axes';

  AnyVec3 StandardConoidVector= ClaviculaConoidR-ScapulaConoidR;

  #if BM_ARM_DELTOID_WRAPPING == _DELTOID_WRAPPING_RAKE_
  // This scaling factor measures unscaled and scaled thickness of scapula segment to assess the relative change in conoid ligament
  AnyVar ScaleFactor = vnorm (..Segments.Scapula.O_deltoideus_scapular_part_5.sRel -..Segments.Scapula.O_deltoideus_scapular_part_3.sRel)
        / vnorm(..Segments.Scapula.Data.O_deltoideus_scapular_part_5_pos -..Segments.Scapula.Data.O_deltoideus_scapular_part_3_pos) ;
  #else
  AnyVar ScaleFactor = vnorm (..Segments.Scapula.O_DeltoideusLateral4.sRel -..Segments.Scapula.O_DeltoideusLateral1.sRel)
  / vnorm(..Segments.Scapula.Data.O_DeltoideusLateral4_pos -..Segments.Scapula.Data.O_DeltoideusLateral1_pos) ;
  #endif

  AnyFloat ScaleStdConoidVectorLength = vnorm(StandardConoidVector)*ScaleFactor;

  AnyKinPLine &ref=.ConoideumLigament;
  DriverPos={ScaleStdConoidVectorLength};
  DriverVel={0};
  Reaction.Type={Off};
};
