
AnyFolder Environment = {
  AnySeg ForcePlatform = {
    Mass = 0;
    Jii = {0, 0, 0};
    AnyDrawSurf ForcePlatformDrawSurf ={
      FileName = "Forceplate";
      Face = -1;
      ScaleXYZ={1,1,1};
      RGB = {0.5,0.5,0.5};
      Transparency = 1.0;
    }; // ForcePlatformDrawSurf
  };
  



  AnyKinEqSimpleDriver FixPlatform = {
    AnyKinMeasureOrg org = {
      AnyKinLinear Lin = {
        AnyRefFrame &Plate = ...ForcePlatform;
      };
      AnyKinRotational Rot = {
        AnyRefFrame &Plate = ...ForcePlatform;
        Type = RotAxesAngles;
      };
    };
    DriverPos = {-1.5, -1.5, -1, 0, 0, 0}/100;
    DriverVel = {0,0,0,0,0,0};
    Reaction.Type = {Off,Off,Off,Off,Off,Off};
  };
  
  AnyFunInterpol Forces = {
    Type=Bspline;
    BsplineOrder = 8;
    FileName = "forcevalues.txt"; //reference to the name of the data file to use
  };
  
  AnyForce F = {
    AnyKinMeasureOrg org = {
      AnyKinLinear Lin = {
        AnyRefFrame &Plate = ...ForcePlatform;
      };
      AnyKinRotational Rot = {
        AnyRefFrame &Plate = ...ForcePlatform;
        Type = RotAxesAngles;
      };
    };
    F = .Forces(t);
  };

  AnyReacForce CowPlate = {
    AnyKinMeasureOrg org = {
      AnyKinLinear Lin = {
        AnyRefFrame &Plate = ...ForcePlatform;
        AnyRefFrame &Hoof = Main.Forelimb.Segs.PhalanxDist;

      };
      AnyKinRotational Rot = {
        AnyRefFrame &Plate = ...ForcePlatform;
        AnyRefFrame &Hoof = Main.Forelimb.Segs.PhalanxDist;
        Type = RotAxesAngles;
      };
    };
  };
};