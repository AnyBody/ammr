AnyFolder SubjectSpecificScaling = {
  
  //transformation from CT bone into scaled segmental ref. frame
  AnyFunTransform3DIdentity RegTransform = {
    PreTransforms = {&.CT2Seg, &..Scale};
  };
  
  //transformation from unscaled anatomical frame  to segmental ref. frame 
  AnyFunTransform3D &TScale2SegFrame = .Scale.T0GT0Inv;
  
  //transformation from flexed bone position to scaled segmental ref. frame
  AnyFunTransform3DIdentity Flex2scaledSeg = {
    PreTransforms = {&.Flex2Seg, &..Scale};
  };  
  
  
  //transformation from the scaled segmental ref. frame to CT ref. frame
  AnyFunTransform3DLin2 Seg2CT = {
    AnyString SourceFN = ANYBODY_PATH_BODY+"\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Seg_" + ..Bone + "_R.stl";
    AnyString TargetFN = ANYBODY_PATH_BODY+"\LegTLEM\TLEM2.1\CT_RefFrame_STLs\CT_" + ..Bone + "_R.stl";
    AnyFileVar SourceSTL = SourceFN;
    AnyFileVar TargetSTL = TargetFN;
    AnyMatrix Points0unscaled = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*...AMirroring;
    Points0 = .TScale2SegFrame(Points0unscaled);
    Points1 = STL_Vertices(TargetSTL,iarr(0, 1, NumPoints1 - 1), 1)*...AMirroring;
    AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
    AnyInt NumPoints1 = STL_Size(TargetSTL, 1)[0];
    Mode = VTK_LANDMARK_AFFINE;
  };
  
  //transformation from the scaled segmental ref. frame to flexed bone position ref. frame
  AnyFunTransform3DLin2 Seg2Flexed = {
    AnyString SourceFN = ANYBODY_PATH_BODY+"\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Seg_" + ..Bone + "_R.stl";
    AnyString TargetFN = ANYBODY_PATH_MODELUTILS +"\MovingAxisKnee\STLs\" + ..Bone + "_90_R.stl";
    AnyFileVar SourceSTL = SourceFN;
    AnyFileVar TargetSTL = TargetFN;
    AnyMatrix Points0unscaled = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*...AMirroring;
    Points0 = .TScale2SegFrame(Points0unscaled);
    Points1 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints1 - 1), 1)*...AMirroring;
    AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
    AnyInt NumPoints1 = STL_Size(TargetSTL, 1)[0];
    Mode = VTK_LANDMARK_AFFINE;
  };
  
  //transformation from CT ref. frame to unscaled segmental ref. frame
  AnyFunTransform3DLin2 CT2Seg = {
    AnyString SourceFN = ANYBODY_PATH_BODY+"\LegTLEM\TLEM2.1\CT_RefFrame_STLs\CT_" + ..Bone + "_R.stl";
    AnyString TargetFN = ANYBODY_PATH_BODY+"\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Seg_" + ..Bone + "_R.stl";
    AnyFileVar SourceSTL = SourceFN;
    AnyFileVar TargetSTL = TargetFN;
    AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
    Points0 = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*...AMirroring;
    Points1 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints0 - 1), 1)*...AMirroring;
    Mode = VTK_LANDMARK_RIGIDBODY;
  };  
  
  //transformation from flexed bone position ref. frame to unscaled segmental ref. frame
  AnyFunTransform3DLin2 Flex2Seg = {
    AnyString SourceFN = ANYBODY_PATH_MODELUTILS +"\MovingAxisKnee\STLs\" + ..Bone + "_90_R.stl";
    AnyString TargetFN = ANYBODY_PATH_BODY+"\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Seg_" + ..Bone + "_R.stl";
    AnyFileVar SourceSTL = SourceFN;
    AnyFileVar TargetSTL = TargetFN;
    AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
    Points0 = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*...AMirroring;
    Points1 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints0 - 1), 1)*...AMirroring;
    Mode = VTK_LANDMARK_RIGIDBODY;
  }; 
  
//  #include "GS_TransformationMatrix_Scaling.any"
}; // SubjectSpecificScaling
