 AnyFolder SubjectSpecificScaling = {        
  
  AnyFolder Right = {  
    
    AnyFolder Thigh = {
      
      //Transforms CT bone into scaled segmental reference frame
      AnyFunTransform3DIdentity RegTransform = {
        PreTransforms = {&.CT2Seg, &....BodyModel.Right.Leg.Seg.Thigh.Scale};
      };
      
      //Transform to the segmental ref. frame from the unscaled anatomical frame
      AnyFunTransform3D &TScale2SegFrame = ...BodyModel.Right.Leg.Seg.Thigh.Scale.T0GT0Inv;
      
      //transformation from the scaled segmental ref. frame to CT ref. frame
      AnyFunTransform3DLin2 Seg2CT = {
        AnyFileVar SourceSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Femur_source_R.stl";
        AnyFileVar TargetSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\CT_RefFrame_STLs\Femur_target_R.stl";
        AnyMatrix Points0unscaled = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*0.001;
        Points0 = .TScale2SegFrame(Points0unscaled);
        Points1 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints1 - 1), 1)*0.001;
        AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
        AnyInt NumPoints1 = STL_Size(TargetSTL, 1)[0];
        Mode = VTK_LANDMARK_AFFINE;
      };
      
      //transformation from CT ref. frame to unscaled segmental ref. frame
      AnyFunTransform3DLin2 CT2Seg = {
        AnyFileVar SourceSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Femur_source_R.stl";
        AnyFileVar TargetSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\CT_RefFrame_STLs\Femur_target_R.stl";
        AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
        Points0 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints0 - 1), 1)*0.001;
        Points1 = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*0.001;
        Mode = VTK_LANDMARK_RIGIDBODY;
      };  
    };
    AnyFolder Shank = { 
      
      //Transforms CT bone into scaled segmental reference frame
      AnyFunTransform3DIdentity RegTransform = {
        PreTransforms = {&.CT2Seg, &....BodyModel.Right.Leg.Seg.Shank.Scale};
      };
      
      //Transform to the segmental ref. frame from the unscaled anatomical frame
      AnyFunTransform3D &TScale2SegFrame = ...BodyModel.Right.Leg.Seg.Shank.Scale.T0GT0Inv;
      
      //transformation from the scaled segmental ref. frame to CT ref. frame
      AnyFunTransform3DLin2 Seg2CT = {
        AnyFileVar TargetSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\CT_RefFrame_STLs\Tibia_target_R.stl";
        AnyFileVar SourceSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Tibia_source_R.stl";
        AnyMatrix Points0unscaled = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0 - 1), 1)*0.001;
        Points0 = .TScale2SegFrame(Points0unscaled);
        Points1 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints1 - 1), 1)*0.001;
        AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0];
        AnyInt NumPoints1 = STL_Size(TargetSTL, 1)[0];
        Mode = VTK_LANDMARK_AFFINE;
      };
      
      //transformation from CT ref. frame to unscaled segmental ref. frame
      AnyFunTransform3DLin2 CT2Seg = {
        AnyFileVar TargetSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\CT_RefFrame_STLs\Tibia_target_R.stl";
        AnyFileVar SourceSTL = "..\..\..\Body\AAUHuman\LegTLEM\TLEM2.1\Seg_RefFrame_STLs\Tibia_source_R.stl";
        AnyInt NumPoints0 = STL_Size(SourceSTL, 1)[0]; 
        AnyInt NumPoints1 = STL_Size(TargetSTL, 1)[0];
        Points0 = STL_Vertices(TargetSTL, iarr(0, 1, NumPoints1-1), 1)*0.001; 
        Points1 = STL_Vertices(SourceSTL, iarr(0, 1, NumPoints0-1), 1)*0.001;
        Mode = VTK_LANDMARK_RIGIDBODY;
      }; 
    };
  };
};




