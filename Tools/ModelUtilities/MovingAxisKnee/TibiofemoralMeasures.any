AnyFolder Tibiofemoral_Measures = {     
    //Femur translations relative to tibia
    AnyKinLinear JCS_translation= {  
      Ref = 0;       
      AnyRefFrame &ThighNode = ..Seg.Thigh.KneeJointAnatomicalFrame;
      AnyRefFrame &ShankNode = ..Seg.Shank.KneeJointAnatomicalFrame;      
    };   
    
    AnyKinMeasureLinComb JCS_rotation = {  
      //Tibia rotations relative to femur
      AnyKinRotational rot = {     
        Type = RotAxesAngles;
        Axis1 = z;
        Axis2 = x; 
        Axis3 = y; 
        AnyRefFrame &ThighNode = ...Seg.Thigh.KneeJointAnatomicalFrame;
        AnyRefFrame &ShankNode = ...Seg.Shank.KneeJointAnatomicalFrame; 
      }; 
      Coef = {{1,0,0}, {0,1,0},{0,0,1}}*180/pi;
      OutDim = 3;
    };
    
    //Grood & Suntay 1983 definitions
    AnyFolder GS_measures = {
      AnyVec3 H = ..Seg.Shank.KneeJointAnatomicalFrame.r-..Seg.Thigh.KneeJointAnatomicalFrame.r;
      AnyVec3 e1 = {..Seg.Thigh.KneeJointAnatomicalFrame.Axes[0][2],..Seg.Thigh.KneeJointAnatomicalFrame.Axes[1][2],..Seg.Thigh.KneeJointAnatomicalFrame.Axes[2][2]}; //  femur medial-lateral axis 
      AnyVec3 e3 = {..Seg.Shank.KneeJointAnatomicalFrame.Axes[0][1],..Seg.Shank.KneeJointAnatomicalFrame.Axes[1][1],..Seg.Shank.KneeJointAnatomicalFrame.Axes[2][1]}; //  tibial superior-inferior axis 
      AnyVec3 e2 = cross(e3,e1); //floating axis

      AnyVar GS_q1 = ..Sign*H*e1';  // lateral tibial displacement
      AnyVar GS_q2 = H*e2';  // anterior tibial drawer displacement
      AnyVar GS_q3 = -H*e3'; // joint distraction

      AnyVar GS_FlexExt = -.JCS_rotation.Pos[0]; // multiply by (-) such that flexion values are positive
      AnyVar GS_AbdAdd = (..Sign)*.JCS_rotation.Pos[1];   // (pi/2 + Adduction, right knee  OR pi/2 - Adduction, left knee)
      AnyVar GS_IntExt = (..Sign)*.JCS_rotation.Pos[2];   // (- for right knee / + for left knee) 
      
    }; 
  };
